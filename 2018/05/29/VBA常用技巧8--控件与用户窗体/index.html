<!DOCTYPE html>
<html>
<head><meta name="generator" content="Hexo 3.8.0">
    <meta http-equiv="content-type" content="text/html; charset=utf-8">
    <meta content="width=device-width, initial-scale=1.0, maximum-scale=1.0, user-scalable=0" name="viewport">
    <meta name="keywords" content="hexo, autumn">
    <title>
        TT ❤ LL 印迹
    </title>
    <!-- favicon -->
    
    <link rel="icon" href="https://cdn.jsdelivr.net/gh/frontendsophie/hexo-theme-autumn@1.0.0/source/img/favicon.ico">
    
    <link rel="stylesheet" href="/css/style.css">

    <!-- highlight -->
    <link rel="stylesheet" href="https://cdn.bootcss.com/highlight.js/9.12.0/styles/github-gist.min.css">
    <script src="//cdn.bootcss.com/highlight.js/9.2.0/highlight.min.js"></script>
    <script>
        hljs.initHighlightingOnLoad()
    </script>
    <script src="https://cdn.bootcss.com/jquery/3.3.1/jquery.min.js"></script>
    <link rel="stylesheet" href="https://cdn.jsdelivr.net/gh/frontendsophie/hexo-infinite-scroll@1.0.0/dist/hexo-infinite-scroll.min.css">
    <script src="https://cdn.jsdelivr.net/gh/frontendsophie/hexo-infinite-scroll@1.0.0/dist/hexo-infinite-scroll.min.js"></script>
    <script>
        infiniteScroll()

        // for mobile menu
        $(function () {
            $('.social-button').click(function () {
                if ($('.social-links').hasClass('hide-links')) {
                    $('.social-links').removeClass('hide-links')
                } else {
                    $('.social-links').addClass('hide-links')
                }
            })
        })
    </script>
</head>

    <body style="background: url(https://cdn.jsdelivr.net/gh/frontendsophie/hexo-theme-autumn@1.0.0/source/img/button-bg.png) #f3f3f3">
        <div class="container">
            <header class="header">
    <h1 class="title">
        <a href="/" class="logo">
            TT ❤ LL 印迹
        </a>
    </h1>
    <h2 class="desc">
        
    </h2>

    <nav class="links">
        <button class="social-button">
            menu
        </button>
        <ul class="social-links hide-links">
            
                <li>
                    <a href="https://github.com/FrontendSophie">
                        Github
                    </a>
                </li>
                
                <li>
                    <a href="https://www.linkedin.com/in/frontendsophie/">
                        LinkedIn
                    </a>
                </li>
                
        </ul>
    </nav>
</header>
                <main class="main">
                    <article class="post">
    
    
    <h4 class="post-cat">
        <a href="/categories/生の术/">
            生の术
        </a>
    </h4>
    
    
    <h2 class="post-title">
        VBA常用技巧8--控件与用户窗体
    </h2>
    <ul class="post-date">
        <li>
            2018-05-29
        </li>
        <li>
            绛墨铤
        </li>
    </ul>
    <div class="post-content">
        <h2 id="第8章-控件与用户窗体"><a href="#第8章-控件与用户窗体" class="headerlink" title="第8章     控件与用户窗体"></a>第8章     控件与用户窗体</h2><h3 id="技巧98-限制文本框的输入"><a href="#技巧98-限制文本框的输入" class="headerlink" title="技巧98     限制文本框的输入"></a>技巧98     限制文本框的输入</h3><p>用户在使用文本框输入数据时，往往希望能限制输入数据的类型，比如只能输入数字。但是没有内置的属性能限制在文本框中只能输入数字，只能在文本框的事件过程中使用代码来测试输入的是哪类字符，然后只允许输入数字字符和一个“-”号、一个“.”号，如下面的代码所示。</p>
<pre><code class="vb">Private Sub TextBox1_KeyPress(ByVal KeyANSI As MSForms.ReturnInteger)
    Select Case KeyANSI
     Case Asc(&quot;0&quot;) To Asc(&quot;9&quot;)
        Case Asc(&quot;-&quot;)
            If InStr(1, Me.TextBox1.Text, &quot;-&quot;) &gt; 0 Or _
                Me.TextBox1.SelStart &gt; 0 Then
                KeyANSI = 0
            End If
        Case Asc(&quot;.&quot;)
            If InStr(1, Me.TextBox1.Text, &quot;.&quot;) &gt; 0 Then
                KeyANSI = 0
            End If
        Case Else
            KeyANSI = 0
    End Select
End Sub
代码解析：
文本框的KeyPress事件过程，测试键盘输入的是哪类字符，只允许输入数字字符和一个“-”号、一个“.”号。
KeyPress事件的语法如下：
Private Sub object_KeyPress( ByVal KeyANSI As MSForms.ReturnInteger)
参数Object是必需的，一个有效的对象。
参数KeyANSI是可选的，整数值，代表标准的数字ANSI 键代码。
第2行代码使用Case Else语句测试文本框KeyPress事件的KeyANSI参数值。
第3行代码，如果键盘输入的是0到9之间的数字字符，则允许输入。如果想在文本框中允许其它类型的字符输入，在此句代码中列出允许输入的字符即可。
第4行到第8行代码，如果键盘输入的是“-”号，先使用InStr函数测试文本框中是否已有“-”号，如果InStr函数返回值大于0，说明文本框中已有“-”号。接下来使用文本框的SelStart 属性来测试插入点，如果文本框的SelStart 属性值大于0，说明“-”号的插入点不是第一个。如果以上两个条件中有任何一个成立，将KeyAscii参数值设置为0，使文本框只能在第一位输入一个“-”号。
第9行到第12行代码，如果键盘输入的是“.”号的话，使用InStr函数测试文本框中是否已有“.”号，如果已有“.”号，将KeyAscii参数值设置为0，使文本框只能输入一个“.”号。
第13、14行代码，如果键盘输入的是其他字符则将KeyAscii参数值设置为0，使文本框不能输入其他字符。</code></pre>
<p>经过以上设置文本框只允许输入数字字符和一个“-”号、一个“.”号，但是能输入中文字符。如果希望限制中文字符的输入，可以在文本框的Change事件中进行设置，如下面的代码所示。</p>
<pre><code class="vb">Private Sub TextBox1_Change()
    Dim i As Integer
    Dim s As String
    With TextBox1
        For i = 1 To Len(.Text)
            s = Mid(.Text, i, 1)
            Select Case s
                Case &quot;.&quot;, &quot;-&quot;, &quot;0&quot; To &quot;9&quot;
                Case Else
                    .Text = Replace(.Text, s, &quot;&quot;)
            End Select
        Next
    End With
End Sub
代码解析：
文本框的Change事件，判断输入的字符是否为数字字符和“-”号、“.”号，如果不是则使用Replace函数将文本框中输入的其他字符替换成空白。
第5、6行代码在文本框输入的所有字符中循环。
第8行代码列出允许输入的字符。如果想在文本框中允许其它字符输入，在此句代码中列出即可。
第9、10行代码，如果不是允许输入的字符，使用Replace函数替换成空白。
经过以上的设置，文本框中只能在第一位输入一个“-”号、一个“.”号和“0”到“9”的数字。</code></pre>
<h3 id="技巧99-文本框添加右键快捷菜单"><a href="#技巧99-文本框添加右键快捷菜单" class="headerlink" title="技巧99     文本框添加右键快捷菜单"></a>技巧99     文本框添加右键快捷菜单</h3><p>VBA中的控件没有提供右键快捷菜单，用户可以使用Excel 中的命令栏自已添加右键快捷菜单。<br>步骤1：按&lt;Alt+F11&gt;组合键进入VBE窗口，单击菜单“插入”→“模块”，在其代码窗口输入以下代码：</p>
<pre><code class="vb">Private ActiveTB As MSForms.TextBox
Public Sub CreateShortCutMenu()
    Dim ShortCutMenu As CommandBar
    Dim ShortCutMenuItem As CommandBarButton
    Dim sCaption As Variant
    Dim iFaceId As Variant
    Dim sAction As Variant
    Dim i As Integer
    sCaption = Array(&quot;剪切(&amp;C)&quot;, &quot;复制(&amp;T)&quot;, &quot;贴粘(&amp;P)&quot;, &quot;删除(&amp;D)&quot;)
    iFaceId = Array(21, 19, 22, 1786)
    sAction = Array(&quot;Action_Cut&quot;, &quot;Action_Copy&quot;, &quot;Action_Paste&quot;, &quot;Action_Delete&quot;)
    On Error Resume Next
    Application.CommandBars(&quot;ShortCut&quot;).Delete
    Set ShortCutMenu = Application.CommandBars.Add(&quot;ShortCut&quot;, msoBarPopup)
    With ShortCutMenu
        For i = 0 To 3
            Set ShortCutMenuItem = .Controls.Add(msoControlButton)
            With ShortCutMenuItem
                .Caption = sCaption(i)
                .faceID = Val(iFaceId(i))
                .OnAction = sAction(i)
            End With
        Next
    End With
End Sub
代码解析：
第1行代码，在模块级别中声明变量ActiveTB是用来对应窗体中的文本框所触发的所有事件的变量。
CreateShortCutMenu过程用来创建标题为“ShortCut”的右键快捷菜单，并添加4个菜单项。关于自定义右键快捷菜单请参阅技巧86 。</code></pre>
<pre><code class="vb">Public Sub ShowPopupMenu(txtCtr As MSForms.TextBox)
    Dim Action As Variant
    Set ActiveTB = txtCtr
    With Application.CommandBars(&quot;ShortCut&quot;)
        .Controls(1).Enabled = txtCtr.SelLength &gt; 0
        .Controls(2).Enabled = .Controls(1).Enabled
                .Controls(3).Enabled = txtCtr.CanPaste
        .Controls(4).Enabled = .Controls(1).Enabled
        .ShowPopup
    End With
End Sub
代码解析：
ShowPopupMenu过程根据文本框中字符的选中状态设置右键快捷菜单菜单项的Enabled属性后使用ShowPopup方法显示右键快捷菜单。
第5行代码，如果当前文本框中已有选中的字符则“剪切”按钮有效。
第6行代码，如果当前文本框中已有选中的字符则“复制”按钮有效。
第7行代码，如果剪贴板中包含对象支持的数据。则“贴粘”按钮有效。
第8行代码，如果当前文本框中已有选中的字符则“删除”按钮有效。
第9行代码，显示快捷菜单。</code></pre>
<pre><code class="vb">Public Sub Action_Cut()
    ActiveTB.Cut
End Sub
Public Sub Action_Copy()
    ActiveTB.Copy
End Sub
Public Sub Action_Paste()
    ActiveTB.Paste
End Sub
Public Sub Action_Delete()
    Dim s As String
    With ActiveTB
        s = .SelText
        .Value = Replace(.Value, s, &quot;&quot;)
    End With
End Sub
代码解析：
Action_Cut过程是快捷菜单中单击“剪切”菜单项所运行的过程。使用Cut 方法将当前选中的文本框中的文本删除并移至剪贴板。
Action_Copy过程是快捷菜单中单击“复制”菜单项所运行的过程。使用Copy方法将文本框选中的文本复制到剪贴板上。
Action_Paste过程是快捷菜单中单击“贴粘”菜单项所运行的过程。使用Paste方法把剪贴板上的内容传送到一个文本框中。
Action_Delete过程是快捷菜单中单击“贴粘”菜单项所运行的过程。使用Replace函数将文本框中选中的文本的文本替换成空字符。</code></pre>
<pre><code class="vb">Public Sub DeleteShortCutMenu()
    On Error Resume Next
    Application.CommandBars(&quot;ShortCut&quot;).Delete
End Sub
代码解析：
DeleteShortCutMenu过程删除创建的右键快捷菜单。
步骤2：在VBE窗口中，单击菜单“插入”→“用户窗体”，在窗体上添加两个文本框控件。双击窗体，在其代码窗口中输入下面的代码。</code></pre>
<pre><code class="vb">Private Sub UserForm_Initialize()
    Call CreateShortCutMenu
End Sub
Private Sub TextBox1_MouseUp(ByVal Button As Integer, ByVal Shift As Integer, ByVal X As Single, ByVal Y As Single)
    If Button = 2 Then ShowPopupMenu ActiveControl
End Sub
Private Sub TextBox2_MouseUp(ByVal Button As Integer, ByVal Shift As Integer, ByVal X As Single, ByVal Y As Single)
    If Button = 2 Then ShowPopupMenu ActiveControl
End Sub
Private Sub UserForm_QueryClose(Cancel As Integer, CloseMode As Integer)
    Call DeleteShortCutMenu
End Sub
代码解析：
第1行到第3行代码，窗体的Initialize事件，在窗体初始化时运行CreateShortCutMenu过程创建右键快捷菜单。
第4行到第9行代码，文本框的MouseUp事件，当用户右健单击文本框时运行ShowPopupMenu过程在选中的菜单项上显示右键快捷菜单。
第10行到第12行代码，窗体的QueryClose事件，在关闭窗体时运行DeleteShortCutMenu过程删除右键快捷菜单。
窗体运行后，右键单击文本框显示右键快捷菜单，如图 99 1所示。</code></pre>
<h3 id="技巧100-文本框回车自动输入"><a href="#技巧100-文本框回车自动输入" class="headerlink" title="技巧100     文本框回车自动输入"></a>技巧100     文本框回车自动输入</h3><p>在使用文本框向工作表输入数据时，为了加快输入速度，可以利用文本框的KeyDown事件，回车后自动输入并清空文本框，如下面的代码所示。</p>
<pre><code class="vb">Private Sub TextBox1_KeyDown(ByVal KeyCode As MSForms.ReturnInteger, ByVal Shift As Integer)
    With TextBox1
        If Len(Trim(.Value)) &gt; 0 Then
            If KeyCode = vbKeyReturn Then
                Sheet1.Range(&quot;A65536&quot;).End(xlUp).Offset(1, 0) = .Value
                .Text = &quot;&quot;
            End If
        End If
    End With
End Sub
代码解析：
文本框的KeyDown事件，在输入数据并按&lt;Enter&gt;键后自动将数据录入到工作表A列最后一个非空单元格的下一个单元格中。
KeyDown事件在按下键盘按键时发生，语法如下：
Private Sub object_KeyDown( ByVal KeyCode As MSForms.ReturnInteger, ByVal Shift As fmShiftState)
参数object是必需的，一个有效的对象。
参数KeyCode是必需的，代表被按下的键的键代码。
参数Shift是可选的，Shift、Ctrl 和Alt的状态。
第3行代码，为了防止误输入空白数据，使用Len 函数和Trim 函数检查文本框内是否为有效数据。
第4行代码，根据KeyCode参数值判断是否按下了回车键。如果用户按下了回车键，KeyCode参数返回常数vbKeyReturn。
第5、6行代码，将文本框数据输入到工作表A列的最后一个单元格内，同时清空文本框内容准备下一次输入。</code></pre>
<h3 id="技巧101-自动选择文本框内容"><a href="#技巧101-自动选择文本框内容" class="headerlink" title="技巧101     自动选择文本框内容"></a>技巧101     自动选择文本框内容</h3><p>如果希望光标进入文本框时能自动选择文本框内容，可以在文本框的MouseUp事件中来完成，如下面的代码所示。</p>
<pre><code class="vb">Private Sub TextBox1_MouseUp(ByVal Button As Integer, ByVal Shift As Integer, ByVal X As Single, ByVal Y As Single)
    With TextBox1
        If Button = 2 Then
            .SelStart = 0
            .SelLength = Len(.Text)
        End If
    End With
End Sub
代码解析：
文本框的MouseUp事件，在光标进入文本框释放鼠标右键时自动选择文本框内容。
MouseUp事件在用户释放鼠标按键时发生，语法如下：
Private Sub object_MouseUp( ByVal Button As fmButton, ByVal Shift As fmShiftState, ByVal X As Single, ByVal Y As Single)
参数object是必需的，一个有效的对象。
参数Button是可选的，设置引起该事件的鼠标按键的整数值，如表格 101 1所示。
常数    值    说明
fmButtonLeft    1    按下左键。
fmButtonRight    2    按下右键。
fmButtonMiddle    3    按下中键。
表格 101 1    Button参数值
参数Shift是可选的，Shift、Ctrl 和Alt的状态。
参数X和参数Y是可选的，窗体、框架或页的位置的横坐标与纵坐标。
第3行到第6行代码，如果用户进入文本框释放鼠标右键，设置文本框的SelStart 属性为0，SelLength属性为文本框的全部字符数。
SelStart 属性指定选中文本的起点，语法如下：
object.SelStart [= Long]
参数object是必需的，一个有效的对象。
参数Long是可选的，指定选中文本的起点。
SelLength 属性指定文本框或组合框的文本部分中选中的字符数，语法如下：
object.SelLength [= Long]
参数object是必需的，一个有效的对象。
参数Long是可选的，指定选中的字符数。
运行窗体，当光标进入文本框释放鼠标右键时自动选择文本框内容，如图 101 1所示。</code></pre>
<h3 id="技巧102-设置文本框数据格式"><a href="#技巧102-设置文本框数据格式" class="headerlink" title="技巧102     设置文本框数据格式"></a>技巧102     设置文本框数据格式</h3><p>文本框在用来输入数据时，除了限制输入的数据类型外，还可以设置文本框的数据格式，如下面的代码所示。</p>
<pre><code class="vb">Private Sub TextBox1_Exit(ByVal Cancel As MSForms.ReturnBoolean)
    TextBox1 = Format(TextBox1, &quot;0.00&quot;)
End Sub
Private Sub TextBox2_Exit(ByVal Cancel As MSForms.ReturnBoolean)
    TextBox2 = Format(TextBox2, &quot;0.00&quot;)
End Sub
代码解析：
文本框的Exit事件过程，在文本框输入数据时使用Format函数格式化为两位小数格式。
控件的Exit事件在同一窗体中的一个控件即将把焦点转移到另一个控件之前发生，语法如下：
Private Sub object_Exit( ByVal Cancel As MSForms.ReturnBoolean)
参数Object是必需的，一个有效的对象。
参数Cancel是必需的，事件状态。如果设置为False表示由该控件处理这个事件（默认方式）。设置为True表示由应用程序处理这个事件，并且焦点留在当前控件上。
当文本框在输入完数据失去焦点时使用Format函数格式化自定义数值格式。Format函数语法如下：
Format(expression[, format[, firstdayofweek[, firstweekofyear]]])
参数expression是必需的，任何有效的表达式。
参数format是可选的，有效的命名表达式或用户自定义格式表达式。
参数firstdayofweek是可选的，常数，表示一星期的第一天。
参数firstweekofyear是可选的，常数，表示一年的第一周。
在本例中，将文本框的数据格式化成自定义的两位小数的数值格式，关于Format函数格式化日期和时间等其他数据请参阅VBA中Format函数的帮助。</code></pre>
<pre><code class="vb">Private Sub TextBox1_Change()
    TextBox3 = Format(Val(TextBox1) * Val(TextBox2), &quot;0.00&quot;)
End Sub
Private Sub TextBox2_Change()
    TextBox3 = Format(Val(TextBox1) * Val(TextBox2), &quot;0.00&quot;)
End Sub
代码解析：
文本框的Change事件过程，在两个文本框输入完数据后，使用文本框的Change事件使TextBox3显示其相乘的金额并格式化为两位小数的数据格式。
Change事件在控件的 Value 属性改变时发生，语法如下：
Private Sub object_Change( )
参数object是必需的，一个有效的对象。
Change事件过程可以使显示在控件上的数据同步或一致。在本例中，当TextBox1或TextBox2的数据发生改变时，两者相乘的金额的金额也随之改变并在TextBox3中显示。
因为文本框的数据类型是文本字符串，不能直接进行计算的，所以计算前先使用Val函数转换为数字，才能进行计算。
运行窗体，输入数据后格式化为两位小数的数据格式，如图 102 1所示。

图 102 1    设置文本框的数据格式</code></pre>
<h3 id="技巧103-限制文本框的输入长度"><a href="#技巧103-限制文本框的输入长度" class="headerlink" title="技巧103     限制文本框的输入长度"></a>技巧103     限制文本框的输入长度</h3><p>在使用文本框输入数据时，可能希望限制能输入的字符长度，即只能输入一定长度的字符，超过设置数值就不能输入，这时可以通过设置文本框的MaxLength属性来实现，如下面的代码所示。</p>
<pre><code class="vb">Private Sub Worksheet_Activate()
    Me.TextBox1.MaxLength = 6
End Sub
代码解析：
工作表的激活事件过程，将文本框的MaxLength属性设置为6，使文本框只能输入6个字符，超过6个字符即不能输入。
应用于文本框控件的MaxLength属性规定用户可以在文本框中输入的最多字符数，语法如下：
object.MaxLength [= Long]
参数object是必需的，一个有效的对象。
参数Long是可选的，整数，表示所允许的字符数。
如果将MaxLength属性设置为0，表示只要内存允许则没有限制。</code></pre>
<h3 id="技巧104-将光标返回文本框中"><a href="#技巧104-将光标返回文本框中" class="headerlink" title="技巧104     将光标返回文本框中"></a>技巧104     将光标返回文本框中</h3><p>在用文本框往工作表录入数据时，一般会在录入到工作表前验证输入的数据是否正确，如果错误，则清空文本框内容，提示用户重新输入。但此时光标已经不在文本框中，需要重新选择文本框才能输入。<br>可以在Exit事件中可以设置Cancel参数值使光标停留在当前文本框中，如下面的代码所示。</p>
<pre><code class="vb">Private Sub TextBox1_Exit(ByVal Cancel As MSForms.ReturnBoolean)
    With TextBox1
        If .Text &lt;&gt; &quot;&quot; And Len(Trim(.Text)) &lt;&gt; 15 And Len(Trim(.Text)) &lt;&gt; 18 Then
            .Text = &quot;&quot;
            MsgBox &quot;身份证号码录入错误!&quot;
            Cancel = True
        End If
    End With
End Sub
代码解析：
文本框的Exit事件，在输入身份证号码后即将把焦点转移到录入按钮控件之前检查输入的身份证号码是否正确。
Exit事件在一个控件从同一窗体的另一个控件实际接收到焦点之前发生,语法如下:
Private Sub object_Exit( ByVal Cancel As MSForms.ReturnBoolean)
Cancel参数为事件状态。False表示由该控件处理这个事件（这是默认方式）。True表示由应用程序处理这个事件，并且焦点应当留在当前控件上。
第3行代码，使用Len函数和Trim函数检查输入的身份证号码是否为15位或18位。
第4行到第6行代码，如果输入的身份证号码不正确，清空文本框以便重新输入并提示用户，设置Cancel参数为True使光标停留在文本框中。
在Exit事件中之所以把文本框为空也做为通过验证的条件之一，因为如果不加上“TextBox1.Text &lt;&gt; &quot;&quot;”这一条件，那么在窗体显示后，如果用户取消输入或关闭输入窗体，也会提示输入错误。所以在录入到工作表之前再验证文本框是否为空，如下面的代码所示。</code></pre>
<pre><code class="vb">Private Sub CommandButton1_Click()
    With TextBox1
        If .Text &lt;&gt; &quot;&quot; Then
            Sheet1.Range(&quot;a65536&quot;).End(xlUp).Offset(1, 0) = .Text
            .Text = &quot;&quot;
        Else
            MsgBox &quot;请输入身份证号码!&quot;
        End If
            .SetFocus
    End With
End Sub
代码解析：
输入按钮的Click事件，把文本框数据录入到工作表A列最后一个单元格中并重新选择文本框准备下一次输入。
第3行代码，在输入到工作表前检查文本框是否为空。
第4、5行代码，如果文本框不为空，录入数据到工作表并清空文本框内容。
第7行代码，如果文本框为空，提示用户输入数据。
第8行代码，使用SetFocus方法将光标返回到文本框中以便重新输入。
SetFocus方法将焦点移动到对象的实例中，语法如下 ：
object.SetFocus
参数object.是必需的，一个有效的对象。
运行窗体，在输入框中输入身份证号码后自动验证输入的数据，如果输入数据错误，清空文本框并提示用户重新输入，如图 104 1所示。

图 104 1    提示用户重新输入</code></pre>
<h3 id="技巧105-文本框的自动换行"><a href="#技巧105-文本框的自动换行" class="headerlink" title="技巧105     文本框的自动换行"></a>技巧105     文本框的自动换行</h3><p>在使用使用文本框显示或录入一段很长的文本时，需要将文本框设置成多行显示，否则文本内容只能在一行中显示，示例代码如下：</p>
<pre><code class="vb">Private Sub UserForm_Initialize()
    With TextBox1
        .WordWrap = True
        .MultiLine = True
        .Text = Space(4) &amp; &quot;VBA(Visual Basic for Application)是&quot; _
                &amp; &quot;微软公司为了加强Office软件的二次开发能力而附加&quot; _
                &amp; &quot;于其中的编程语言。VBA的确非常强大，其与VB完全一&quot; _
                &amp; &quot;致的语法结构，高效控制Office对象模型的能力，令无&quot; _
                &amp; &quot;数人为之折腰。利用VBA，几乎可以在Office里面做任何&quot; _
                &amp; &quot;其他程序能做的事情。但是，应该清楚的认识到VBA是依&quot; _
                &amp; &quot;托其宿主─—Excel（或其他Office组件）而存在的，对&quot; _
                &amp; &quot;于Excel用户来讲，VBA只不过是锦上添花的东西，切不可&quot; _
                &amp; &quot;本末倒置，捡了芝麻丢了西瓜，把明明能够利用Excel内置&quot; _
                &amp; &quot;功能完成的任务，硬是搬到VBA里面去做，以为用代码实现&quot; _
                &amp; &quot;就是高人一头的表现。其实，真正的高手，会尽量发挥&quot; _
                &amp; &quot;Excel自身的威力，不到万不得已的时候是不会去&lt;Alt+F11&gt;的。&quot;
    End With
End Sub
代码解析：
窗体的Initialize事件过程，在窗体显示时将文本框设置成多行显示文本。
第3行代码设置文本框的WordWrap属性。WordWrap属性指定一个控件的内容在行末是否自动换行，语法如下：
object.WordWrap [= Boolean]
参数object是必需的，一个有效的对象。
参数Boolean是可选的，控件是否扩展以适应文本的大小，设置为True，文本换行，设置为False，文本不换行。
第4行代码设置文本框的MultiLine属性。MultiLine属性规定控件能否接受和显示多行文本，语法如下：
object.MultiLine [= Boolean]
参数object是必需的，一个有效的对象。
参数Boolean是可选的，控件是否支持多行文本，设置为True，以多行显示文本，设置为False，不多行显示文本。如果将多行文本框的MultiLine属性设置为False，则文本框的所有字符都将合并为一行，包括非打印字符（如，回车和换行）。
对于既支持WordWrap属性又支持MultiLine属性的控件，当MultiLine属性为False时，WordWrap属性被忽略。
运行窗体，文本框显示如图 105 1所示。

图 105 1    文本框自动换行</code></pre>
<h3 id="技巧106-多个文本框数据相加"><a href="#技巧106-多个文本框数据相加" class="headerlink" title="技巧106     多个文本框数据相加"></a>技巧106     多个文本框数据相加</h3><p>在技巧102 中，我们在TextBox1、TextBox2中输入完数据后，利用文本框的Change事件使TextBox3显示其两者相乘的金额，但是如果窗体中有多个文本框，需要在每一个文本框的Change事件中写上相同的重复代码，因此使用类模块可以简化代码。<br>在附件的窗体有七个文本框，其中六个用来输入数据，一个用来显示其他六个文本框相加后的合计数，首先打开VBE，插入一个类模块建立一个类，类模块的名字就是类的名字修改为“cmds”，在类模块中输入下面的代码：<br>Public WithEvents cmd As MSForms.TextBox<br>代码解析：<br>使用Public语句声明变量cmd是用来响应由TextBox对象触发的事件的对象变量。<br>在窗体的Initialize事件中写入下面的代码：</p>
<pre><code class="vb">Dim col As New Collection
Private Sub UserForm_Initialize()
    Dim i As Integer
    Dim myc As cmds
    For i = 1 To 6
        Set myc = New cmds
        Set myc.cmd = Me.Controls(&quot;TextBox&quot; &amp; i)
        col.Add myc
    Next
    Set myc = Nothing
End Sub
代码解析：
第1行代码在模块顶部声明变量col的类型为集合。
第5行到第9行代码，将窗体中的六个文本框赋给col集合。
（关于类模块请参阅论坛中有关的资料。）</code></pre>
<p>在类模块中写入下面的代码：</p>
<pre><code class="vb">Private Sub cmd_Change()
    Dim i As Integer
    Dim Dval As Double
    For i = 1 To 6
        Dval = Dval + Val(UserForm1.Controls(&quot;TextBox&quot; &amp; i))
        UserForm1.TextBox7.Value = Dval
    Next
End Sub
代码解析：
窗体中的六个文本框统一的Change事件，当任何一个文本框中的数据发生变化时，所有文本框相加的合计数显示在最后一个文本框中。
运行窗体在文本框中输入数据结果如图 106 1所示。

图 106 1    多个文本框数据相加</code></pre>
<h3 id="技巧107-控件跟随活动单元格"><a href="#技巧107-控件跟随活动单元格" class="headerlink" title="技巧107     控件跟随活动单元格"></a>技巧107     控件跟随活动单元格</h3><p>在工作表中使用控件时一般都把控件放在工作表的上部，如果工作表中数据较多，当页面滚动到工作表下面的区域时，控件会离开当前可视区域，这时操作起来很不方便。解决方法除了冻结工作表的第一行放置控件的外，还可以使控件出现在选定的单元格位置，如下面的代码所示。</p>
<pre><code class="vb">Private Sub Worksheet_SelectionChange(ByVal Target As Range)
    With Me.CommandButton1
        .Top = Target.Top
        .Left = Target.Left + Target.Width
    End With
End Sub
代码解析：
工作表的SelectionChange事件，使工作表中的按钮控件出现在选定单元格的右边。
第3行代码，设置按钮的Top属性等于选定单元格的Top属性。Top属性设置对象顶端到第一行顶端的距离。
第4行代码，设置按钮的Left属性等于选定单元格的Left属性加上选定单元格的宽度，即按钮出现在选定单元格的右边。Left属性设置对象左边界至 A 列左边界的距离。
当单击工作表区域的任一单元格，按钮出现在单元格的右边，如图 107 1所示。

图 107 1    控件跟随活动单元格</code></pre>
<h3 id="技巧108-高亮显示按钮"><a href="#技巧108-高亮显示按钮" class="headerlink" title="技巧108     高亮显示按钮"></a>技巧108     高亮显示按钮</h3><p>为了达到当鼠标掠过按钮时以高亮和凸起显示按钮的效果，可以在窗体和按钮的MouseMove事件中进行模拟，如下面的代码所示。</p>
<pre><code class="vb">#001  Private Sub CommandButton1_MouseMove(ByVal Button As Integer, ByVal Shift As Integer, ByVal X As Single, ByVal Y As Single)
    With Me.CommandButton1
        .BackColor = &amp;HFFFF00
        .Width = 62
        .Height = 62
        .Top = 69
        .Left = 31
    End With
End Sub
Private Sub UserForm_MouseMove(ByVal Button As Integer, ByVal Shift As Integer, ByVal X As Single, ByVal Y As Single)
    With Me.CommandButton1
        .BackColor = Me.BackColor
        .Width = 60
        .Height = 60
        .Top = 70
        .Left = 32
    End With
End Sub
代码解析：
窗体和按钮的MouseMove事件过程，以高亮和凸起显示按钮。
当用户在窗体中移动鼠标时，分别在窗体和按钮的MouseMove事件设置按钮的BackColor属性值，指定按钮的背景色，当鼠标移动到按钮时以高亮显示，当鼠标移动到窗体时恢复原来的设置。接下来分别设置按钮不同的Width属性、Height属性、Top属性和Left属性值，以模拟按钮凸起的效果。
运行窗体，当鼠标掠过按钮时效果如图 108 1所示。

图 108 1    高亮和凸起显示按钮</code></pre>
<h3 id="技巧109-组合框和列表框添加列表项的方法"><a href="#技巧109-组合框和列表框添加列表项的方法" class="headerlink" title="技巧109     组合框和列表框添加列表项的方法"></a>技巧109     组合框和列表框添加列表项的方法</h3><p>组合框和列表框是Excel中最常用的控件，可以用来显示工作表中的数据。为组合框和列表框添加列表项的方法有多种，下面以列表框为例演示添加列表项的方法。</p>
<h4 id="109-1-使用RowSource属性添加列表项"><a href="#109-1-使用RowSource属性添加列表项" class="headerlink" title="109-1    使用RowSource属性添加列表项"></a>109-1    使用RowSource属性添加列表项</h4><p>使用RowSource属性将列表框直接与工作表上的一个单元格区域相链接，如下面的代码所示。</p>
<pre><code class="vb">Private Sub UserForm_Initialize()
    Dim iRow As Integer
    iRow = Sheet1.Range(&quot;A65536&quot;).End(xlUp).Row
    Me.ListBox1.RowSource = &quot;sheet1!a1:a&quot; &amp; iRow
End Sub
代码解析：
在窗体初始化时使用RowSource属性为列表框添加列表项。
RowSource属性的语法如下：
object.RowSource [= String]
参数object是必需的，一个有效的对象。
参数String是可选的，组合框或列表框列表的来源。
RowSource属性也可以使用单元格地址，第4行代码可以改成下面的代码：
Me.ListBox1.RowSource = Sheet1.Range(&quot;A1:A&quot; &amp; iRow).Address(External:=True)
需要注意的是，如果RowSource属性指定的工作表区域不是活动工作表的话，Address属性的External参数是不可缺的，设置为True表示是外部引用，如果缺省此参数或为False，将不能为列表框添加列表项。
RowSource属性还可以使用命名的单元格区域，如果已把工作表区域命名为“城市”，第4行代码可以改成下面的代码：
Me.ListBox1.RowSource = &quot;城市&quot;</code></pre>
<p>对于工作表中的列表框控件或使用窗体添加的列表框控件不能使用RowSource属性，需要使用ListFillRange属性指定填充列表框的工作表区域，如下面的代码所示。</p>
<pre><code class="vb">Sub ListFillRange()
    Dim iRow As Integer
    iRow = Sheet1.Range(&quot;A65536&quot;).End(xlUp).Row
    Sheet2.ListBox1.ListFillRange = &quot;Sheet1!a1:a&quot; &amp; iRow
    Sheet2.Shapes(&quot;列表框&quot;).ControlFormat.ListFillRange = &quot;Sheet1!a1:a&quot; &amp; iRow
End Sub
代码解析：
ListFillRange过程为工作表中的列表框的填充区域，ListFillRange属性用于指定填充列表框的工作表区域。
第4行代码对于使用窗体添加的列表框控件需要使用ControlFormat属性来返回窗体控件以后才能设置其ListFillRange属性。
109-2    使用List属性添加列表项</code></pre>
<p>使用List属性为列表框添加列表项，如下面的代码所示。</p>
<pre><code class="vb">Private Sub UserForm_Initialize()
    Dim Arr As Variant
    Dim iRow As Integer
    iRow = Sheet1.Range(&quot;A65536&quot;).End(xlUp).Row
    Arr = Sheet1.Range(&quot;A1:A&quot; &amp; iRow)
    Me.ListBox1.List = Arr
End Sub
代码解析：
在窗体初始化时使用List属性为列表框添加列表项。</code></pre>
<p>List属性的语法如下：<br>object.List( row, column ) [= Variant]<br>参数object是必需的，一个有效对象。<br>参数row是必需的，取值范围为 0 到列表条目数减 1 之间的数值。<br>参数column是必需的，取值范围为 0 到总列数减 1 之间的数值。<br>参数Variant是可选的，列表框中指定条目的内容。<br>第6行代码，使用List属性把数组复制到列表框控件上。<br>除了使用数组外，List属性还可以使用命名的单元格区域，如果已把工作表区域命名为“城市”，可以改成下面的代码：</p>
<pre><code class="vb">Private Sub UserForm_Initialize()
    Me.ComboBox1.List = Range(&quot;城市&quot;).Value
End Sub</code></pre>
<p>对于工作表中使用窗体添加的列表框控件使用List属性添加列表项，如下面的代码所示。</p>
<pre><code class="vb">Sub List()
    Dim Arr As Variant
    Dim iRow As Integer
    Dim myObj As Object
    iRow = Sheet1.Range(&quot;A65536&quot;).End(xlUp).Row
    Arr = Sheet1.Range(&quot;A1:A&quot; &amp; iRow)
    Set myObj = Sheet2.Shapes(&quot;列表框&quot;).ControlFormat
    myObj.List = Arr
End Sub
代码解析：
List过程设置列表框的List性，用于指定填充列表框的工作表区域。
109-3    使用AddItem方法添加列表项</code></pre>
<p>使用AddItem方法添加列表项，对于单列的列表框，在列表中添加一项。对于多列的列表框，在列表中添加一行，如下面的代码所示。</p>
<pre><code class="vb">Private Sub UserForm_Initialize()
    Dim iRow As Integer
    Dim i As Integer
    iRow = Sheet1.Range(&quot;A65536&quot;).End(xlUp).Row
    For i = 1 To iRow
        Me.ListBox1.AddItem (Sheet1.Cells(i, 1))
    Next
End Sub
代码解析：
在窗体初始化时使用AddItem方法为列表框添加列表项。
AddItem方法的语法如下：
object.AddItem [ item [, varIndex]]
参数object是必需的，一个有效的对象。
参数item是可选的，指定要添加的项或行。第一个项或行的编号为 0；第二个项或行的编号为 1，依此类推。
参数varIndex是可选的，指定新的项或行在对象中的位置。
如果提供一个有效的varIndex的值，AddItem方法就把项或行放在列表中的那个位置。如果忽略 varIndex，此方法就把项或行添加在列表的末尾。对于多列列表框或者组合框，AddItem 方法插入一个完整的行，为控件的每一列都插入一项。为了给第一列后面的项赋值，可用List或Column属性来规定项的行和列。</code></pre>
<p>对于工作表中使用窗体添加的列表框控件使用AddItem方法添加列表项，如下面的代码所示。</p>
<pre><code class="vb">Sub AddItem()
    Dim iRow As Integer
    Dim i As Integer
    iRow = Sheet1.Range(&quot;A65536&quot;).End(xlUp).Row
    With Sheet2.Shapes(&quot;列表框&quot;).ControlFormat
        .RemoveAllItems
        For i = 1 To iRow
            .AddItem Sheet1.Cells(i, 1)
        Next
    End With
End Sub
代码解析：
AddItem过程设置使用AddItem方法添加为工作表中使用窗体控件添加的列表框添加列表项。
其中第5行代码使用ControlFormat属性来返回窗体控件，第6行代码使用RemoveAllItems方法删除窗体控件中的列表框的所有数据项，如果控件是ActiveX 列表框则需要使用Clear方法。</code></pre>
<h3 id="技巧110-去除列表框数据源的重复值和空格"><a href="#技巧110-去除列表框数据源的重复值和空格" class="headerlink" title="技巧110     去除列表框数据源的重复值和空格"></a>技巧110     去除列表框数据源的重复值和空格</h3><p>列表框的数据源引用工作表的数据时，如果工作表数据有重复值和空格，列表框也会出现重复值和空格，如图 110 1所示。</p>
<p>图 110 1    列表框中的重复值和空格<br>为了在窗体显示时去除列表框的重复值和空格，可以使用Add方法，如下面的代码所示。</p>
<pre><code class="vb">Private Sub UserForm_Initialize()
    On Error Resume Next
    Dim Col As New Collection
    Dim rng As Range, arr
    Dim i As Integer
    For Each rng In Range(&quot;A1:A&quot; &amp; [a65536].End(xlUp).Row)
        If Trim(rng) &lt;&gt; &quot;&quot; Then
            Col.Add rng, key:=CStr(rng)
        End If
    Next
    ReDim arr(1 To Col.Count)
    For i = 1 To Col.Count
        arr(i) = Col(i)
    Next
    Me.ListBox1.List = arr
End Sub
代码解析：
窗体的初始化事件，去除列表框引用工作表数据中的重复值和空格。
第2行代码，错误处理语句，忽略错误。
第3行到第5行代码，声明变量类型。
第6行到第9行代码代码，在列表框引用的工作表数据中循环，把工作表数据源中的空格去除后使用Add方法添加到变量Col中。Add方法添加一个成员到Collection 对象，语法如下：
object.Add item, key, before, after
参数object是必需的，一个有效的对象。
参数Item是必需的，任意类型的表达式，指定要添加到集合中的成员。
参数Key是可选的，唯一字符串表达式，指定可以使用的键字符串，代替位置索引来访问集合中的成员。
如果指定的key和集合中现有成员的key发生重复，则会导致错误发生。所以在第2行代码中使用错误处理语句，忽略错误，继续执行下一句代码，这样就将数据源中的重复值去除。
参数before是可选的，指定集合中的相对位置。在集合中将添加的成员放置在before参数识别的成员之前。如果参数是数值表达式，则before必须是介于 1 和集合Count属性值之间的值。如果参数是字符串表达式，则当添加一个被引用的成员到集合时，before 必须对应于指定的key值。可以指定before位置或after位置，但不能同时指定这两个位置。
参数after是可选的，指定集合中的相对位置。在集合中将添加的成员放置在After参数识别的成员之后。如果参数是数值表达式，则after必须是介于 1 和集合Count属性值之间的值；如果参数是字符串表达式，则当添加一个被引用的成员到集合时，after 必须对应于指定的key值。可以指定before位置或after位置，但不能同时指定这两个位置。
第10行到第14行代码，重新定义数组arr大小，把Col中数据赋给数组。
第15行代码，把数组arr复制到列表框中。
运行窗体，窗体中的列表框引用去除重复值和空格后的工作表数据，如图 110 2所示。

图 110 2    去除重复值和空格的列表框</code></pre>
<h3 id="技巧111-移动列表框条目"><a href="#技巧111-移动列表框条目" class="headerlink" title="技巧111     移动列表框条目"></a>技巧111     移动列表框条目</h3><p>将列表框中的条目进行上下移动，如下面的代码所示。</p>
<pre><code class="vb">Dim Intlist As Integer
Dim Strlist As String
Private Sub CommandButton1_Click() 
    With Me.ListBox1
        Intlist = .ListIndex
        Select Case Intlist
            Case -1
                MsgBox &quot;请选择一行后再移动!&quot;
            Case 0
                MsgBox &quot;已经是最上一行了!&quot;
            Case Is &gt; 0
                Strlist = .List(Intlist)
                .List(Intlist) = .List(Intlist - 1)
                .List(Intlist - 1) = Strlist
                .ListIndex = Intlist - 1
        End Select
    End With
End Sub
Private Sub CommandButton2_Click() 
    With ListBox1
        Intlist = .ListIndex
        Select Case Intlist
            Case -1
                MsgBox &quot;请选择一行后再移动!&quot;
            Case .ListCount - 1
                MsgBox &quot;已经是最下一行了!&quot;
            Case Is &lt; .ListCount - 1
                Strlist = .List(Intlist)
                .List(Intlist) = .List(Intlist + 1)
                .List(Intlist + 1) = Strlist
                .ListIndex = Intlist + 1
        End Select
    End With
End Sub
代码解析：
第1、2行代码在模块顶部声明两个变量分别用于保存列表框当前选中行的索引和内容。
第3行到第18行代码，将列表框当前选中行的内容上移一行的代码。其中第5行代码使用变量Intlist保存列表框当前选中行的索引号，第6行代码判断索引号,，第7、8行代码如果变量Intlist值为-1 ，说明当前没有选中的行，显示一个消息框进行提示。第9、10行代码变量Intlist值为0 ，说明当前选中的行已是第一行了。
列表框的ListIndex属性指定当前选中的列表框或组合框条目，语法如下：
object.ListIndex [= Variant]
参数object是必需的，一个有效的对象。
参数Variant是可选的，控件中当前被选的条目。
第11行到第15行代码将当前选中的行向下移动一行，其中第12行代码将当前选中的行的内容赋给变量Strlist，第13行代码将当前选中行的内容更改为下面一行的内容，第14行代码将当前选中行的下面一行的内容更改为变量Strlist保存的内容，第15行代码将选中行向下移动一行，这样就将当前选中的行向下移动了一行。
第19行到第34行代码将当前选中的行向上移动一行。</code></pre>
<p>将移动后的列表框条目保存到工作表中的代码如下：</p>
<pre><code class="vb">Private Sub CommandButton3_Click()
    Dim i As Integer
    For i = 1 To ListBox1.ListCount
        Sheet1.Cells(i + 1, 1) = ListBox1.List(i - 1)
    Next
End Sub
代码解析：
窗体中“保存”按钮的单击过程，将移动后的列表框条目保存到工作表。
第3行到第5行代码使用For...Next 语句循环遍历列表框所有条目，将List属性返回的列表框的列表条目写入到工作表中。List属性返回或设置列表框或组合框的列表条目数，语法请参阅技巧109-2。
运行窗体效果如所示。</code></pre>
<h3 id="技巧112-允许多项选择的列表框"><a href="#技巧112-允许多项选择的列表框" class="headerlink" title="技巧112     允许多项选择的列表框"></a>技巧112     允许多项选择的列表框</h3><p>一般情况下在显示的列表框中用户只能选择一个列表项，而经过简单的设置，列表框条目前可以显示选项按钮，允许进行多项选择，如下面的代码所示。</p>
<pre><code class="vb">Private Sub UserForm_Initialize()
    Dim arr As Variant
    arr = Array(&quot;经理室&quot;, &quot;办公室&quot;, &quot;生技科&quot;, &quot;财务科&quot;, &quot;营业部&quot;, &quot;制水车间&quot;, &quot;污水厂&quot;, &quot;安装公司&quot;, &quot;其他&quot;)
    With Me.ListBox1
        .List = arr
        .ListStyle = 1
        .MultiSelect = 1
    End With
End Sub
代码解析：
窗体的Initialize事件过程，在窗体初始化时对列表框进行设置。
其中第5行代码使用List属性为列表框添加列表项，请参阅技巧109-2。
第6行代码将列表框的ListStyle属性设置为1（fmListStyleOption），显示用于多重选择列表的复选框，ListStyle属性规定列表框或组合框中的列表的外观，语法如下：
object.ListStyle [= fmListStyle]
参数object是必需的，一个有效的对象。
参数fmListStyle是可选的，列表的可视风格，设置值如表格 112 1所示。
常量    值    说明
fmListStylePlain    0    外观与常规的列表框相似，条目的背景为高亮
fmListStyleOption    1    显示选项按钮，或显示用于多重选择列表的复选框（默认）。当用户选定组中的条目时，与该条目相关的选项按钮即被选中，而该组其他条目的选项按钮则被取消选择
表格 112 1    fmListStyle设置值
ListStyle 属性可用来改变列表框或组合框的可视外观。通过一种不同于 fmListStylePlain 的设置，可以将任意控件的内容作为一组单独项目演示，每个项目都包含一个可视记号用以表示它是否被选中。
如果控件支持单一选择（MultiSelect属性被设置为mMultiSelectSingle），则可按下组中的一个按钮。如果控件支持多重选择，则可以按下组中两个或更多的按钮。
第7行代码将MultiSelect属性设置为1（fmMultiSelectMulti），允许列表框进行多项选择，MultiSelect属性表示对象是否允许多项选择，语法如下：
object.MultiSelect [= fmMultiSelect]
参数object是必需的，一个有效的对象。
参数fmMultiSelect是可选的，控件所用的选择方式，设置值如表格 112 2所示。
常量    值    说明
fmMultiSelectSingle    0    只可选择一个条目（默认）
fmMultiSelectMulti    1    按空格键或单击鼠标以选定列表中一个条目或取消选定
fmMultiSelectExtended    2    按 Shift 并单击鼠标，或按 Shift 的同时按一个方向键，将所选条目由前一项扩展到当前项。按 Ctrl 的同时单击鼠标可选定或取消选定
表格 112 2    fmMultiSelect设置值
经过以上设置，列表框显示时可以进行多项选择并且条目前都有一个选项按钮用以表示它是否被选中，如图 112 1所示。

图 112 1    允许多项选择的列表框
如果将列表框的ListStyle属性设置为0则与常规的列表框相似。
如果将列表框的MultiSelect属性设置0则列表框只能进行单项选择，如图 112 2所示。

图 112 2    允许单项选择的列表框</code></pre>
<p>通过列表框的Selected属性值可以判断列表框中条目的选定状态，如下面的代码所示。</p>
<pre><code class="vb">Private Sub CommandButton1_Click()
    Dim i As Integer
    Dim s As String
    For i = 0 To ListBox1.ListCount - 1
        If ListBox1.Selected(i) = True Then
            s = s &amp; ListBox1.List(i) &amp; Chr(13)
        End If
    Next
    If s &lt;&gt; &quot;&quot; Then
        MsgBox &quot;你选择了：&quot; &amp; Chr(13) &amp; s
    Else
        MsgBox &quot;请最少选择一个部门!&quot;
    End If
End Sub
代码解析：
按钮的单击过程，将列表框中选中的条目使用消息框显示出来。
第4行到第8行代码使用For...Next 语句循环遍历列表框所有条目，通过返回的Selected属性值判断列表框中条目的选定状态，如果处于选中状态，第6行代码将列表框选中条目的值赋给字符串变量s。
Selected属性判断列表框中条目的选定状态，语法如下：
object.Selected( index ) [= Boolean]
参数object是必需的，一个有效的对象。
参数index是必需的，整数，取值范围是0到列表中的条目数减1之间的数值。
参数Boolean是必需的，判断一个条目是否被选中。
第9行到第13行代码使用消息框显示列表框中选中的条目。
运行窗体结果如图 112 3所示。

图 112 3    允许多项选择的列表框</code></pre>
<h3 id="技巧113-多列组合框和列表框的设置"><a href="#技巧113-多列组合框和列表框的设置" class="headerlink" title="技巧113     多列组合框和列表框的设置"></a>技巧113     多列组合框和列表框的设置</h3><h4 id="113-1-多列组合框和列表框添加列表项"><a href="#113-1-多列组合框和列表框添加列表项" class="headerlink" title="113-1    多列组合框和列表框添加列表项"></a>113-1    多列组合框和列表框添加列表项</h4><p>如果组合框和列表框是多列的话，除了使用技巧109 的方法外，还需要设置控件的其他属性，如下面的代码所示。</p>
<pre><code class="vb">Private Sub UserForm_Initialize()
    Dim iRow As Integer
    Dim Arr As Variant
    iRow = Sheet1.Range(&quot;A65536&quot;).End(xlUp).Row
    Arr = Sheet1.Range(&quot;A1:G&quot; &amp; iRow)
    With Me.ListBox1
        .ColumnCount = 7
        .ColumnWidths = &quot;45,45,45,45,45,30,45&quot;
        .BoundColumn = 1
        .Column = Application.WorksheetFunction.Transpose(Arr)
    End With
End Sub
代码解析：
在窗体初始化时为多列列表框添加列表项。
第4行代码，设置列表框显示的列数。ColumnCount 属性指定列表框或组合框的显示列数，语法如下：
object.ColumnCount [= Long]
参数object是必需的，一个有效的对象。
参数Long是可选的，指定需显示的列数。
如果将ColumnCount设为 -1，将显示所有列。
第8行代码，设置列表框各列的宽度。ColumnWidths 属性指定多列的组合框或列表框中的各列的宽度，语法如下：
object.ColumnWidths [= String]
参数object是必需的，一个有效的对象。
参数String是可选的，以磅为单位设置列的宽度。
如将ColumnWidths 属性设为 -1 或空，则将控件宽度等分，给予列表中的各列。设为 0 则隐藏该列，大于 0 的数值则是该列的精确宽度值。若要指定另一种不同的度量单位，设置时必须包括该度量单位。
第9行代码，设置多列列表框中的第一列为数据的来源。BoundColumn 属性标识多列组合框或列表框中的数据的来源，语法如下：
object.BoundColumn [= Variant]
参数object是必需的，一个有效的对象。
参数Variant是可选的，标识选择 BoundColumn 属性值的方法，设置值如表格 113 1所示：
值    说明
0    将 ListIndex 属性的值赋予控件。
1 或者大于 1    将指定列中的值赋予控件。当采用此属性时，列从 1 开始计数（默认值）。
表格 113 1    Variant 的设置值
当选择了多列列表框的一行时，BoundColumn 属性标识出将该行的哪一条目作为控件的值存储。BoundColumn属性设为 0，将所选行的行号赋予控件，作为控件的值。如果BoundColumn属性设为1 或者大于 1，则将指定列中的值赋予控件。
第10行代码，设置多列列表框中列表的来源。在设置列表来源时除了可以使用技巧109 所介绍的方法外，还可以使用Column属性指定列表框中的一个或多个条目，Column属性语法如下：
object.Column( column, row ) [= Variant]
参数object是必需的，一个有效对象。
参数column是可选的，取值范围为0到总列数减1之间的数值。
参数row是可选的，取值范围为0到总行数减1之间的数值。
参数Variant是可选的，指定欲加载到列表框的一个值、一列值或一个二维数组。
注意   当从一个二维数组中复制数据时，使用Column属性将转置控件中数组的内容，所以在加载时需使用Transpose函数对数组进行转置。
多列列表框设置完成后效果如图 113 1所示。

图 113 1    多列列表框</code></pre>
<h4 id="113-2-多列列表框写入工作表"><a href="#113-2-多列列表框写入工作表" class="headerlink" title="113-2    多列列表框写入工作表"></a>113-2    多列列表框写入工作表</h4><p>在把多列列表框的写入工作表中时，只能把BoundColumn属性所指定列中的值写入工作表中，不能把选中的整行内容写入到工作表中。如果需要把多列列表框中选中行的整行内容写入工作表中，可以使用循环语句将列表框各列的写入工作表，如下面的代码所示。</p>
<pre><code class="vb">Private Sub UserForm_Initialize()
    Dim iRow As Integer
    iRow = Sheet2.Range(&quot;A65536&quot;).End(xlUp).Row
    With Me.ListBox1
        .ColumnCount = 7
        .ColumnWidths = &quot;45,45,45,45,45,30,45&quot;
        .BoundColumn = 1
        .ColumnHeads = True
        .RowSource = Sheet2.Range(&quot;A2:G&quot; &amp; iRow).Address(External:=True)
    End With
End Sub
Private Sub ListBox1_Click()
    Dim iRow As Integer
    Dim i As Byte
    iRow = Sheet1.Range(&quot;A65536&quot;).End(xlUp).Row + 1
    For i = 1 To ListBox1.ColumnCount
        Sheet1.Cells(iRow, i) = ListBox1.Column(i - 1)
    Next
End Sub
代码解析：
第1行到第11行代码窗体的Initialize事件过程，在窗体初始化时为多列列表框添加列表项，请参阅技巧113-1。
第8行代码，设置多列列表框中的第一行为列标题行。ColumnHeads 属性显示列表框、组合框及接受列题注的对象中的列标题行，语法如下：
object.ColumnHeads [= Boolean]
参数object是必需的，一个有效的对象。
参数Boolean是可选的，指定是否显示列标题。
将ColumnHeads 属性设置为True，多列列表框的第一行显示为列标题，默认值为False，不显示列标题。
需要注意的是，当数据项中的第一行作为列标题时，则不可选中该行。
第9行代码，使用RowSource属性设置多列列表框中列表的来源。关于RowSource属性请参阅技巧109-1。
注意 如果已将多列列表框中列表项来源的第一行设置为列标题，在设置RowSource属性时应从列表项来源的第二行开始设置。
第12行到第19行代码列表框的Click事件，单击多列列表框时把选中行的整行内容写入工作表中。其中第17行代码，使用循环语句将多列列表框选中行的各列的值写入工作表对应的单元格中。关于Column属性请参阅技巧113-1，在本例中没有指定row参数，所以是把当前选中行的内容写入工作表。
运行窗体后，单击列表框将选中的整行内容写入工作表中，如图 113 2所示。

图 113 2    多列列表框选中的整行内容写入工作表</code></pre>
<h3 id="技巧114-输入时逐步提示信息"><a href="#技巧114-输入时逐步提示信息" class="headerlink" title="技巧114     输入时逐步提示信息"></a>技巧114     输入时逐步提示信息</h3><p>用户在录入数据时，比如在工作表中输入产品名称，除了希望有所有产品名称的下拉列表供选择外，更希望能逐步给出提示信息。比如在输入一两个字符后把符合条件的数据筛选出来供选择，最好是中英文、拼音首字母、大小写能混合查询，如输入“LJ”或“六角”后所有以“六角”开头的产品名称都筛选到列表中供选择，这将大大提高录入速度和正确率。<br>为了达到这一目的，首先在工作簿需要有如图 114 1所示的基础数据表。</p>
<p>图 114 1    基础数据表<br>基础数据表中A列保存不重复的产品名称，为了能用中英文、拼音首字母、大小写混合查询，要把产品名称转换成小写的拼音首字母保存在B列。<br>步骤1：在VBE窗口单击菜单“插入”→“模块”，在代码窗口写入下面的代码。</p>
<pre><code class="vb">Public Function LChin(Str As String) As Variant
    On Error Resume Next
    Str = StrConv(Str, vbNarrow)
    If Asc(Str) &gt; 0 Or Err.Number = 1004 Then LChin = &quot;&quot;
    LChin = WorksheetFunction.VLookup(Str, [{&quot;吖&quot;,&quot;a&quot;;&quot;八&quot;,&quot;b&quot;;&quot;嚓&quot;,&quot;c&quot;;&quot;咑&quot;,&quot;d&quot;;&quot;鵽&quot;,&quot;e&quot;;&quot;发&quot;,&quot;f&quot;;&quot;猤&quot;,&quot;g&quot;;&quot;铪&quot;,&quot;h&quot;;&quot;夻&quot;,&quot;j&quot;;&quot;咔&quot;,&quot;k&quot;;&quot;垃&quot;,&quot;l&quot;;&quot;嘸&quot;,&quot;m&quot;;&quot;旀&quot;,&quot;n&quot;;&quot;噢&quot;,&quot;o&quot;;&quot;妑&quot;,&quot;p&quot;;&quot;七&quot;,&quot;q&quot;;&quot;囕&quot;,&quot;r&quot;;&quot;仨&quot;,&quot;s&quot;;&quot;他&quot;,&quot;t&quot;;&quot;屲&quot;,&quot;w&quot;;&quot;夕&quot;,&quot;x&quot;;&quot;丫&quot;,&quot;y&quot;;&quot;帀&quot;,&quot;z&quot;}], 2)
#006  End Function
代码解析：
自定义LChin函数，该函数把中文字符转换为拼音首字母。
步骤2：在VBE窗口双击Sheet2表，在代码窗口写入下面的代码。</code></pre>
<pre><code class="vb">Private Sub Worksheet_Change(ByVal Target As Range)
    Dim i As Integer
    Dim myStr As String
    With Target
        If .Column &lt;&gt; 1 Or .Count &gt; 1 Then Exit Sub
        If WorksheetFunction.CountIf(Sheet2.Range(&quot;A:A&quot;), .Value) &gt; 1 Then
            .Value = &quot;&quot;
            MsgBox &quot;不能输入重复的产品名称!&quot;, 64
            Exit Sub
        End If
        For i = 1 To Len(.Value)
            If Asc(Mid$(.Value, i, 1)) &gt; 255 Or Asc(Mid$(.Value, i, 1)) &lt; 0 Then
                myStr = myStr &amp; LChin(Mid$(.Value, i, 1))
            Else
                myStr = myStr &amp; LCase(Mid$(.Value, i, 1))
            End If
        Next
        .Offset(, 1).Value = myStr
    End With
End Sub
代码解析：
工作表的Change事件，当A列输入不重复的产品名称后，转换成小写的字母保存在B列的单元格中，便于以后的查询。
第11行代码，设置事件触发的条件，只有在A列输入产品名称后才触发Change事件。
第12行到第16行代码，使用工作表CountIf函数检查输入的产品名称是否重复。
第17行到第23行代码，字符的转换过程。首先检查是否是中文字符，如果是使用自定义函数LChin转换成小写拼音首字母。如果是大写的英文字母使用LCase函数转换成小写字母。
第24行代码，将转换后的字符保存到B列。
步骤3：基础数据表完成后，在工作表“录入表”中添加一个文本框控件和一个列表框控件。在VBE窗口中双击Sheet1表，写入下面的代码。</code></pre>
<pre><code class="vb">Private Sub Worksheet_SelectionChange(ByVal Target As Range)
    Dim i As Integer
    If Target.Count = 1 Then
        If Target.Column = 1 And Target.Row &gt; 1 Then
            With Me.TextBox1
                .Visible = True
                .Top = Target.Top
                .Left = Target.Left
                .Width = Target.Width
                .Height = Target.Height
                .Activate
            End With
            With Me.ListBox1
                .Visible = True
                .Top = Target.Top
                .Left = Target.Left + Target.Width
                .Width = Target.Width
                .Height = Target.Height * 5
                For i = 2 To Sheet2.Range(&quot;A65536&quot;).End(xlUp).Row
                    .AddItem Sheet2.Cells(i, 1).Value
                Next
            End With
        Else
            Me.ListBox1.Clear
            Me.TextBox1 = &quot;&quot;
            Me.ListBox1.Visible = False
            Me.TextBox1.Visible = False
        End If
    End If
End Sub
代码解析：
工作表的SelectionChange事件，当用户选定工作表A列第2行以下的单个单元格时，设置文本框和列表框的Visible为True，使它们成为可见的，并设置其外观，同时给列表框加载列表项。当用户选定其他列的单元格时隐藏文本框和列表框控件。
步骤4：在设计模式下双击文本框，在代码窗口写入下面的代码。</code></pre>
<pre><code class="vb">Private Sub TextBox1_KeyUp(ByVal KeyCode As MSForms.ReturnInteger, ByVal Shift As Integer)
    Dim i As Integer
    Dim Language As Boolean
    Dim myStr As String
    Me.ListBox1.Clear
    With Me.TextBox1
        For i = 1 To Len(.Value)
            If Asc(Mid$(.Value, i, 1)) &gt; 255 Or Asc(Mid$(.Value, i, 1)) &lt; 0 Then
                Language = True
                myStr = myStr &amp; Mid$(.Value, i, 1)
            Else
                myStr = myStr &amp; LCase(Mid$(.Value, i, 1))
            End If
        Next
    End With
    With Sheet2
        For i = 2 To .Range(&quot;A65536&quot;).End(xlUp).Row
            If Language = True Then
                If Left(.Cells(i, 1).Value, Len(myStr)) = myStr Then
                    Me.ListBox1.AddItem .Cells(i, 1).Value
                End If
            Else
                If Left(.Cells(i, 2).Value, Len(myStr)) = myStr Then
                    Me.ListBox1.AddItem .Cells(i, 1).Value
                End If
            End If
        Next
    End With
End Sub
代码解析：
文本框的KeyUp事件，在文本框输入查询条件时筛选符合条件的数据加载到列表框。
第3行代码，声明变量Language为Boolean数据类型，在下面的代码中使用Language的值判断输入的是否为中文。
第5行代码，使用Clear方法删除列表框所有的列表项，语法如下：
object.Clear
参数object是必需的，一个有效的对象。
注意 如果列表框绑定了数据，Clear方法将会失败。
第6行到第15行代码，判断文本框输入的是否为中文字符。如果是中文字符，将变量Language赋值为True，并把文本框中的字符赋给变量myStr。如果是英文字符则转换成小写字母后赋变量myStr。
第16行到第29行代码，如果变量Language的值为True，在基础数据表的A列中使用Left函数查找与文本框字符相符的单元格并加载到列表框，否则就在B列查找。
步骤5：在设计模式下双击文本框，在代码窗口写入下面的代码。</code></pre>
<pre><code class="vb">Private Sub TextBox1_KeyDown(ByVal KeyCode As MSForms.ReturnInteger, ByVal Shift As Integer)
    If KeyCode = vbKeyReturn Then
        Sheet1.ListBox1.Activate
    End If
End Sub
代码解析：
文本框的KeyDown事件，当用户在文本框中输入完成，列表框中已显示所需的内容后按回车键后选择列表框。
步骤6：在设计模式下双击列表框，在代码窗口写入下面的代码</code></pre>
<pre><code class="vb">Private Sub ListBox1_GotFocus()
    On Error Resume Next
    ListBox1.ListIndex = 0
End Sub
代码解析：
列表框的GotFocus事件，当用户在文本框中输入完成按回车键后，选定列表框中第1个条目，方便用户进行下一步操作。</code></pre>
<pre><code class="vb">Private Sub ListBox1_KeyDown(ByVal KeyCode As MSForms.ReturnInteger, ByVal Shift As Integer)
    If KeyCode = vbKeyReturn Then
        ActiveCell.Value = ListBox1.Value
        Me.ListBox1.Clear
        Me.TextBox1 = &quot;&quot;
        Me.ListBox1.Visible = False
        Me.TextBox1.Visible = False
    End If
End Sub
代码解析：
列表框的KeyDown事件，当用户在列表框中按下回车后将列表框选中的条目写入到活动工作表的单元格中，同时清空文本框和列表框内容后隐藏，准备下一次录入。</code></pre>
<pre><code class="vb">Private Sub ListBox1_DblClick(ByVal Cancel As MSForms.ReturnBoolean)
    ActiveCell.Value = ListBox1.Value
    Me.ListBox1.Clear
    Me.TextBox1 = &quot;&quot;
    Me.ListBox1.Visible = False
    Me.TextBox1.Visible = False
End Sub
代码解析：
列表框的DblClick事件，当用户双击列表框的列表项时，把列表框数据赋给活动单元格，同时清空文本框和列表框内容后隐藏，准备下一次录入。
以上设置完成后，在“录入”工作表的A列选定单元格后，显示一个文本框和一个列表框，在文本框中输入查询条件后列表框显示符合查询条件的所有内容供用户选择，如图 114 2所示。

图 114 2    输入时逐步提示信息</code></pre>
<h3 id="技巧115-二级组合框"><a href="#技巧115-二级组合框" class="headerlink" title="技巧115     二级组合框"></a>技巧115     二级组合框</h3><p>在使用多个组合框输入数据时，往往希望后一个组合框中的条目能根据前一个组合框的内容有所不同，如示例中第二个选择城市的组合框根据第一个选择省份的组合框所选择的不同省份加载不同的县市名称，示例代码如下：</p>
<pre><code class="vb">Private Sub UserForm_Initialize()
    Dim col As New Collection
    Dim arr As Variant
    Dim rng As Range
    Dim i As Integer
    On Error Resume Next
    For Each rng In Range(&quot;A2:A&quot; &amp; Sheet1.Range(&quot;A65536&quot;).End(xlUp).Row)
        If rng &lt;&gt; &quot;&quot; Then col.Add rng, CStr(rng)
    Next
    ReDim arr(1 To col.Count)
    For i = 1 To col.Count
        arr(i) = col(i)
    Next
    ComboBox1.List = arr
    ComboBox1.ListIndex = 0
    CommandButton1.Accelerator = &quot;D&quot;
End Sub
Private Sub ComboBox1_Change()
    Dim myAddress As String
    Dim rng As Range
    Dim mymsg As Integer
    ComboBox2.Clear
    With Sheet1.Range(&quot;A:A&quot;)
        Set rng = .Find(What:=ComboBox1.Text)
            If Not rng Is Nothing Then
                myAddress = rng.Address
                Do
                    ComboBox2.AddItem rng.Offset(, 1)
                    Set rng = .FindNext(rng)
                Loop While Not rng Is Nothing And rng.Address &lt;&gt; myAddress
            End If
        End With
        ComboBox2.ListIndex = 0
End Sub
代码解析：
第1行到第17行代码窗体的Initialize事件过程，在窗体显示时将工作表A列中的省份名称去除重复后加载到组合框中。
其中第7行到第13行代码把工作表A列中的省份名称使用Add方法去除重复后加载到组合框中，应用于Collection 对象的Add方法添加一个成员对象，请参阅技巧110 。
第15行代码设置组合框的ListIndex属性为0，选中组合框的第一行条目。ListIndex属性指定当前选中的列表框或组合框条目，语法如下：
object.ListIndex [= Variant]
参数Variant是可选的，控件中当前被选的条目。
ListIndex 属性包含列表中被选行的索引，取值范围为 -1 到列表总行数减 1（即ListCount - 1）之间的数值。当用户没有选中行时，ListIndex 返回 -1。列表中第一行的 ListIndex值是0，第二行的ListIndex 值是1，依此类推。
第16行代码设置窗体中按钮的Accelerator属性值。Accelerator属性设置或检索控件的加速键，语法如下：
object.Accelerator [= String]
参数String是可选的，用作加速键的字符。
先按住 Alt 再紧接着按加速键，会将焦点定位到该对象上，并初始化与该对象关联的一个或多个事件，也就是说窗体显示后按Alt+D组合键将会执行“关闭”按钮中的代码关闭窗体。
第18行到第34行代码ComboBox1的Change事件过程，使用Find方法将所有属于ComboBox1所选择的省份的县市加载到ComboBox2中。关于Find方法请参阅技巧5-1。
窗体运行后效果如图 115 1所示。

图 115 1    二级组合框</code></pre>
<h3 id="技巧116-使用DTP控件输入日期"><a href="#技巧116-使用DTP控件输入日期" class="headerlink" title="技巧116     使用DTP控件输入日期"></a>技巧116     使用DTP控件输入日期</h3><p>在工作表中输入日期可以使用日期时间控件（Microsoft Date and Time Picker Control 6.0，简称DTP控件）。<br>在工作表中单击菜单“视图”→“工具栏”→“控件工具箱”，选择“其他控件”中的DTP控件如图 116 1所示，在工作表中添加一个DTP控件。</p>
<p>图 116 1    选择DTP控件<br>在设计模式下双击DTP控件写入下面的代码：</p>
<pre><code class="vb">Private Sub Worksheet_SelectionChange(ByVal Target As Range)
    With Me.DTPicker1
        If Target.Count = 1 And Target.Column = 2 And (Not Target.Row = 1) Or Target.MergeCells Then
            .Visible = True
            .Top = Selection.Top
            .Left = Selection.Left
            .Height = Selection.Height
            .Width = Selection.Width
            If Target.Cells(1, 1) &lt;&gt; &quot;&quot; Then
                .Value = Target.Cells(1, 1).Value
            Else
                .Value = Date
            End If
        Else
            .Visible = False
        End If
    End With
End Sub
Private Sub DTPicker1_CloseUp()
    ActiveCell.Value = Me.DTPicker1.Value
    Me.DTPicker1.Visible = False
End Sub
Private Sub Worksheet_Change(ByVal Target As Range)
    If Target.Count = 1 And Target.Column = 2 Or Target.MergeCells Then
        If Target.Cells(1, 1).Value = &quot;&quot; Then
            DTPicker1.Visible = False
        End If
    End If
End Sub
代码解析：
第1行到第18行代码工作表的SelectionChange事件，当选择工作表的B列第2行以下的单个单元格时显示日期控件供用户选择日期。
其中第3行代码设置显示日期控件的触发条件。只有当用户选择B列第2行以下单元格且只能选择单个单元格时才显示日期控件，因为本例B列中存在合并单元格，所以需要加上Or Target.MergeCells这个条件，否则单击合并单元格不显示日期控件。
第4行到第8行代码显示日期控件并设置日期控件的大小等于所选单元格的大小。
第9行到第13行代码，如果单元格已经输入了日期，将单元格中的日期赋给日期控件，否则将当前日期赋给日期控件。因为本例B列中存在合并单元格，而合并区域的值在该区域左上角的单元格中指定，所以用Target.Cells(1, 1) 指定合并单元格的值，否则代码会出错。
第15行代码如果选择的是其他列则隐藏日期控件。
第19行到第22行代码日期控件的CloseUp事件，将日期控件的值赋给活动单元格后隐藏日期控件。
第23行到第29行代码工作表的Change事件，如果删除了B列单元格的日期则隐藏日期控件。
当用户选择B列单元格时效果如图 116 2所示。

图 116 2    使用DTP控件输入日期</code></pre>
<h3 id="技巧117-使用RefEdit控件选择区域"><a href="#技巧117-使用RefEdit控件选择区域" class="headerlink" title="技巧117     使用RefEdit控件选择区域"></a>技巧117     使用RefEdit控件选择区域</h3><p>在技巧76-2中介绍了如何使用InputBox方法获得所选单元格区域的地址，而使用RefEdit控件获得单元格区域的地址比使用InputBox方法更加方便，可以单击RefEdit控件中的按钮以折叠用户窗体，选定区域后再单击按钮展开用户窗体，示例代码如下：</p>
<pre><code class="vb">Private Sub CommandButton1_Click()
    Dim Rng As Range
    On Error GoTo line
    Set Rng = Range(RefEdit1.Value)
        Rng.Interior.ColorIndex = 15
        Unload UserForm1
        Exit Sub
line:
    MsgBox &quot;你选择的是非单元格区域!&quot;
End Sub
代码解析：
用户窗体中按钮的单击事件过程，改变用户使用RefEdit控件所选择的单元格区域内部的颜色。
第3行代码，错误处理语句。因为如果用户输入或选定了错误的单元格区域地址，将显示一错误信息，如图 117 1所示，所以必需使用On Error GoTo语句来绕过错误。

图 117 1    提示运行错误
第4行代码，使用Set语句将用户选择的单元格区域赋给变量rng。
第5行代码，改变用户所选单元格区域内部的颜色。
注意 不能在无模式用户窗体中使用RefEdit控件。
窗体运行后，当用户在工作表中选择一个单元格区域后改变所选单元格区域内部的颜色，如图 117 2所示。

图 117 2    使用RefEdit控件获得区域地址</code></pre>
<h3 id="技巧118-如何注册控件"><a href="#技巧118-如何注册控件" class="headerlink" title="技巧118     如何注册控件"></a>技巧118     如何注册控件</h3><p>Excel文件中如果有ActiveX控件如日期时间控件（Microsoft Date and Time Picker Control 6.0，简称DTP控件），在有些电脑上运行时会出现“无法装载这个对象，因为它不适于这台计算机”的提示，如图 118 1所示。文件中的控件丢失，无法正常使用。</p>
<p>图 118 1    无法装载对象提示<br>这是因为DTP控件没有注册引起的，解决办法是在能运行该控件的电脑中复制DTP控件的文件到目标电脑中进行注册。在VBE窗口中右键单击“工具箱”，选择“附加控件”，在“附加控件”对话框中选择DTP控件，对话框底部会显示控件的名称和文件所在的路径，如图 118 2所示。</p>
<p>图 118 2    OCX文件名称和路径<br>DTP控件的文件名为MSCOMCT2.OCX，在C盘的Windows\system32文件夹中，把该文件复制到目标电脑C盘的Windows\system32文件夹中，单击“开始”→“运行”，在“运行”对话框中键入“regsvr32 C:\Windows\system32\MSCOMCT2.OCX”，注册成功后会出现如图 118 3所示的对话框，DTP控件即能正常使用。</p>
<p>图 118 3    注册成功提示<br>在Excel中可以使用程序代码进行自动注册，代码如下：</p>
<pre><code class="vb">Sub regsvrs()
    Dim SouFile As String
    Dim DesFile As String
    On Error Resume Next
    SouFile = ThisWorkbook.Path &amp; &quot;\MSCOMCT2.OCX&quot;
    DesFile = &quot;c:\Windows\system32\MSCOMCT2.OCX&quot;
    FileCopy SouFile, DesFile
    Shell &quot;regsvr32 /s&quot; &amp; DesFile
    MsgBox &quot;DTP控件已成功注册，现在可以使用了!&quot;
End Sub
代码解析：
Regsvrs过程将保存在同一目录中的MSCOMCT2.OCX文件复制到电脑的文件夹中，使用Shell函数注册DTP控件。
第4行代码，错误处理语句，用于忽略复制文件时可能出现的错误。因为如果电脑文件夹中已存在MSCOMCT2.OCX文件，使用FileCopy方法复制时会发生错误，如图 118 4所示。

图 118 4    复制文件错误提示
第7行代码，使用FileCopy方法复制MSCOMCT2.OCX文件到电脑中。
FileCopy方法的语法如下：
FileCopy source, destination
参数Source是必需的，字符串表达式，用来表示要被复制的文件名。
参数destination是必需的，字符串表达式，用来指定要复制的目的文件名。
第8行代码，使用Shell函数注册DTP控件。
Shell函数执行一个可执行文件，语法如下：
Shell(pathname[,windowstyle])
参数pathname是必需的，要执行的程序名，以及任何必需的参数或命令行变量，可能还包括目录或文件夹，以及驱动器。
参数windowstyle是可选的，表示在程序运行时窗口的样式。windowstyle参数值如表格 118 1所示。</code></pre>
<p>常量    值    描述<br>vbHide    0    窗口被隐藏，且焦点会移到隐式窗口。常数vbHide在Macintosh平台不可用。<br>VbNormalFocus    1    窗口具有焦点，且会还原到它原来的大小和位置。<br>VbMinimizedFocus    2    窗口会以一个具有焦点的图标来显示。<br>VbMaximizedFocus    3    窗口是一个具有焦点的最大化窗口。<br>VbNormalNoFocus    4    窗口会被还原到最近使用的大小和位置，而当前活动的窗口仍然保持活动。<br>VbMinimizedNoFocus    6    窗口会以一个图标来显示。而当前活动的的窗口仍然保持活动。<br>表格 118 1    windowstyle参数值<br>运行程序前应确保在工作簿同一目录中存在MSCOMCT2.OCX文件。此代码相当于在“运行”对话框中键入“regsvr32 C:\ Windows\system32\MSCOMCT2.OCX”后进行注册，只是在“REGSVR32”后加上了s参数，使注册成功后不会出现如图 118 3所示的对话框。<br>可以使用程序代码卸载该控件，代码如下：</p>
<pre><code class="vb">Sub regsvru()
    Shell &quot;REGSVR32 /u &quot; &amp; ThisWorkbook.Path &amp; &quot;\MSCOMCT2.OCX&quot;
End Sub
代码解析：
Regsvru过程使用Shell函数注册DTP控件，在pathname参数“REGSVR32”后加上u参数，对DTP控件进行反注册。</code></pre>
<h3 id="技巧119-遍历控件的方法"><a href="#技巧119-遍历控件的方法" class="headerlink" title="技巧119     遍历控件的方法"></a>技巧119     遍历控件的方法</h3><p>如果窗体或工作表中的控件很多，在写代码时，如果是相同的代码，可以使用循环语句遍历控件，无需每个控件都写相同的代码，以减少代码量。</p>
<h4 id="119-1-使用名称中的变量遍历控件"><a href="#119-1-使用名称中的变量遍历控件" class="headerlink" title="119-1    使用名称中的变量遍历控件"></a>119-1    使用名称中的变量遍历控件</h4><p>如果控件使用系统缺省名称，如“TextBox1”、“TextBox2”，前面是固定的字符串，后面是序号的，可以使用For…Next 语句循环遍历控件。<br>对于窗体中的控件，如下面的代码所示。</p>
<pre><code class="vb">Private Sub CommandButton1_Click()
    Dim i As Integer
    For i = 1 To 3
        Me.Controls(&quot;TextBox&quot; &amp; i) = &quot;&quot;
    Next
End Sub
代码解析：
窗体按钮的单击事件，一次性清空窗体中三个文本框的内容。
第4行代码，将窗体中三个文本框名称中的最后一个序号设成变量，在文本框中循环并清空其内容。
对于工作表中的控件，如下面的代码所示。</code></pre>
<pre><code class="vb">    Dim i As Integer
    For i = 1 To 4
        Me.OLEObjects(&quot;TextBox&quot; &amp; i).Object.Text = &quot;&quot;
    Next
End Sub
代码解析：
工作表中按钮的单击事件，在工作表中的三个文本框中循环，清空文本框的内容。
第4行代码，将工作表中四个文本框名称中的最后一个序号设成变量，使用OLEObjects方法在工作表中的文本框中循环。
OLEObjects方法返回图表或工作表上单个OLE对象（OLEObject）或所有OLE对象的集合（OLEObjects集合）的对象，语法如下：
expression.OLEObjects(Index)
参数expression是必需的，返回一个Chart 对象或Worksheet 对象。
参数Index 是可选的，OLE对象的名称或编号。
注意 控件的名称是指控件在属性窗口中的名称，如图 119 1所示。如果控件的名称没有规律不适用此方法。</code></pre>
<h4 id="119-2-使用对象类型遍历控件"><a href="#119-2-使用对象类型遍历控件" class="headerlink" title="119-2    使用对象类型遍历控件"></a>119-2    使用对象类型遍历控件</h4><p>如果控件的名称没有规律，可以使用For Each…Next 语句循环遍历所有控件，使用TypeName函数返回控件的对象类型，根据控件的对象类型进行相应的操作。<br>对于窗体中的控件，如下面的代码所示。</p>
<pre><code class="vb">Private Sub CommandButton1_Click()
    Dim Ctr As Control
    For Each Ctr In Me.Controls
        If TypeName(Ctr) = &quot;TextBox&quot; Then
            Ctr = &quot;&quot;
        End If
    Next
End Sub
代码解析：
按钮的单击事件，遍历所有控件并把所有文本框的内容清空。
第2行代码，声明变量类型。
第3行代码，使用For Each...Next 语句遍历窗体所有控件。
第4行代码，使用TypeName 函数返回变量的对象类型。
TypeName 函数返回一个字符串，提供有关变量的信息，语法如下：
TypeName(varname)
参数varname是必需的，它包含用户定义类型变量之外的任何变量。
如果变量Ctr是文本框控件，无论该文本框的名称是否已经被修改，TypeName(Ctr)都会返回“TextBox”字符串。
对于工作表中的控件，则使用下面的代码。</code></pre>
<pre><code class="vb">Private Sub CommandButton1_Click()
    Dim Obj As OLEObject
    For Each Obj In Me.OLEObjects
        If TypeName(Obj.Object) = &quot;TextBox&quot; Then
            Obj.Object.Text = &quot;&quot;
        End If
    Next
End Sub</code></pre>
<h4 id="119-3-使用程序标识符遍历控件"><a href="#119-3-使用程序标识符遍历控件" class="headerlink" title="119-3    使用程序标识符遍历控件"></a>119-3    使用程序标识符遍历控件</h4><p>工作表中的ActiveX控件还可以根据控件的程序标识符找到相应的控件，如下面的代码所示。</p>
<pre><code class="vb">Private Sub CommandButton1_Click()
    Dim Obj As OLEObject
    For Each Obj In Me.OLEObjects
        If Obj.progID = &quot;Forms.TextBox.1&quot; Then
            Obj.Object.Text = &quot;&quot;
        End If
    Next
End Sub
代码解析：
工作表中按钮的单击事件，遍历工作表中的所有控件并把工作表中所有文本框的内容清空。
第2行代码，声明变量类型。
第3行代码，使用For Each...Next 语句遍历工作表中的所有控件。
第4行代码，使用控件的ProgId 属性返回控件的程序标识符。
ProgId 属性返回控件的程序标识符，语法如下：
expression.ProgId
参数expression是必需的，一个有效的对象。</code></pre>
<p>ActiveX 控件的程序标识符如表格 119 1所示。<br>控件名称    标识符<br>复选框    Forms.CheckBox.1<br>组合框    Forms.ComboBox.1<br>命令按钮    Forms.CommandButton.1<br>框架    Forms.Frame.1<br>图像    Forms.Image.1<br>标签    Forms.Label.1<br>列表框    Forms.ListBox.1<br>多页    Forms.ListBox.1<br>选项按钮    Forms.OptionButton.1<br>滚动条    Forms.ScrollBar.1<br>旋转按钮    Forms.SpinButton.1<br>TabStrip    Forms.TabStrip.1<br>文字框    Forms.TextBox.1<br>切换按钮    Forms.ToggleButton.1<br>表格 119 1    ActiveX 控件的程序标识符<br>文本框控件返回的程序标识符是“Forms.TextBox.1”，此返回值并不受文本框控件名称的影响，所以根据工作表中控件的程序标识符可以找出全部文本框控件。</p>
<h4 id="119-4-使用名称中的变量遍历图形"><a href="#119-4-使用名称中的变量遍历图形" class="headerlink" title="119-4    使用名称中的变量遍历图形"></a>119-4    使用名称中的变量遍历图形</h4><p>如果工作表中有多个图形，可以根据名称的序号使用For…Next 语句遍历图形，如下面的代码所示。</p>
<pre><code class="vb">Private Sub CommandButton1_Click()
    Dim i As Integer
    For i = 1 To 3
        Me.Shapes(&quot;文本框 &quot; &amp; i).TextFrame.Characters.Text = &quot;TextBox&quot; &amp; i
    Next
End Sub
代码解析：
工作表中按钮的单击事件，在工作表中的三个图形文本框中依次写入“TextBox1”、“TextBox2”和“TextBox3”字符串。
第3行到第5行代码，使用Shapes属性在工作表上的三个图形文本框中循环。
Shapes属性返回Shapes对象，代表工作表或图形工作表上的所有图形，可以使用Shapes(index)（其中index是图形的名称或索引号）返回单个的Shape对象。
返回单个的Shape对象后使用Characters 方法向图形文本框中添加字符。Characters 方法的语法如下：
expression.Characters(Start, Length)
参数expression是必需的，返回一个指定文本框内Characters对象的表达式。
参数Start是可选的，表示将要返回的第一个字符。如果此参数设置为 1 或被忽略，则Characters方法会返回以第一个字符为起始字符的字符区域。
参数Length是可选的，表示要返回的字符个数。如果此参数被忽略，则Characters 方法会返回该字符串的剩余部分。
119-5    使用FormControlType属性遍历图形</code></pre>
<p>如果工作表中的是窗体控件，可以使用For Each…Next语句遍历工作表中图形并根据其FormControlType属性返回特定的窗体控件，如下面的代码所示。</p>
<pre><code class="vb">Private Sub CommandButton2_Click()
    Dim myShape As Shape
    For Each myShape In Sheet4.Shapes
        If myShape.Type = msoFormControl Then
            If myShape.FormControlType = xlCheckBox Then
                myShape.ControlFormat.Value = 1
            End If
        End If
    Next
End Sub
代码解析：
工作表中按钮的单击事件，清除工作表中所有的复选框。
第2行代码声明变量myShape为图形对象。
第3行代码使用For Each...Next语句遍历工作表中的图形。
第4行代码根据图形的Type属性判断图形是否为窗体控件。应用于Shape对象的Type属性返回或设置图形类型，窗体控件返回常量msoFormControl。
第5行代码根据控件的FormControlType属性判断窗体控件是否为复选框控件。FormControlType属性返回窗体控件的类型，可以为表格 119 2所示的XlFormControl常量之一。</code></pre>
<p>常量    值    控件类型<br>xlButtonControl    0    按钮<br>xlCheckBox    1    复选框<br>xlDropDown    2    组合框<br>xlGroupBox    4    分组框<br>xlLabel    5    标签<br>xlListBox    6    列表框<br>xlOptionButton    7    选项按钮<br>xlScrollBar    8    滚动条<br>xlSpinner    9    微调项<br>表格 119 2    XlFormControl常量<br>第6行代码使用ControlFormat属性返回工作表中的复选框，并将其他Value属性设置为1选中复选框，如果需要取消复选框只需将Value属性设置为-4146。</p>
<h3 id="技巧120-使微调框最小变动量小于"><a href="#技巧120-使微调框最小变动量小于" class="headerlink" title="技巧120     使微调框最小变动量小于"></a>技巧120     使微调框最小变动量小于</h3><p>在用微调框调节数值时，默认的变动量只能设置成整数。为了使微调框的变动量小于1，如每次的变动量为0.01，需要在代码中做必要的设置，如下面的代码所示。</p>
<pre><code class="vb">Private Sub UserForm_Initialize()
    With Me.SpinButton1
        .Max = 10000
        .Min = -10000
        .SmallChange = 1
        .Value = 0
        Me.TextBox1 = Format(.Value, &quot;0.00&quot;)
    End With
End Sub
Private Sub SpinButton1_Change()
    Me.TextBox1 = Format(Me.SpinButton1 * 0.01, &quot;0.00&quot;)
End Sub
代码解析：
使用微调框调节文本框的数值，每次的变动量为0.01。
第1行代码到第9行代码，窗体的初始化事件，在窗体显示时对微调框控件进行必要的设置。
第3、4行代码，设置微调框控件的Max、Min 属性。Max、Min 属性规定滚动条或数值调节钮的 Value 属性可接收的最大值和最小值，语法如下：
object.Max [= Long]
object.Min [= Long]
参数object是必需的，一个有效的对象。
参数Long是可选的，指定Value属性的最大设置值或最小设置值。
第5行代码，设置微调框控件的SmallChange属性为1。SmallChange属性设定当用户单击滚动条或数值调节钮中的滚动箭头时发生的变动量，语法如下：
object.SmallChange [= Long]
参数object是必需的，一个有效的对象。
参数Long是可选的，设定Value属性的变动量。
SmallChange属性只能设置为整数。
第6行代码，设置窗体显示时微调框控件的Value属性为0。
第7行代码，使用Format函数将将文本框的初始值格式化为“0.00”。关于Format函数请参阅技巧102 。
第11行代码，微调框控件的Change事件，在微调框控件的Value属性发生变动时，将变动量乘0.01后赋给文本框，使文本框的变动量每次为0.01。
窗体运行后效果如图 120 1所示。

图 120 1    微调框变动量小于1</code></pre>
<h3 id="技巧121-不打印工作表中的控件"><a href="#技巧121-不打印工作表中的控件" class="headerlink" title="技巧121     不打印工作表中的控件"></a>技巧121     不打印工作表中的控件</h3><p>在打印工作表时，如果工作表中有控件，会把控件也一起打印出来，从而影响打印出来的工作表的美观。经过简单的设置能使工作表中的控件不被打印出来。</p>
<h4 id="121-1-设置控件格式"><a href="#121-1-设置控件格式" class="headerlink" title="121-1    设置控件格式"></a>121-1    设置控件格式</h4><p>如果工作表中的是窗体控件，设置时右键单击控件，在显示的右键快捷菜单中选择“设置控件格式”，在“设置控件格式”选项卡中选择“属性”页面，使“打印对象”前的复选框为空白状态，如图 121 1所示。</p>
<p>图 121 1    窗体控件<br>如果工作表中的控件是ActiveX控件，那么需要在设计模式下右键单击控件，在显示的右键快捷菜单中选择“设置控件格式”，在“设置控件格式”选项卡中选择“属性”页面，使“打印对象”前的复选框为空白状态，如图 121 2所示。</p>
<p>图 121 2    ActiveX控件</p>
<h4 id="121-2-设置控件的printobjcet属性"><a href="#121-2-设置控件的printobjcet属性" class="headerlink" title="121-2    设置控件的printobjcet属性"></a>121-2    设置控件的printobjcet属性</h4><p>如果工作表中的控件是ActiveX控件，使用除了使用技巧121-1的方法外，还可以在设计模式下右键单击控件，选择“属性”，设置控件的printobjcet属性为False。如图 121 3所示。</p>
<p>图 121 3    设置控件printobjcet属性</p>
<h3 id="技巧122-在框架中使用滚动条"><a href="#技巧122-在框架中使用滚动条" class="headerlink" title="技巧122     在框架中使用滚动条"></a>技巧122     在框架中使用滚动条</h3><p>如果需要在窗体中显示较多的内容，比如使用标签显示一段很长的文本内容，而又不希望窗体很大的话，可以在窗体中使用框架放置标签，设置框架可滚动区域的高度，使标签可以进行上下移动以查看全部区域。<br>在VBE窗口中单击菜单“插入”→“用户窗体”，在窗体中添加一个框架控件，在框架中添加一个标签控件。根据需要显示的内容调整好标签的大小，再将框架和窗体调整为合适的大小。<br>在VBE中双击窗体，写入下面的代码。</p>
<pre><code class="vb">Private Sub UserForm_Initialize()
    Dim sLab As String
    sLab = Space(4) &amp; &quot;欢迎来到ExcelHome技术论坛，全球最领先的Excel技术论坛之一。&quot; &amp; vbLf _
        &amp; Space(4) &amp; &quot;在这里，我们讨论Microsoft Office系列产品的应用技术，重点讨论Microsoft Excel。本论坛从属于Excel Home这一全球最大的华语Excel技术门户，目前是个人、非营利性质的网站学习平台。各行各业的Excel使用者都活跃在此，各种形式的学习资源也汇聚于在此，所以，只要您愿意花时间，并使用正确的方法，我们有理由相信您的绝大部分应用问题和学习愿望都在这里被满足。无数已经取得了非凡进步的人，也可以证明这一点。&quot; &amp; vbLf _
        &amp; Space(4) &amp; &quot;Let’s do it better!这是Excel Home的口号，我们的宗旨是帮助大家解决在使用Office软件中的问题，提升自己的应用技能。&quot; &amp; vbLf _
        &amp; Space(4) &amp; &quot;鉴于许多人在此之前没有正确使用网络学习资源的经验，或者对Excel Home的行为规则缺乏了解，我们特别准备了这样一篇文章，送给每一位有志与我们一起成长的朋友。本文将重点叙述学习方法和论坛的规则，对于如何使用论坛的各项功能，请阅读论坛的帮助系统（http://club.excelhome.net/boardhelp.asp ）&quot;
    Label1.Caption = sLab
    With Frame1
        .ScrollBars = 2
        .ScrollHeight = Label1.Height
    End With
End Sub
代码解析：
窗体的初始化事件，在窗体加载时使用标签显示文本内容。
第3行到第6行代码，变量sLab为要显示的文本，使用Space函数在每段的首字前插入4个空格，使首字缩进。在需要换行的地方插入常数vbLf进行换行。
第9行代码，设置框架的ScrollBars属性为显示垂直滚动条。ScrollBars属性指定一个控件、窗体或页面是否有垂直或水平滚动条，或两者都有，语法如下：
object.ScrollBars [= fmScrollBars]
参数object是必需的，一个有效的对象。
参数fmScrollBars是可选的，滚动条的显示位置，设置值如表格 122 1所示。
常量    值    说明
fmScrollBarsNone    0    不显示滚动条（默认）。
fmScrollBarsHorizontal    1    显示水平滚动条。
fmScrollBarsVertical    2    显示垂直滚动条。
fmScrollBarsBoth    3    垂直和水平滚动条都显示。
表格 122 1    ScrollBars属性设置值
第10行代码，设置框架的ScrollHeight属性为标签的高度。ScrollHeight属性指定通过移动控件、窗体或页面中的滚动条，可以查看的全部区域的高度，语法如下：
object.ScrollHeight [= Single]
参数object是必需的，一个有效的对象。
参数Single是可选的，可滚动区域的高度。
如果框架具有水平滚动条，可以设置框架的ScrollWidth属性来设置可以查看的全部区域的宽度。
运行窗体，使用标签显示文本内容，可通过框架的滚动条查看全部内容，如图 122 1所示。</code></pre>
<h3 id="技巧123-使用多页控件"><a href="#技巧123-使用多页控件" class="headerlink" title="技巧123     使用多页控件"></a>技巧123     使用多页控件</h3><p>在处理可以划分为不同类别的大量信息时可以使用多页控件。例如，在示例中，多页控件的第一页用于显示欢迎信息，另三页显示其他信息。利用多页控件能够将相关信息组织在一起显示出来，同时又能够随时访问整条记录。<br>多页控件中的每个页面都是一个窗体，含有自己的控件，并且可以有唯一的布局。一般情况下，多页控件中的页面都有标签，以便让用户选择单个页面。<br>在窗体中使用多页控件时，往往希望窗体显示时能显示特定的页面，比如每次打开窗体时先显示第一页的欢迎信息，除了在VBE中选择多页控件的第一页后保存外，还可以通过设置多页控件的Value属性来实现，如下面的代码所示。</p>
<pre><code class="vb">Private Sub UserForm_Initialize()
    MultiPage1.Value = 0
End Sub
代码解析：
窗体的Initialize事件，在窗体显示时选择多页控件的第一页。
控件的Value属性定义某给定的控件的状态或内容，对于多页控件标识当前激活页。
Value属性是多页控件的默认属性，该属性返回当前活动页面的索引编号（位于多页控件的Pages集合中），零 ( 0 ) 表示是第一页，最大值比总页数少一。</code></pre>
<p>多页控件的默认事件是Change事件，示例中使用消息框显示当前活动页面的Caption属性，代码如下：</p>
<pre><code class="vb">Private Sub MultiPage1_Change()
    If MultiPage1.SelectedItem.Index &gt; 0 Then
        MsgBox &quot;欢迎来到&quot; &amp; MultiPage1.SelectedItem.Caption &amp; &quot;版块!&quot;
    End If
End Sub
代码解析：
MultiPage1_Change过程根据当前活动页面是否是第一页，如果不是则使用消息框显示当前活动页面的Caption属性。
应用于Page对象的Index属性指Pages集合中Page对象的位置，语法如下：
object.Index [= Integer]
参数object是必需的，一个有效对象。
参数Integer是可选的，当前选定的Page对象的索引。
Index 属性指定了标签出现的顺序，改变Index属性的值将改变多页控件中页面的顺序，第一页的索引值是0，第二页的索引值是 1，依此类推。
应用于多页控件的SelectedItem属性返回当前选中的Page对象，SelectedItem属性是只读的，用SelectedItem属性可对当前选中的Page对象进行可编程控制。
运行窗体，多页控件显示第一页的欢迎信息，当选择其他页面时显示提示信息，如图 123 1所示。

图 123 1    使用多页控件</code></pre>
<h3 id="技巧124-标签文字垂直居中对齐"><a href="#技巧124-标签文字垂直居中对齐" class="headerlink" title="技巧124     标签文字垂直居中对齐"></a>技巧124     标签文字垂直居中对齐</h3><p>在使用标签控件为其他控件作题注时，只能设置题注文字在水平方向的对齐方式，不能设置为垂直居中。要达到题注文字垂直居中的效果，可以使用两个标签控件组合来完成。<br>步骤1，在窗体中添加一个标签控件Label1，将Caption属性设置为空，再设置需要的背景颜色及边框颜色。<br>步骤2，添加一个标签控件Label2，将Caption属性设置为需要的标题；AutoSize属性设置为True，BackStyle属性设置为0，TextAligh属性设置为fmTextAlignCenter，其它属性不改变。<br>AutoSize属性规定对象是否自动调整大小以显示其完整的内容，语法如下：<br>object.AutoSize [= Boolean]<br>参数object是必需的，一个有效对象。<br>参数Boolean是可选的，是否自动调整大小。设置值为True控件可自动调整大小以显示其完整的内容，设置为False控件尺寸保持不变。如果内容超出了控件的区域，内容将被剪裁（默认）。<br>BorderStyle属性指定控件或窗体的边框类型，语法如下：<br>object.BorderStyle [= fmBorderStyle]<br>参数object是必需的，一个有效对象。<br>参数fmBorderStyle是可选的，指定边框类型，设置值如表格 124 1所示。<br>常量    值    描述<br>fmBackStyleTransparent    0    背景为透明<br>fmBackStyleOpaque    1    背景为不透明（默认值）<br>表格 124 1    fmBorderStyle设置值<br>TextAligh属性定义控件中文本的对齐方式，语法如下：<br>object.TextAlign [= fmTextAlign]<br>参数object是必需的，一个有效对象。<br>参数fmTextAlign是可选的，控件中文本的对齐方式，设置值如表格 124 2所示。<br>常量    值    描述<br>fmTextAlignLeft    1    将所显示文本的第一个字符与控件显示或编辑区的左边界对齐（默认值）。<br>fmTextAlignCenter    2    在控件的显示或编辑区中，使文本中央对齐<br>fmTextAlignRight    3    将所显示文本的最后一个字符与控件显示或编辑区的右边界对齐。<br>表格 124 2    fmTextAlign设置值<br>步骤3，同时选中两个标签控件，在右键弹出菜单中选择“统一尺寸”→“宽度相同”，再右击选择“对齐”→“左对齐”，重新右键“对齐”→“中间对齐”。<br>步骤4，最后同时选中两个标签控件，在右键弹出菜单中选择“生成组”，就达到标题为垂直居中的效果了，如图 124 1窗体中左边的标签所示。</p>
<p>图 124 1    标签控件标题垂直居中</p>
<h3 id="技巧125-使用TabStrip控件"><a href="#技巧125-使用TabStrip控件" class="headerlink" title="技巧125     使用TabStrip控件"></a>技巧125     使用TabStrip控件</h3><p>使用TabStrip控件，可以在用户窗体中的同一区域定义多个数据页面，也就是说使用TabStrip控件可以使用户窗体中的同一组控件根据TabStrip控件所选择的页面具有不同的功能，而不必像多页控件那样需要在每个页面中放置相同的控件。<br>在示例的窗体中使用一个图像控件和一个标签控件，根据TabStrip控件所选择的页面来显示相应城市的图片和标签控件的题注。<br>步骤1，在窗体中添加一个TabStrip控件，默认情况下，一个TabStrip控件包含两个页面，所以需要在TabStrip控件上右键单击，在显示的右键菜单中选择“新建页”继续添加三个页面。因为TabStrip控件不像多页控件具有分页的属性窗口，所以需要在显示的右键菜单中选择“重命名”将页面分别重命名为各城市的名称。<br>步骤2，在TabStrip控件上添加一个Image控件和一个Label控件，调整为合适的大小。<br>步骤3，双击窗体写入下面的代码：</p>
<pre><code class="vb">Private Sub TabStrip1_Change()
    Dim FilPath As String
    FilPath = ThisWorkbook.Path &amp; &quot;\&quot; &amp; TabStrip1.SelectedItem.Caption &amp; &quot;.jpg&quot;
    Image1.Picture = LoadPicture(FilPath)
    Label1.Caption = TabStrip1.SelectedItem.Caption &amp; &quot;欢迎您!&quot;
End Sub
Private Sub UserForm_Initialize()
    TabStrip1.Value = 0
End Sub
代码解析：
第1行到第6行代码，TabStrip控件的Change事件过程，根据TabStrip控件所选择的页面来显示相应城市的图片和标签控件的题注。
第3行代码设置Image控件需加载图片的完整路径，使用SelectedItem属性返回TabStrip控件当前选中页面的Caption属性，即窗体中所选城市的名称，将图片的完整路径设置为保存在同一目录中已命名为所选城市的图片。
 第4行代码为Image控件加载图片。Picture 属性指定显示在对象上的位图，语法如下：
object.Picture = LoadPicture( pathname )
参数expression是必需的，一个有效的对象。
参数pathname是必需的，一个图片文件的完整路径。
第5行代码设置标签控件的题注为窗体中所选城市的名称和“欢迎您!”。
第7行到第9行代码窗体的Initialize事件过程，为了使窗体显示时TabStrip控件显示第一页，将其Value设置为零 ( 0 )。
运行窗体，选择不同的标签将显示不同城市的图片，如图 125 1所示。

图 125 1    使用TabStrip控件（一）
如果将TabStrip控件的Style属性设置为1则在标签条中显示的是按钮而不是标签，如图 125 2所示。

图 125 2    使用TabStrip控件（二）</code></pre>
<h3 id="技巧126-显示GIF动画图片"><a href="#技巧126-显示GIF动画图片" class="headerlink" title="技巧126     显示GIF动画图片"></a>技巧126     显示GIF动画图片</h3><p>如果希望在Excel中显示GIF格式的动画图片，可以使用AniGif控件。<br>步骤1，在工作表中单击菜单“视图”→“工具栏”→“控件工具箱”→“其他控件”，选择“VBAniGIF. AniGif”后在工作表中拖动添加AniGif控件，如图 126 1所示。</p>
<p>图 126 1    添加AniGif控件<br>如果“其他控件”中没有该控件，那么需要对该控件进行注册。注册控件请参阅技巧118 。AniGif控件的文件名为VBAniGIF.OCX，也可以在工作表中单击菜单“视图”→“工具栏”→“控件工具箱”→“其他控件”，选择“注册自定义控件”，在显示的对话框中选择VBAniGIF.OCX文件进行注册，如图 126 2所示。</p>
<p>图 126 2    注册AniGif控件<br>步骤2，在设计模式下右键单击AniGif控件，选择“属性”，设置AniGif控件的Filename属性为CIF图片所在的路径，如图 126 3所示。</p>
<p>图 126 3    设置Filename属性<br>可以使用代码设置AniGif控件的Filename属性，如下面的代码所示。</p>
<pre><code class="vb">Private Sub Workbook_Open()
    Sheet1.AniGif1.Filename = ThisWorkbook.Path &amp; &quot;\001.gif&quot;
End Sub
代码解析：
工作簿打开时将AniGif控件的Filename属性设置为同一目录中的“001.gif”文件。
工作簿打开时可能出现如图 126 4所示的对话框，这是因为当打开包含ActiveX控件的文件时，如果该控件被标识为初始化不安全时，Office程序不加载或激活未被标志为初始化安全的ActiveX控件。

图 126 4    初始化不安全ActiveX控件提示</code></pre>
<p>解决此问题的方法是更改Office程序处理ActiveX组件的方式，需要对注册表进行修改。也可以使用以下代码修改注册表：</p>
<pre><code class="vb">Sub RegWriteProc()
    Dim WshShell
    Set WshShell = CreateObject(&quot;Wscript.Shell&quot;)
    WshShell.RegWrite &quot;HKCU\Software\Microsoft\Office\Common\Security\UFIControls&quot;, 1, &quot;REG_DWORD&quot;
    WshShell.RegWrite &quot;HKCU\Software\Microsoft\VBA\Security\LoadControlsInForms&quot;, 1, &quot;REG_DWORD&quot;
    Set WshShell = Nothing
End Sub
代码解析：
RegWriteProc过程修改注册表设置。第4行代码将UFIControls子项设置为1（最不安全）。第5行代码将LoadControlsInForms子项设置为1（最不安全）。关于为ActiveX控件授予权限请参阅微软的技术文章：http://support.microsoft.com/kb/827742/zh-cn
退出设计模式后，将在工作表中显示GIF动画图片，如图 126 5所示。

图 126 5    显示GIF动画图片</code></pre>
<h3 id="技巧127-播放Flash文件"><a href="#技巧127-播放Flash文件" class="headerlink" title="技巧127     播放Flash文件"></a>技巧127     播放Flash文件</h3><p>如果需要在工作表中播放Flash文件，那么可以使用ShockwaveFlash控件。<br>步骤1，在工作表中单击菜单“视图”→“工具栏”→“控件工具箱”→“其他控件”，选择“ShocKwave Flash Object”后在工作表中拖动添加ShockwaveFlash控件，如图 127 1所示。</p>
<p>图 127 1    添加ShockwaveFlash控件<br>如果“其他控件”中没有该控件，请参阅技巧126 对其进行注册，ShockwaveFlash控件的文件名为Flash9d.OCX。<br>步骤2，在设计模式下右键单击ShockwaveFlash控件，选择“属性”，设置ShockwaveFlash控件的Base属性和Movie属性为Flash文件所在的路径，设置Embedmovie属性为True，使Flash文件嵌入到Excel中，如图 127 2所示。</p>
<p>图 127 2    设置ShockwaveFlash控件属性<br>可以使用代码设置ShockwaveFlash控件的各项属性，如下面的代码所示。</p>
<pre><code class="vb">Private Sub Workbook_Open()
    With Sheet1.ShockwaveFlash1
        .Base = ThisWorkbook.Path &amp; &quot;\face.swf&quot;
        .Movie = ThisWorkbook.Path &amp; &quot;\face.swf&quot;
        .EmbedMovie = True
    End With
End Sub
代码解析：
工作簿打开时将ShockwaveFlash控件的Base属性和Movie属性设置为同一目录中的“face.swf”文件，设置Embedmovie属性为True。
退出设计模式后，将在工作表中显示Flash动画，如图 127 3所示。

图 127 3    显示Flash动画</code></pre>
<h3 id="技巧128-在工作表中添加窗体控件"><a href="#技巧128-在工作表中添加窗体控件" class="headerlink" title="技巧128     在工作表中添加窗体控件"></a>技巧128     在工作表中添加窗体控件</h3><p>在工作表中添加窗体控件，除了使用手工添加外，还可以使用代码添加，方法如下：</p>
<h4 id="128-1-使用AddFormControl方法"><a href="#128-1-使用AddFormControl方法" class="headerlink" title="128-1    使用AddFormControl方法"></a>128-1    使用AddFormControl方法</h4><p>使用AddFormControl方法在工作表中添加窗体控件，如下面的代码所示。</p>
<pre><code class="vb">Sub AddFormControls()
    Dim myShape As Shape
    On Error Resume Next
    Sheet1.Shapes(&quot;myButton&quot;).Delete
    Set myShape = Sheet1.Shapes.AddFormControl(0, 108, 72, 108, 27)
    With myShape
        .Name = &quot;myButton&quot;
        With .TextFrame.Characters
            .Font.ColorIndex = 3
            .Font.Size = 12
            .Text = &quot;新建的按钮&quot;
        End With
        .OnAction = &quot;myButton&quot;
    End With
End Sub
Sub myButton()
    MsgBox &quot;这是使用AddFormControl方法新建的按钮!&quot;
End Sub
代码解析：
AddFormControls过程使用AddFormControl方法在工作表中添加窗体控件。
第3、4行代码为了避免在工作表中重复添加按钮控件，先删除工作表中的“myButton”按钮。
第5行代码，使用AddFormControl方法在工作表中添加命令按钮控件并设置控件的坐标和大小。应用于Shapes对象的AddFormContl方法创建一个Microsoft Excel控件，返回一个Shape对象，该对象代表新建的控件，语法如下：
expression.AddFormControl(Type, Left, Top, Width, Height)
参数expression是必需的，一个有效的对象。
参数Type是必需的，Microsoft Excel控件类型，可以为表格 128 1所列XlFormControl 常量之一。
常量    值    说明
xlButtonControl    0    命令按钮
xlCheckBox    1    复选框
xlDropDown    2    组合框
xlEditBox    3    编辑框
xlGroupBox    4    分组框
xlLabel    5    标签
xlListBox    6    列表框
xlOptionButton    7    选项按钮
xlScrollBar    8    滚动条
xlSpinner    9    微调项
表格 128 1    XlFormControl 常量
参数Left是必需的，新对象的初始坐标（以磅为单位）相对于工作表 A1 单元格的左上角或图表的左上角。
参数Top是必需的，新对象的初始坐标（以磅为单位）相对于工作表 A1 单元格的左上角或图表的左上角。
参数Width是必需的，以磅为单位的新对象的初始大小。
参数Height是必需的，以磅为单位的新对象的初始大小。
第7行代码将新添加的按钮名称设置为“myButton”。
第8行到第12行代码设置新添加的按钮文字设置为“新建的按钮”，并设置文字的大小和颜色。
第13行代码，指定新添加按钮所执行的宏名称。
myButton过程是单击新添加按钮所执行的过程，显示一个消息框。
运行AddFormControls过程将在工作表中添加一个命令按钮，单击按钮显示一个消息框，如图 128 1所示。

图 128 1    使用AddFormControl方法添加窗体控件</code></pre>
<h4 id="128-2-使用Add方法"><a href="#128-2-使用Add方法" class="headerlink" title="128-2    使用Add方法"></a>128-2    使用Add方法</h4><p>在工作表中添加窗体控件还可以使用Add方法，如下面的代码所示。</p>
<pre><code class="vb">Sub AddChartObjects()
    Dim myButton As Button
    On Error Resume Next
    Sheet1.Shapes(&quot;myButton&quot;).Delete
    Set myButton = Sheet1.Buttons.Add(108, 72, 108, 27)
    With myButton
        .Name = &quot;myButton&quot;
        .Font.Size = 12
        .Font.ColorIndex = 5
        .Characters.Text = &quot;新建的按钮&quot;
        .OnAction = &quot;myButton&quot;
    End With
End Sub
Sub myButton()
    MsgBox &quot;这是使用Add方法新建的按钮!&quot;
End Sub
代码解析：
AddChartObjects过程使用Add方法在工作表中添加窗体控件。
第3、4行代码为了避免在工作表中重复添加按钮控件，先删除工作表中的“myButton”按钮。
第5行代码，使用Add方法在工作表中添加命令按钮控件，Add方法适用于ChartObjects对象的语法如下：
expression.Add(Left, Top, Width, Height)
参数expression是必需的，该表达式返回一个ChartObjects对象。
如果需要在工作表中添加其他窗体控件，可以将参数expression设置为表格 128 2所示的ChartObjects对象之一。
类型    ChartObjects对象
复选框    CheckBoxes
组合框    DropDowns
标签    Labels
列表框    ListBoxes
选项按钮    OptionButtons
滚动条    ScrollBars
微调项    Spinners
表格 128 2    ChartObjects对象
参数Left和Top是必需的，以磅为单位给出新对象的初始坐标，该坐标是相对于工作表上单元格 A1 的左上角或图表的左上角的坐标。
参数Width和参数Height是必需的，以磅为单位给出新对象的初始大小。
第7行代码将新添加的按钮的名称设置为“myButton”。
第8行到第10代码新添加的按钮的文字设置为“新建的按钮”并设置文字的大小和颜色。
第11行代码，指定新添加命令按钮所执行的宏名称。
myButton过程是单击新添加按钮所执行的过程，显示一个消息框。
运行AddChartObjects过程将在工作表中添加一个命令按钮，单击按钮显示一个消息框，如图 128 2所示。</code></pre>
<h3 id="技巧129-在工作表中添加ActiveX控件"><a href="#技巧129-在工作表中添加ActiveX控件" class="headerlink" title="技巧129     在工作表中添加ActiveX控件"></a>技巧129     在工作表中添加ActiveX控件</h3><p>技巧128 中使用代码在工作表中添加的是窗体控件，而本例中使用代码在工作表中添加的是ActiveX控件，两者是有区别的，在工作表中前者是使用窗体对话框添加，而后者是使用控件工具箱添加，如图 129 1所示。 </p>
<p>图 129 1    窗体控件和ActiveX控件的区别</p>
<h4 id="129-1-使用Add方法"><a href="#129-1-使用Add方法" class="headerlink" title="129-1    使用Add方法"></a>129-1    使用Add方法</h4><p>使用Add方法在工作表中添加ActiveX控件，如下面的代码所示。</p>
<pre><code class="vb">Sub AddObj()
    Dim Obj As New OLEObject
    On Error Resume Next
    Sheet1.OLEObjects(&quot;MyButton&quot;).Delete
    Set Obj = Sheet1.OLEObjects.Add(ClassType:=&quot;Forms.CommandButton.1&quot;, _
            Left:=108, Top:=72, Width:=108, Height:=27)
    With Obj
        .Name = &quot;MyButton&quot;
        .Object.Caption = &quot;新建的按钮&quot;
        .Object.Font.Size = 16
        .Object.ForeColor = &amp;HFF&amp;
    End With
    With ActiveWorkbook.VBProject.VBComponents(Sheet1.CodeName).CodeModule
        If .Lines(1, 1) &lt;&gt; &quot;Option Explicit&quot; Then
            .InsertLines 1, &quot;Option Explicit&quot;
        End If
        If .Lines(2, 1) = &quot;Private Sub MyButton_Click()&quot; Then Exit Sub
        .InsertLines 2, &quot;Private Sub MyButton_Click()&quot;
        .InsertLines 3, vbTab &amp; &quot;MsgBox &quot;&quot;这是使用Add方法新建的按钮!&quot;&quot;&quot;
        .InsertLines 4, &quot;End Sub&quot;
    End With
End Sub
代码解析：
AddOLEObject过程使用Add方法在向工作表中添加ActiveX控件中的命令按钮和相应的代码。
第3、4行代码为了避免在工作表中重复添加按钮控件，先删除工作表中的名称为“myButton”的按钮。
第5、6行代码，使用Add方法在向工作表中添加ActiveX控件中的命令按钮，Add方法应用于OLEObjects 对象的语法如下：
expression.Add(ClassType, FileName, Link, DisplayAsIcon, IconFileName, IconIndex, IconLabel, Left, Top, Width, Height)
其中参数expression是必需的，返回一个 OLEObjects 对象。
参数ClassType是可选的，创建的对象的程序标识符。如果指定了 ClassType参数，则忽略FileName参数和Link参数。
在本例中指定添加控件的程序标识符为“Forms.CommandButton.1”，即命令按钮控件，关于对象的程序标识符请参阅技巧119-3。
参数Left和参数Top是必需的，以磅为单位给出新对象的初始坐标，该坐标是相对于工作表上单元格 A1 的左上角或图表的左上角的坐标。
参数Width和参数Height是可选的，以磅为单位给出OLE对象的初始大小。
第8行代码，设置命令按钮的名称为“MyButton”。
第9行代码，设置命令按钮的文字为“新建的按钮”
第10行代码，设置命令按钮的文字的大小。
第11行代码，设置命令按钮的文字的颜色。
第13行到第21行代码，在工作表中写入新添加的命令按钮的单击事件代码。
ActiveX控件不能像窗体控件用OnAction属性来指定宏，需要使用CodeModule对象的InsertLines方法在工作表中插入代码。
应用于CodeModule对象的InsertLines方法的语法如下：
object.InsertLines(line, code)
参数object是必需的，一个有效的对象。
参数line是必需的，用来指定要插入代码的位置。
参数code是必需的，要插入的代码。
第14行到第16行代码判断首行内容是否为要求变量声明，如不是则添加要求变量声明语句。
第17行到第20行代码判断是否已存在相同名称的过程，如不存在则使用InsertLines方法在工作表中插入代码。
运行AddOLEObject过程，将在工作表中添加一个命令按钮和相应的代码，单击按钮显示一个消息框，如图 129 2所示。

图 129 2    使用Add方法添加ActiveX控件</code></pre>
<h4 id="129-2-使用AddOLEObject方法"><a href="#129-2-使用AddOLEObject方法" class="headerlink" title="129-2    使用AddOLEObject方法"></a>129-2    使用AddOLEObject方法</h4><p>在工作表中添加ActiveX控件，还可以使用AddOLEObject方法，如下面的代码所示。</p>
<pre><code class="vb">Sub AddShapes()
    Dim ShpBut As Shape
    On Error Resume Next
    Sheet1.OLEObjects(&quot;MyButton&quot;).Delete
    Set ShpBut = Sheet1.Shapes.AddOLEObject(ClassType:=&quot;Forms.CommandButton.1&quot;, _
            Left:=108, Top:=72, Width:=108, Height:=27)
            ShpBut.Name = &quot;MyButton&quot;
    With ActiveWorkbook.VBProject.VBComponents(Sheet1.CodeName).CodeModule
        If .Lines(1, 1) &lt;&gt; &quot;Option Explicit&quot; Then
            .InsertLines 1, &quot;Option Explicit&quot;
        End If
        If .Lines(2, 1) = &quot;Private Sub MyButton_Click()&quot; Then Exit Sub
        .InsertLines 2, &quot;Private Sub MyButton_Click()&quot;
        .InsertLines 3, vbTab &amp; &quot;MsgBox &quot;&quot;这是使用AddOLEObject方法新建的按钮!&quot;&quot;&quot;
        .InsertLines 4, &quot;End Sub&quot;
    End With
End Sub
代码解析：
AddShapes过程使用AddOLEObject方法在向工作表中添加ActiveX控件中的命令按钮和相应的代码。
第5、6行代码，使用AddOLEObject方法在向工作表中添加ActiveX控件中的命令按钮，AddOLEObject方法创建OLE对象，语法如下：
expression.AddOLEObject(ClassType, FileName, Link, DisplayAsIcon, IconFileName, IconIndex, IconLabel, Left, Top, Width, Height)
AddOLEObject方法参数与Add方法类似，请参阅技巧129-1。
运行AddShapes过程，将在工作表中添加一个命令按钮和相应的代码，单击按钮显示一个消息框，如图 129 3所示。

图 129 3    使用AddOLEObject方法添加ActiveX控件</code></pre>
<h3 id="技巧130-使用spreadsheet控件"><a href="#技巧130-使用spreadsheet控件" class="headerlink" title="技巧130     使用spreadsheet控件"></a>技巧130     使用spreadsheet控件</h3><p>如果希望在窗体中显示类似工作表的表格，并且可以像工作表一样进行操作，那么可以在窗体中使用表格控件（Spreadsheet控件）。<br>步骤1，在VBE窗口中单击菜单“插入”→“用户窗体”，在窗体上添加一个Spreadsheet控件，双击窗体，在其代码窗口中输入下面的代码：</p>
<pre><code class="vb">Private Sub UserForm_Initialize()
    Dim iRow As Integer
    Dim arr As Variant
    With Me.Spreadsheet1
        .DisplayToolbar = False
        .DisplayHorizontalScrollBar = False
        .DisplayVerticalScrollBar = False
        .DisplayWorkbookTabs = False
        iRow = Sheet1.Range(&quot;B65536&quot;).End(xlUp).Row
        arr = Sheet1.Range(&quot;B2:H&quot; &amp; iRow)
        With .Range(&quot;B2:H&quot; &amp; iRow)
            .Value = arr
            .Borders.LineStyle = xlContinuous
            .Borders.Weight = xlMedium
            .Borders.ColorIndex = 10
        End With
        With .Range(&quot;B2:H2&quot;)
            .HorizontalAlignment = -4108
            .VerticalAlignment = -4108
            .Interior.ColorIndex = 44
        End With
        .Range(&quot;B3:B&quot; &amp; iRow).HorizontalAlignment = -4108
        .Range(&quot;C3:H&quot; &amp; iRow).NumberFormat = &quot;0.00&quot;
        .Rows(2).RowHeight = 23.25
        .Columns(&quot;A&quot;).ColumnWidth = 2.75
        .Columns(&quot;B:H&quot;).ColumnWidth = 8
    End With
End Sub
代码解析：
用户窗体的初始化事件过程，使用窗体显示工作表中的表格。
第5行代码，设置Spreadsheet控件不显示工具栏。
DisplayToolbar 属性设置工具栏是否隐藏，语法如下：
expression.DisplayToolbar
参数expression是必需的，一个有效的对象。
如果指定电子表格、图表区或“数据透视表”列表显示了工具栏，则返回True。
第6、7行代码，设置Spreadsheet控件不显示水平和垂直滚动条。
第8行代码，设置Spreadsheet控件不显示工作表标签。
第9行代码，取得工作表B列有数据的最后一行的行号。
第10行代码，把工作表数据赋值给数组。
第11行到16行代码，把数组赋给Spreadsheet控件的单元格，使Spreadsheet控件显示工作表内容，并且添加加框线。
第17行到第21行代码，设置Spreadsheet控件中表格第一行的字体对齐方式为居中并添加单元格的底纹颜色。
第22行代码，设置Spreadsheet控件中表格第一列的字体对齐方式为居中。
第23行代码，设置Spreadsheet控件中表格数据的格式。
第24行到26行代码，设置Spreadsheet控件的行高与列宽。</code></pre>
<p>步骤2，在窗体上添加一个按钮控件，将其Caption属性设置为“保存”，双击按钮控件，在其代码窗口中输入下面的代码：</p>
<pre><code class="vb">Private Sub CommandButton1_Click()
    Dim iRow As Integer
    Dim arr As Variant
    If MsgBox(&quot;是否保存对表格所作的修改?&quot;, 4 + 32) = 6 Then
        With Me.Spreadsheet1
            iRow = .Range(&quot;B65536&quot;).End(xlUp).Row
            arr = .Range(&quot;B2:H&quot; &amp; iRow).Value
            Sheet1.Range(&quot;B2:H&quot; &amp; iRow).Value = arr
        End With
    End If
    Unload Me
End Sub
代码解析：
用户窗体中“保存”按钮的单击过程，把在窗体中对数据的修改重新保存到工作表。
第4行代码，询问用户是否保存修改。
第5行到第10行代码，如果用户选择保存，把Spreadsheet控件中的数据保存到工作表。
运行窗体，显示效果如图 130 1所示。

图 130 1    使用Spreadsheet控件</code></pre>
<h3 id="技巧131-使用Listview控件"><a href="#技巧131-使用Listview控件" class="headerlink" title="技巧131     使用Listview控件"></a>技巧131     使用Listview控件</h3><p>ListView控件是VBA程序开发中的常用控件，可以用来显示各项带图标的列表，也可以用来显示带有子项的列表。</p>
<h4 id="131-1-使用Listview控件显示数据列表"><a href="#131-1-使用Listview控件显示数据列表" class="headerlink" title="131-1    使用Listview控件显示数据列表"></a>131-1    使用Listview控件显示数据列表</h4><p>使用Listview控件在用户窗体中显示数据列表，代码如下：</p>
<pre><code class="vb">Private Sub UserForm_Initialize()
    Dim Itm As ListItem
    Dim r As Integer
    Dim c As Integer
    With ListView1
        .ColumnHeaders.Add , , &quot;人员编号 &quot;, 50, 0
        .ColumnHeaders.Add , , &quot;技能工资 &quot;, 50, 1
        .ColumnHeaders.Add , , &quot;岗位工资 &quot;, 50, 1
        .ColumnHeaders.Add , , &quot;工龄工资 &quot;, 50, 1
        .ColumnHeaders.Add , , &quot;浮动工资 &quot;, 50, 1
        .ColumnHeaders.Add , , &quot;其他  &quot;, 50, 1
        .ColumnHeaders.Add , , &quot;应发合计&quot;, 50, 1
        .View = lvwReport
        .Gridlines = True
        For r = 2 To Sheet1.[A65536].End(xlUp).Row
            Set Itm = .ListItems.Add()
            Itm.Text = Space(2) &amp; Sheet1.Cells(r, 1)
            For c = 1 To 6
                Itm.SubItems(c) = Format(Sheet1.Cells(r, c + 1), &quot;##,#,0.00&quot;)
            Next
        Next
        End With
    Set Itm = Nothing
End Sub
代码解析：
窗体的初始化事件，在窗体显示时将工作表中数据显示在Listview控件中。
第6行到第12行代码，使用ColumnHeader对象的Add方法在Listview控件中添加标题列，并设置列标题、列宽和文本对齐方式。
ColumnHeader对象是ListView控件中包含标题文字的项目，应用于ColumnHeader对象的Add方法语法如下：
object.ColumnHeader.Add(index,key,text,width,alignment)
其中参数text代表标题文字，参数width代表标题的列宽，参数alignment代表列标题中文本对齐方式。Listview控件中文本的对齐方法有三种，如表格 131 1所示。
常数    值    说明
lvwColumnLeft    0    文本向左对齐。（缺省值）
lvwColumnRight    1    文本向右对齐。
lvwColumnCenter    2    文本居中对齐。
表格 131 1    Listview控件中文本的对齐方法
在Listview控件中第一列的文本对齐方式只能设置为左对齐。
第13行代码，设置Listview控件的View属性为lvwReport，使Listview控件显示为报表型。View属性决定在列表中控件使用何种视图显示项目，语法如下：
object.view [= value]
参数object是必需的，对象表达式，listview控件。
参数value是必需的，指定控件外观的整数或常数，如表格 131 2所示。
常数    值    说明
lvwicon    0    图标
lvwsmallicon    1    小图标
lvwlist      2    列表
lvwreport    3    报表
表格 131 2    View属性的设置值
第14行代码，设置Listview控件的Gridlines属性为True，显示网格线。只有在将View属性设置为lvwReport时才能显示网格线，否则Gridlines属性无效。
第16行代码，使用ListItem对象的Add方法在Listview控件中添加项目。应用于ListItem对象的Add方法语法如下：
ListItems.Add(index,key,text,icon,smallIcon)
其中参数text代表添加的项目内容。
第17行代码，添加行标题。ListItem对象的text属性代表Listview控件的第一列内容，因为Listview控件的第一列的文本对齐方式只能设置为左对齐，所以在添加时使用Space函数插入两个空格，使行标题达到居中显示的效果。
第18行到20行代码，继续添加其他列的内容。Listview控件其他列的项目需要使用SubItems属性来添加。
运行窗体，Listview控件显示工作表中的内容，如图 131 1所示。

图 131 1    使用Listview控件显示数据</code></pre>
<h4 id="131-2-在Listview控件中使用复选框"><a href="#131-2-在Listview控件中使用复选框" class="headerlink" title="131-2    在Listview控件中使用复选框"></a>131-2    在Listview控件中使用复选框</h4><p>在Listview控件中使用复选框，可以进行多重选择，示例代码如下：</p>
<pre><code class="vb">Private Sub UserForm_Initialize()
    Dim Itm As ListItem
    Dim r As Integer
    Dim c As Integer
    With ListView1
        .ColumnHeaders.Add , , &quot;人员编号 &quot;, 50, 0
        .ColumnHeaders.Add , , &quot;技能工资 &quot;, 50, 1
        .ColumnHeaders.Add , , &quot;岗位工资 &quot;, 50, 1
        .ColumnHeaders.Add , , &quot;工龄工资 &quot;, 50, 1
        .ColumnHeaders.Add , , &quot;浮动工资 &quot;, 50, 1
        .ColumnHeaders.Add , , &quot;其他  &quot;, 50, 1
        .ColumnHeaders.Add , , &quot;应发合计&quot;, 50, 1
        .View = lvwReport
        .Gridlines = True
        .FullRowSelect = True
        .CheckBoxes = True
        For r = 2 To Sheet2.[A65536].End(xlUp).Row - 1
            Set Itm = .ListItems.Add()
            Itm.Text = Sheet2.Cells(r, 1)
            For c = 1 To 6
                Itm.SubItems(c) = Format(Sheet2.Cells(r, c + 1), &quot;##,#,0.00&quot;)
            Next
        Next
        End With
    Set Itm = Nothing
End Sub
Private Sub CommandButton1_Click()
    Dim r As Integer
    Dim i As Integer
    Dim c As Integer
    r = Sheet1.[A65536].End(xlUp).Row
    If r &gt; 1 Then Sheet1.Range(&quot;A2:G&quot; &amp; r) = &quot;&quot;
    With ListView1
        For i = 1 To .ListItems.Count
            If .ListItems(i).Checked = True Then
                Sheet1.Range(&quot;A65536&quot;).End(xlUp).Offset(1, 0) = .ListItems(i)
                For c = 1 To 6
                    Sheet1.Cells(65536, c + 1).End(xlUp).Offset(1, 0) = .ListItems(i).SubItems(c)
                Next
            End If
        Next
    End With
End Sub
代码解析：
第1行到第26行代码，用户窗体的Initialize事件过程，在窗体显示时将工作表中数据显示在Listview控件中，请参阅技巧0。
其中第15行代码设置Listview控件的FullRowSelect属性为True，使用户可以选择整行。
第16行代码设置Listview控件的CheckBoxes属性为True，使Listview控件在列表的每个项的旁边显示复选框。
第27行到第43行代码，用户窗体中“保存”按钮的单击过程，将Listview控件中选中的项目写入到工作表中。
第31、32行代码，删除工作表中原有的数据，
第34、35行代码遍历Listview控件中所有的ListItem对象，判定其Checked值，如果为True，即说明其处于选中状态。
第36行到第40行代码将Listview控件中选中的内容依次写入到工作表中。
运行窗体，Listview控件显示工作表中的内容，单击“保存”按钮将如Listview控件中选中的内容依次写入到工作表中，如图 131 2所示。

图 131 2    Listview控件使用复选框</code></pre>
<h4 id="131-3-调整Listview控件的行距"><a href="#131-3-调整Listview控件的行距" class="headerlink" title="131-3    调整Listview控件的行距"></a>131-3    调整Listview控件的行距</h4><p>在使用Listview控件显示数据列表时，行距是由Listview控件所设置的字体大小决定的，无法自定义行距，即使调整了字体大小，行距还是很近。<br>如果需要自定义Listview控件的行距，可以在窗体中添加一个ImageList控件，在ImageList控件中导入一张大小合适的空白图片，然后指定Listview控件的SmallIcons属性为ImageList控件中的图片，代码如下：</p>
<pre><code class="vb">Private Sub UserForm_Initialize()
    Dim Itm As ListItem
    Dim r As Integer
    Dim c As Integer
    Dim Img As ListImage
    With ListView1
        .ColumnHeaders.Add , , &quot;人员编号 &quot;, 50, 0
        .ColumnHeaders.Add , , &quot;技能工资 &quot;, 50, 1
        .ColumnHeaders.Add , , &quot;岗位工资 &quot;, 50, 1
        .ColumnHeaders.Add , , &quot;工龄工资 &quot;, 50, 1
        .ColumnHeaders.Add , , &quot;浮动工资 &quot;, 50, 1
        .ColumnHeaders.Add , , &quot;其他  &quot;, 50, 1#013          .ColumnHeaders.Add , , &quot;应发合计&quot;, 50, 1
        .View = lvwReport
        .Gridlines = True
        .FullRowSelect = True
        Set Img = ImageList1.ListImages.Add(, , LoadPicture(ThisWorkbook.Path &amp; &quot;\&quot; &amp; &quot;1×25.bmp&quot;))
        .SmallIcons = ImageList1
        For r = 2 To Sheet1.[A65536].End(xlUp).Row - 1
            Set Itm = .ListItems.Add()
            Itm.Text = Space(2) &amp; Sheet1.Cells(r, 1)
            For c = 1 To 6
                Itm.SubItems(c) = Format(Sheet1.Cells(r, c + 1), &quot;##,#,0.00&quot;)
            Next
        Next
    End With
    Set Itm = Nothing
    Set Img = Nothing
End Sub
代码解析：
用户窗体的Initialize事件过程，在窗体显示时将工作表中数据显示在Listview控件中并调整Listview控件的行距。
第17行代码使用Add方法在ImageList控件中添加图片。ImageList控件是一个向其他控件提供图像的资料中心，它包含了一组ListImage对象即一组图像的集合，该集合中的每个对象都可以通过其索引或关键字被其他控件所引用，但控件本身并不能单独使用。
在运行时给ImageList控件添加图片需要使用Add方法，语法如下：
Add(index,key,picture)
参数index是可选的，整数，指定要插入的ListImage对象的位置。如果没有指定index，ListImage对象将被添加到ListImages集合的末尾。
参数key是可选的，用来标识ListImage对象的唯一字符串。
参数picture是必需的，指定欲添加到集合中的图片。
也可以在设计时在ImageList控件中添加图片，这样就无需在文件夹中保留图片文件。在VBE中选择ImageList控件属性页中的“自定义”，在显示的“属性页”对话框中插入图片，如图 131 3所示。

图 131 3    在ImageList控件中添加图片
第18行代码，指定Listview控件的SmallIcons属性为ImageList控件中的图片，使用图片来调整行距。
运行窗体，Listview控件显示工作表中的内容，调整Listview控件的行距，如图 131 4所示。

图 131 4    调整Listview控件的行距</code></pre>
<h4 id="131-4-在Listview控件中排序"><a href="#131-4-在Listview控件中排序" class="headerlink" title="131-4    在Listview控件中排序"></a>131-4    在Listview控件中排序</h4><p>在使用Listview控件显示报表型的数据时，可能通过单击Listview控件的列标题对列表数据进行排序，代码如下：</p>
<pre><code class="vb">Private Sub ListView1_ColumnClick(ByVal ColumnHeader As MSComctlLib.ColumnHeader)
    With ListView1
        .Sorted = True
        .SortOrder = (.SortOrder + 1) Mod 2
        .SortKey = ColumnHeader.Index - 1
    End With
End Sub
代码解析：
Listview控件的ColumnClick事件过程，单击列标题时触发，对列表数据进行升序或降序排序。
第3行代码将Listview控件的Sorted属性设置为True。Sorted属性返回或设置确定ListView控件中的ListItem对象是否排序，设置为False则不进行排序。
第4行代码设置Listview控件的排序方式。SortOrder属性返回或设置一个值，决定ListView控件中的ListItem对象以升序或降序排序，设置为0以升序排序，设置为1则以降序排序。在设置SortOrder属性值时使用Mod运算符以达到第一次排序以降序排序，再次排序时以升序排序，交替进行的效果。
第5行代码设置Listview控件排序关键字的整数，即指定Listview控件以当前选定的列数据进排序。SortKey属性返回或设置一个值，此值决定ListView控件中的ListItem对象如何排序，语法如下：
object.SortKey [=integer]
参数object是必需的，对象表达式，其值为ListView控件。
参数integer是必需的，指定排序关键字的整数，设置为0使用ListItem对象的Text属性排序，即第一列的数据进行排序。设置为大于0的整数则使用子项目的集合索引排序。
运行窗体，Listview控件显示工作表中的内容，单击列标题对列表数据进行升序或降序排序，如图 131 5所示。

图 131 5    在Listview控件中排序
#### 131-5    Listview控件的图标设置
ListView 控件作为一个可以显示图标或者子项的列表控件，可以在控件中显示自定义的图标，它最重要的属性就是View 属性，该属性决定了以哪种视图模式显示控件的项，请参阅技巧131-1。
在ListView 控件中显示图标，需要在用户窗体中添加一个ImageList控件用于保存图像文件。关于ImageList控件的使用请参阅技巧131-3。</code></pre>
<p>以大图标模式显示ListView控件的代码如下：</p>
<pre><code class="vb">Private Sub UserForm_Initialize()
    Dim ITM As ListItem
    Dim r As Integer
    With ListView1
        .View = lvwIcon
        .Icons = ImageList1
        For r = 2 To 6
            Set ITM = .ListItems.Add()
            ITM.Text = Cells(r, 1)
            ITM.Icon = r - 1
        Next
    End With
    Set ITM = Nothing
End Sub
代码解析：
在用户窗体中以大图标模式显示ListView控件，可使用鼠标拖放图标，并重新排列。
第5行代码将ListView控件的View属性设置为lvwIcon，大图标视图模式。
第6行代码使用ListView控件的Icons 属性建立与ImageList控件的关联。
第7行到第11行代码在ListView控件中添加ListItem对象，其中第10行代码设置使用ListItem对象的Icon属性指定其图像文件在ImageList控件中的编号。
ListView控件以大图标视图模式显示时如图 131 6所示。

图 131 6    大图标视图模式</code></pre>
<p>以小图标模式显示ListView控件的代码如下：</p>
<pre><code class="vb">Private Sub UserForm_Initialize()
    Dim ITM As ListItem
    Dim r As Integer
    With ListView1
        .View = lvwSmallIcon
        .SmallIcons = ImageList1
        For r = 2 To 6
            Set ITM = .ListItems.Add()
            ITM.Text = Sheet1.Cells(r, 1)
            ITM.SmallIcon = r - 1
        Next
    End With
    Set ITM = Nothing
End Sub
代码解析：
在用户窗体中以小图标模式显示ListView控件，可使用鼠标拖放图标，并重新排列。
第5行代码将ListView控件的View属性设置为lvwSmallIcon，小图标视图模式。
与大图标视图模式有所不同的是，当使用小图标视图模式时需要使用ListView控件的SmallIcons属性建立与ImageList控件的关联，使用ListItem对象的SmallIcon属性指定其图像文件在ImageList控件中的编号。
ListView控件以小图标视图模式显示时如图 131 7所示。

图 131 7    小图标视图模式
将ListView控件的View属性设置为lvwList，以列表视图模式显示，如图 131 8所示。

图 131 8    列表视图模式
将ListView控件的View属性设置为lvwReport，以报表视图模式显示，如图 131 9所示。

图 131 9    报表视图模式</code></pre>
<h3 id="技巧132-调用非模式窗体"><a href="#技巧132-调用非模式窗体" class="headerlink" title="技巧132     调用非模式窗体"></a>技巧132     调用非模式窗体</h3><p>在VBA中显示用户窗体需要使用Show方法，Show方法显示窗体对象，语法如下：<br>[object.]Show modal<br>参数object是可选的，对象表达式。如果省略掉object，则将与活动的窗体模块相关联的窗体当作object。<br>参数modal是可选的，决定窗体是模态的还是非模式的。Modal参数的设置值如表格 132 1所示。<br>常数    值    描述<br>vbModal    1    UserForm是模态的，缺省值。<br>vbModeless    0    UserForm是非模式的。<br>表格 132 1    modal参数的设置值<br>当窗体显示时是模态时，用户在使用应用程序的其它部分之前，必须先对其作出响应。在隐藏或卸载窗体之前，后续代码不会被执行。<br>比如下面的代码，希望在显示窗体的同时给单元格赋值，但因为窗体显示为模态的，在窗体没有关闭之前，给单元格赋值的代码是不会执行的，所以达不到显示窗体的同时给单元格赋值的目的。</p>
<pre><code class="vb">Private Sub CommandButton1_Click()
    Dim i As Integer
    Columns(1).ClearContents
    UserForm1.Show 0
    For i = 1 To 1000
        Cells(i, 1) = i
    Next
End Sub
只有在窗体显示为非模式时，后续代码才一出现即被执行。模态下是无法操作工作表的，所以应将第4行代码改成如下的代码，才能在显示窗体的同时给单元格赋值，如图 132 1所示。
UserForm1.Show 0

图 132 1    调用非模式窗体</code></pre>
<h3 id="技巧133-进度条的制作"><a href="#技巧133-进度条的制作" class="headerlink" title="技巧133     进度条的制作"></a>技巧133     进度条的制作</h3><p>如果程序执行时间较长，使用进度条能让用户知道程序执行到何种程度，大约需等待多长时间，可以使界面显得友好。</p>
<h4 id="133-1-使用进度条控件"><a href="#133-1-使用进度条控件" class="headerlink" title="133-1    使用进度条控件"></a>133-1    使用进度条控件</h4><p>使用窗体加进度条控件（ProgressBar）制作进度条是最常用的方法。<br>在VBE窗口中单击菜单“插入”→“用户窗体”，在窗体上添加一个进度条控件，调整为合适的大小，如图 133 1所示。</p>
<p>图 133 1    使用ProgressBar控件<br>在工作表中添加一个命令按钮，双击后写入下面的代码。</p>
<pre><code class="vb">Private Sub CommandButton1_Click()
    Dim i As Integer
    UserForm1.Show 0
    With UserForm1.ProgressBar1
        .Min = 1
        .Max = 10000
        .Scrolling = 0
        For i = 1 To 10000
            Cells(i, 1) = i
            .Value = i
            UserForm1.Caption = &quot;正在运行,已完成&quot; &amp; i / 100 &amp; &quot;%,请稍候!&quot;
        Next
    End With
    Unload UserForm1
    Columns(1).ClearContents
End Sub
代码解析：
工作表中命令按钮的单击事件，在给工作表A1到A10000单元格赋值的同时使用进度条显示其运行速度。
第3行代码，使用Show方法显示进度条控件所在的窗体，并且设置为无模式显示，请参阅技巧132 。
第5、6行代码，设置进度条控件的最小值和最大值，应与第8行代码中的循环计数器的start参数和End参数相一致。
第7行代码，设置进度条控件显示为有间隔的。如果将Scrolling属性设置为1则显示为无间隔的。
第9行代码，在单元格中进行无意义的填充数据以演示进度条。在实际应用中可以将进度条嵌入到程序的循环中。
第11行代码，在窗体的标题栏中显示已完成的百分比。
第14行代码，使用Unload 语句卸载窗体。
Unload 语句从内存中删除一个对象，语法如下：
Unload object
参数object参数是必需的，一个有效的对象。
第19行代码，清空A列填充的数据。
单击工作表中的命令按钮，填充单元格并显示进度条，如图 133 2所示。</code></pre>
<h4 id="133-2-使用标签控件"><a href="#133-2-使用标签控件" class="headerlink" title="133-2    使用标签控件"></a>133-2    使用标签控件</h4><p>在窗体中使用标签可以制作双色的进度条。<br>步骤1，在VBE窗口中单击菜单“插入”→“用户窗体”，在窗体上添加一个框架控件，在框架控件中添加两个标签控件。<br>步骤2，在控件的属性窗口中将框架的BackColor 属性设为&amp;H000000FF&amp;，使框架的背景色为红色。将标签1的BackColor属性设为&amp;H0000C000&amp;，使标签1的背景色为绿色。将标签2的BackStyle属性设为fmBackStyleTransparent，使标签2的背景为透明，并把它们的Caption属性全部设置为空白。<br>步骤3，将窗体和控件调整为合适的大小，如图 133 3所示。</p>
<p>图 133 3    制作标签进度条</p>
<p>步骤4，在VBE中双击窗体，写入下面的代码。</p>
<pre><code class="vb">Private Declare Function DrawMenuBar Lib &quot;user32&quot; (ByVal Hwnd As Long) As Long
Private Declare Function GetWindowLong Lib &quot;user32&quot; Alias &quot;GetWindowLongA&quot; (ByVal Hwnd As Long, ByVal nIndex As Long) As Long
Private Declare Function SetWindowLong Lib &quot;user32&quot; Alias &quot;SetWindowLongA&quot; (ByVal Hwnd As Long, ByVal nIndex As Long, ByVal dwNewLong As Long) As Long
Private Declare Function FindWindow Lib &quot;user32&quot; Alias &quot;FindWindowA&quot; (ByVal lpClassName As String, ByVal lpWindowName As String) As Long
Private Const GWL_STYLE As Long = (-16)
Private Const GWL_EXSTYLE = (-20)
Private Const WS_CAPTION As Long = &amp;HC00000
Private Sub UserForm_Initialize()
    Dim IStyle As Long
    Dim Hwnd As Long
    If Val(Application.Version) &lt; 9 Then
        Hwnd = FindWindow(&quot;ThunderXFrame&quot;, Me.Caption)
    Else
        Hwnd = FindWindow(&quot;ThunderDFrame&quot;, Me.Caption)
    End If
    IStyle = GetWindowLong(Hwnd, GWL_STYLE)
    IStyle = IStyle And Not WS_CAPTION
    SetWindowLong Hwnd, GWL_STYLE, IStyle
    DrawMenuBar Hwnd
    UserForm1.Height = 28
End Sub
代码解析：
窗体的初始化事件，在窗体加载时使用API函数去除其标题栏。
第1行到第7行代码，API函数的声明。
第11行到第15行代码，获取窗口句柄。
第16行到第19行代码，去除窗体标题栏。
第20行代码，设置窗体的高度。
步骤5，在工作表中添加一个命令按钮，双击后写入下面的代码。</code></pre>
<pre><code class="vb">Private Sub CommandButton1_Click()
    Dim n As Integer
    Dim i As Integer
    n = 10000
    With UserForm1
        .Show 0
        For i = 1 To n
            Cells(i, 1) = i
            .Label1.Width = i / n * .Frame1.Width
            .Label2.Caption = &quot;已完成&quot; &amp; Round(i / n * 100, 0) &amp; &quot;%&quot;
            .Label2.Left = .Label1.Width - 50
            DoEvents
        Next
    End With
    Unload UserForm1
    Range(&quot;A1:A&quot; &amp; n).ClearContents
End Sub
代码解析：
工作表中命令按钮的单击事件，在给工作表A1到A10000单元格赋值的同时使用进度条显示其运行速度。
第4行代码，设置循环最大值，可根据实际需要设置。
第6行代码，使用Show方法显示窗体，并且设置为无模式的。
第8行代码，在单元格中进行无意义的填充数据以演示进度条。
第9行代码，根据程序运行程度动态设置标签1的宽度，使之达到进度条的效果。
第10行代码，标签2显示已完成百分比。
第11行代码，根据标签1的宽度动态设置标签2的Left属性，使已完成百分比跟随标签1移动。
第12行代码，使用DoEvents函数转让控制权。DoEvents函数将控制权传给操作系统。当操作系统处理完队列中的事件，并且在 SendKeys队列中的所有键也都已送出之后，返回控制权。如果不使用DoEvents函数转让控制权，进度条不能正常显示。
第15行代码，使用Unload 语句卸载窗体。
单击工作表中的命令按钮，填充单元格并显示进度条，如图 133 4所示。

图 133 4    标签进度条</code></pre>
<h3 id="技巧134-使用TreeView控件显示层次"><a href="#技巧134-使用TreeView控件显示层次" class="headerlink" title="技巧134     使用TreeView控件显示层次"></a>技巧134     使用TreeView控件显示层次</h3><p>TreeView控件是一个树形结构的控件，该控件用于显示分层数据，如目录或文件目录，使程序的表现更为灵活，用户的操作更加方便，示例代码如下：</p>
<pre><code class="vb">Private Sub UserForm_Initialize()
    Dim c As Integer
    Dim r As Integer
    Dim rng As Variant
    rng = Sheet1.UsedRange
    With Me.TreeView1
        .Style = tvwTreelinesPlusMinusPictureText
        .LineStyle = tvwRootLines
        .CheckBoxes = False
        With .Nodes
            .Clear
            .Add Key:=&quot;科目&quot;, Text:=&quot;科目名称&quot;
            For c = 1 To Sheet1.UsedRange.Columns.Count
                For r = 2 To Sheet1.UsedRange.Rows.Count
                    If Not IsEmpty(rng(r, c)) Then
                        If c = 1 Then
                            .Add relative:=&quot;科目&quot;, relationship:=tvwChild, Key:=rng(r, c), Text:=rng(r, c)
                        ElseIf Not IsEmpty(rng(r, c - 1)) Then
                            .Add relative:=rng(r, c - 1), relationship:=tvwChild, Key:=rng(r, c), Text:=rng(r, c)
                        Else
                            .Add relative:=CStr(Sheet1.Cells(r, c - 1).End(xlUp)), relationship:=tvwChild, Key:=rng(r, c), Text:=rng(r, c)
                        End If
                    End If
                Next
            Next
        End With
    End With
End Sub
代码解析：
在窗体初始化时将工作表中的科目名称填充TreeView控件。
第7行代码，设置TreeView控件每个列表的组成方式。Style属性设置值如表格 131 2所示。
常量    值    描述
tvwTextOnly    0    文本
tvwPictureText    1    图像文本
tvwPlusMinusText    2    符号文本
tvwTreelinesText    4    直线文本
tvwTreelinesPlusMinusPictureText    7    正常显示
表格 134 1    Style属性设置值
第8行代码，设置TreeView控件显示根节点连线。TreeView控件的LineStyle属性设置为tvwRootLines显示根节点连线，设置为tvwTreeLines则隐藏根节点连线。
第9行代码，设置TreeView控件不显示复选框。
第10行代码使用Nodes属性返回对TreeView控件的Node对象的集合的引用。
第11行代码，清除TreeView控件所有的节点。
第12行代码，使用Add方法在Treeview控件的Nodes集合中添加一个Node对象。，Add方法语法如下：
object.Add(relative, relationship, key, text, image, selectedimage)
参数Object是必需的，一个有效的对象。
参数Relative是可选的，代表已存在的Node对象的索引号或键值。
参数relationship是可选的，代表新节点与已存在的节点间的关系，指定的Node对象的相对位置。relationship的设置值如表格 134 2所示。
常量    值    说明
tvwFirst    0    首节点，该Node和在relative中被命名的节点位于同一层，并位于所有同层节点之前。
tvwLast    1    最后的节点，该Node和在relative中被命名的节点位于同一层，并位于所有同层节点之后。任何连续地添加的节点可能位于最后添加的节点之后。
tvwNext    2    下一个节点，该Node位于在relative中被命名的节点之后。
tvwPrevious    3    前一个节点，该Node位于在relative中被命名的节点之前。
tvwChild    4    子节点。该Node 为在relative中被命名的节点的子节点。
表格 134 2    relationship的设置值
参数key是可选的，唯一的字符串，可用于用Item方法检索Node。
参数text 是必需的，在Node中出现的字符串。
参数image是可选的，代表一个图像或在ImageList控件中图象的索引。
参数selectedimage是可选的，代表一个图像或在ImageList控件中图象的索引，在 Node被选中时显示。
第13行到第25行代码代，在根节点下添加子节点。添加子节点仍然使用Add方法，需要一个唯一的Key值，必须提供根节点的Key值（参数relative）和参数relationship值（tvwChild）。要将子节点链接到根节点的下面，参数relative必须与根节点的Key值一致，参数relationship必须设置为tvwchild。要使子节点有效，子节点必须也有自已唯一的Key值。
获得双击TreeView控件后的返回值的代码如下：
Private Sub TreeView1_DblClick()
    If TreeView1.SelectedItem.Children = 0 Then
        Sheet1.Range(&quot;A65536&quot;).End(xlUp).Offset(1) = TreeView1.SelectedItem.Text
    Else
        MsgBox &quot;所选择的不是末级科目,请重新选择科目!&quot;
    End If
End Sub
代码解析：
TreeView1_ DblClick过程是TreeView控件的双击事件，将所选的科目名称写入到工作表中。
第2行代码判断所选节点是否是末级科目。TreeView控件的SelectedItem属性返回当前所选择的节点，而Children属性检查所选节点是否还有子节点，如没有子节点则返回0。
运行窗体效果如图 134 1所示。

图 134 1    使用TreeView控件显示层次</code></pre>
<h3 id="技巧135-用户窗体添加图标"><a href="#技巧135-用户窗体添加图标" class="headerlink" title="技巧135     用户窗体添加图标"></a>技巧135     用户窗体添加图标</h3><p>窗体在显示时标题栏上是没有图标的，如果希望在窗体上添加图标，可以借助API函数在窗体显示时添加自定义的图标。<br>在VBE窗口中单击菜单“插入”→“用户窗体”，插入一个窗体，在窗体中添加一个Image控件，设置Image控件Picture属性为自定义图标的位图，并将Image控件的Visible属性设置为False，使窗体运行时隐藏Image控件，如图 135 1所示。</p>
<p>图 135 1    窗体中添加Image控件<br>在VBE中双击窗体，写入下面的代码。</p>
<pre><code class="vb">Private Declare Function SendMessage Lib &quot;user32&quot; Alias &quot;SendMessageA&quot; (ByVal hWnd As Long, ByVal wMsg As Long, ByVal wParam As Long, lParam As Any) As Long
Private Declare Function DrawMenuBar Lib &quot;user32&quot; (ByVal hWnd As Long) As Long
Private Declare Function FindWindow Lib &quot;user32&quot; Alias &quot;FindWindowA&quot; (ByVal lpClassName As String, ByVal lpWindowName As String) As Long
Private Const WM_SETICON = &amp;H80
Private Const ICON_SMALL = 0&amp;
Private Const ICON_BIG = 1&amp;
Sub ChangeIcon(ByVal hWnd As Long, Optional ByVal hicon As Long = 0&amp;)
    SendMessage hWnd, WM_SETICON, ICON_SMALL, ByVal hicon
    SendMessage hWnd, WM_SETICON, ICON_BIG, ByVal hicon
    DrawMenuBar hWnd
End Sub
Private Sub UserForm_Initialize()
    Dim hWnd As Long
    hWnd = FindWindow(vbNullString, Me.Caption)
    Call ChangeIcon(hWnd, Image1.Picture.Handle)
End Sub
代码解析：
窗体的初始化事件，窗体在显示时运行ChangeIcon函数，在标题栏中添加图标。
第1行到第6行代码， API函数声明。
第7行到第11行代码，ChangeIcon过程，用于转换图标。
第14行代码，获得窗口句柄。
第15行代码，运行ChangeIcon过程，将Image控件中的位图显示在窗体的标题栏上。
运行窗体后，在窗体标题栏上添加图标，如图 135 2所示。

图 135 2    在窗体标题栏中添加图标</code></pre>
<h3 id="技巧136-用户窗体添加最大最小化按纽"><a href="#技巧136-用户窗体添加最大最小化按纽" class="headerlink" title="技巧136     用户窗体添加最大最小化按纽"></a>技巧136     用户窗体添加最大最小化按纽</h3><p>VBA中的窗体标题栏上只有关闭按纽，没有最大最小化按纽的，可以使用API函数在窗体的标题栏上添加最大最小化按纽，如下面的代码所示。</p>
<pre><code class="vb">Private Declare Function FindWindow Lib &quot;user32&quot; Alias &quot;FindWindowA&quot; (ByVal lpClassName As String, ByVal lpWindowName As String) As Long
Private Declare Function GetWindowLong Lib &quot;user32&quot; Alias &quot;GetWindowLongA&quot; (ByVal hWnd As Long, ByVal nIndex As Long) As Long
Private Declare Function SetWindowLong Lib &quot;user32&quot; Alias &quot;SetWindowLongA&quot; (ByVal hWnd As Long, ByVal nIndex As Long, ByVal dwNewLong As Long) As Long
Private Const WS_MAXIMIZEBOX = &amp;H10000
Private Const WS_MINIMIZEBOX = &amp;H20000
Private Const GWL_STYLE = (-16)
Private Sub UserForm_Initialize()
    Dim hWndForm As Long
    Dim iStyle As Long
    hWndForm = FindWindow(&quot;ThunderDFrame&quot;, Me.Caption)
    iStyle = GetWindowLong(hWndForm, GWL_STYLE)
    iStyle = iStyle Or WS_MINIMIZEBOX
    iStyle = iStyle Or WS_MAXIMIZEBOX
    SetWindowLong hWndForm, GWL_STYLE, iStyle
End Sub
代码解析：
窗体初始化时使用API函数在标题栏上添加最大最小化按纽。
第1行到第6行代码，API函数声明。
第10行代码，获取窗口句柄。
第11行到第14行代码，在标题栏上添加最大最小化按纽。
运行窗体后效果如图 136 1所示。

图 136 1    标题栏上添加最大最小化按纽</code></pre>
<h3 id="技巧137-禁用窗体标题栏的关闭按钮"><a href="#技巧137-禁用窗体标题栏的关闭按钮" class="headerlink" title="技巧137     禁用窗体标题栏的关闭按钮"></a>技巧137     禁用窗体标题栏的关闭按钮</h3><p>如果不希望用户通过窗体标题栏的关闭命令来关闭窗体，可以禁用窗体标题栏上的关闭按钮，如下面的代码所示。</p>
<pre><code class="vb">Private Sub UserForm_QueryClose(Cancel As Integer, CloseMode As Integer)
    If CloseMode &lt;&gt; 1 Then
        Cancel = True
        MsgBox &quot;请点击按钮关闭窗体!&quot;
    End If
End Sub
代码解析：
窗体的QueryClose事件，禁用窗体标题栏上的关闭按钮。
窗体的QueryClose事件发生在窗体关闭之前，语法如下：
Private Sub UserForm_QueryClose(cancel As Integer, closemode As Integer)
参数Cance是可选的，整数。将此参数设置成 0 以外的任意值，在所有加载的用户窗体中停止QueryClose事件，并防止关闭窗体与应用程序。
参数closemode是可选的，一个值或常数，用来指示引起QueryClose事件的原因。
closemode参数的设置值如表格 137 1所示。
常数    值    描述
vbFormControlMenu    0    用户在 UserForm上选择“控制”菜单中的“关闭”命令
VbFormCode    1    由代码调用 Unload 语句
vbAppWindows    2    正在结束当前 Windows 操作环境的过程。(仅用于Visual Basic 5.0 )
vbAppTaskManager    3    Windows 的“任务管理器”正在关闭这个应用。(仅用于Visual Basic 5.0 )
表格 137 1    closemode 参数
第2、3行代码，如果窗体不是由代码调用Unload语句关闭，则停止关闭过程，从而禁用窗体标题栏的关闭按钮。
需要注意的是，一定要在窗体上设置关闭窗体的途径，否则会使窗体无法关闭。
窗体运行后，禁用窗体上的关闭按钮关闭窗体，只能使用按钮关闭窗体，如图 137 1所示。

图 137 1    禁用窗体标题栏的关闭命令</code></pre>
<h3 id="技巧138-屏蔽窗体标题栏的关闭按钮"><a href="#技巧138-屏蔽窗体标题栏的关闭按钮" class="headerlink" title="技巧138     屏蔽窗体标题栏的关闭按钮"></a>技巧138     屏蔽窗体标题栏的关闭按钮</h3><p>使用API函数可以屏蔽窗体标题栏的关闭按钮，如下面的代码所示。</p>
<pre><code class="vb">Private Declare Function FindWindow Lib &quot;user32&quot; Alias &quot;FindWindowA&quot; (ByVal lpClassName As String, ByVal lpWindowName As String) As Long
Private Declare Function GetWindowLong Lib &quot;user32&quot; Alias &quot;GetWindowLongA&quot; (ByVal Hwnd As Long, ByVal nIndex As Long) As Long
Private Declare Function SetWindowLong Lib &quot;user32&quot; Alias &quot;SetWindowLongA&quot; (ByVal Hwnd As Long, ByVal nIndex As Long, ByVal dwNewLong As Long) As Long
Private Declare Function DrawMenuBar Lib &quot;user32&quot; (ByVal Hwnd As Long) As Long
Private Const GWL_STYLE = (-16)
Private Const WS_SYSMENU = &amp;H80000
Private Hwnd As Long
Private Sub UserForm_Initialize()
    Dim Istype As Long
    Hwnd = FindWindow(&quot;ThunderDFrame&quot;, Me.Caption)
    Istype = GetWindowLong(Hwnd, GWL_STYLE)
    Istype = Istype And Not WS_SYSMENU
    SetWindowLong Hwnd, GWL_STYLE, Istype
    DrawMenuBar Hwnd
End Sub
代码解析：
第1行到第7行代码是API函数声明。
第8行到第15行代码是窗体的Initialize事件，当窗体显示时屏蔽窗体标题栏的关闭按钮。
窗体运行后，屏蔽窗体上的关闭按钮，只能使用按钮关闭窗体，如图 138 1所示。

图 138 1    屏蔽窗体标题栏的关闭按钮</code></pre>
<h3 id="技巧139-无标题栏和边框的窗体"><a href="#技巧139-无标题栏和边框的窗体" class="headerlink" title="技巧139     无标题栏和边框的窗体"></a>技巧139     无标题栏和边框的窗体</h3><p>如果希望制作无标题栏和边框的窗体，那么可以使用API函数。<br>在VBE窗口中单击菜单“插入”→“用户窗体”，双击窗体，在其代码窗口中输入下面的代码：</p>
<pre><code class="vb">Private Declare Function DrawMenuBar Lib &quot;user32&quot; (ByVal Hwnd As Long) As Long
Private Declare Function GetWindowLong Lib &quot;user32&quot; Alias &quot;GetWindowLongA&quot; (ByVal Hwnd As Long, ByVal nIndex As Long) As Long
Private Declare Function SetWindowLong Lib &quot;user32&quot; Alias &quot;SetWindowLongA&quot; (ByVal Hwnd As Long, ByVal nIndex As Long, ByVal dwNewLong As Long) As Long
Private Declare Function FindWindow Lib &quot;user32&quot; Alias &quot;FindWindowA&quot; (ByVal lpClassName As String, ByVal lpWindowName As String) As Long
Private Const GWL_STYLE As Long = (-16)
Private Const GWL_EXSTYLE = (-20)
Private Const WS_CAPTION As Long = &amp;HC00000
Private Const WS_EX_DLGMODALFRAME = &amp;H1&amp;
Private Sub UserForm_Initialize()
    Dim IStyle As Long
    Dim Hwnd As Long
    If Val(Application.Version) &lt; 9 Then
        Hwnd = FindWindow(&quot;ThunderXFrame&quot;, Me.Caption)
    Else
        Hwnd = FindWindow(&quot;ThunderDFrame&quot;, Me.Caption)
    End If
    IStyle = GetWindowLong(Hwnd, GWL_STYLE)
    IStyle = IStyle And Not WS_CAPTION
    SetWindowLong Hwnd, GWL_STYLE, IStyle
    DrawMenuBar Hwnd
    IStyle = GetWindowLong(Hwnd, GWL_EXSTYLE) And Not WS_EX_DLGMODALFRAME
    SetWindowLong Hwnd, GWL_EXSTYLE, IStyle
End Sub
Private Sub UserForm_Click()
    Unload Me
End Sub
代码解析：
窗体初始化时使用API函数去除其标题栏和边框。
第1行到第8行代码，API函数的声明。
第12行到第16行代码，获取窗口句柄。
第17行到第20行代码，去除窗体标题栏。
第21、22行代码，去除窗体边框。
第24行到第26行代码，窗体的单击事件，单击窗体后关闭该窗体。
窗体运行后如图 139 1所示，单击后关闭该窗体。

图 139 1    无标题栏和边框的窗体</code></pre>
<h3 id="技巧140-制作年月选择窗体"><a href="#技巧140-制作年月选择窗体" class="headerlink" title="技巧140     制作年月选择窗体"></a>技巧140     制作年月选择窗体</h3><p>在工作表中需要输入日期时，可以使用日期时间控件（Microsoft Date and Time Picker Control 6.0，简称DTP控件），请参阅技巧116 。但有时只需要输入年份和月份，使用DTP控件选择月份并不方便，此时可以使用文本框结合微调框做一个年月选择窗体供用户输入年份和月份。<br>步骤1，在VBE窗口中单击菜单“插入”→“用户窗体”，将窗体的Caption属性设置为“请选择年月”。<br>步骤2，在窗体上添加一个框架控件和两个命令按纽控件。在框架控件中添加两个文本框控件和两个SpinButton控件，并把命令按纽的Caption属性分别设置为“确定”和“取消”。<br>步骤3，调整好控件位置，双击窗体写入下面的代码。</p>
<pre><code class="vb">Private Sub UserForm_Initialize()
    SpinButton1.Value = Year(Date)
    SpinButton2.Value = Month(Date)
    TextBox1.Text = Year(Date) &amp; &quot;年&quot;
    TextBox2.Text = Month(Date) &amp; &quot;月份&quot;
End Sub
Private Sub SpinButton1_Change()
    TextBox1.Text = SpinButton1.Value &amp; &quot;年&quot;
End Sub
Private Sub SpinButton2_Change()
    With SpinButton2
        Select Case .Value
            Case 1 To 12
                TextBox2.Text = .Value &amp; &quot;月份&quot;
            Case Is &gt; 12
                TextBox1.Text = Left(TextBox1.Text, 4) + 1 &amp; &quot;年&quot;
                .Value = 1
            Case Is &lt; 1
                TextBox1.Text = Left(TextBox1.Text, 4) - 1 &amp; &quot;年&quot;
                .Value = 12
        End Select
    End With
End Sub
Private Sub CommandButton1_Click()
    Sheet1.Range(&quot;A65536&quot;).End(xlUp).Offset(1) = TextBox1.Text &amp; TextBox2.Text
End Sub
Private Sub CommandButton2_Click()
    Unload Me
End Sub
代码解析：
第1行到第6行代码，窗体的初始化事件，在窗体加载时设置文本框和微调框的初始值。
第2行代码，设置微调框1的初始值为当前年份。Year函数返回年份的整数，语法如下：
Year(date)
参数date是必需的，可以是任何能够表示日期的Variant、数值表达式、字符串表达式或它们的组合。
第3行代码，设置微调框2的初始值为当前月份。Mont函数返回值为1到12之间的整数，表示一年中的某月，语法如下：
Month(date)
参数date与Year函数的参数date相同。
第4行代码，设置文本框1显示的文本为当前年份。
第5行代码，设置文本框2显示的文本为当前月份。
第7行到第9行代码，微调框1的Change事件过程。当单击微调框1数值调节钮的向上键或向下键调节年份时，文本框1显示的年份等于调节后的年份。
第10行到第23行代码，微调框2的Change事件过程。当单击微调框2数值调节钮的向上键或向下键调节月份时，文本框2显示的月份等于调节后的月份。如果是一年以内的调节，只调节文本框2显示的月份，否则还需要调节文本框1显示的年份。
第25行代码，“确定”按钮的单击过程，将选择好的年月写入工作表中。
第28行代码，使用Unload 语句卸载窗体。
运行窗体后效果如图 140 1所示。

图 140 1    年月选择窗体</code></pre>
<h3 id="技巧141-自定义窗体中的鼠标指针类型"><a href="#技巧141-自定义窗体中的鼠标指针类型" class="headerlink" title="技巧141     自定义窗体中的鼠标指针类型"></a>技巧141     自定义窗体中的鼠标指针类型</h3><p>使用对象的MousePointer属性可以自定义鼠标掠过窗体控件时的指针类型，如下面的代码所示。</p>
<pre><code class="vb">Private Sub UserForm_Initialize()
    With Me.TextBox1
        .MousePointer = 99
        .MouseIcon = LoadPicture(ThisWorkbook.Path &amp; &quot;\myMouse.ico&quot;)
    End With
End Sub
代码解析：
当用户把鼠标放到窗体的文本框上时，所显示的鼠标指针的类型为自定义图标。
第3行代码设置文本框的MousePointer属性。MousePointer属性指定当用户把鼠标放到特定对象上时，所显示鼠标指针的类型，语法如下：
object.MousePointer [= fmMousePointer]
参数object是必需的，一个有效对象。
参数fmMousePointer是可选的，所需鼠标指针的形状。fmMousePointer的设置值如表格 141 1所示。
常量    值    说明
fmMousePointerDefault    0    标准指针。根据对象来决定指针的图像（默认）
fmMousePointerArrow    1    箭头
fmMousePointerCross    2    十字线指针
fmMousePointerIBeam    3    I 形标
fmMousePointerSizeNESW    6    斜下的双箭头
fmMousePointerSizeNS    7    南北向的双箭头
mMousePointerSizeNWSE    8    斜上的双箭头
fmMousePointerSizeWE    9    东西向的双箭头
fmMousePointerUpArrow    10    向上键
fmMousePointerHourglass    11    沙漏
fmMousePointerNoDrop    12    在被拖动的对象上有 “Not”符号（有一条斜线的圆）。表示是无效的放置目标。
fmMousePointerAppStarting    13    带沙漏的箭头
fmMousePointerHelp    14    带问号的箭头
fmMousePointerSizeAll    15    调整所有尺寸的光标（四向箭头）
fmMousePointerCustom    99    使用由MouseIcon属性指定的图标
表格 141 1    fmMousePointer的设置值
第3行代码将文本框的MousePointer属性设置为99，使用由MouseIcon属性指定的自定义图标。MouseIcon属性为对象指定一个自定义的图标，语法如下：
object.MouseIcon = LoadPicture( pathname )
参数object是必需的，一个有效的对象。
参数pathname是必需的，指定包含自定义图标的文件的路径和文件名。
设置后的鼠标指针的形状如图 141 1所示。

图 141 1    自定义鼠标指针类型</code></pre>
<h3 id="技巧142-调整窗体的显示位置"><a href="#技巧142-调整窗体的显示位置" class="headerlink" title="技巧142     调整窗体的显示位置"></a>技巧142     调整窗体的显示位置</h3><p>用户窗体显示时，默认的位置是窗体所在Excel文件的中央。如果需要调整，可以在窗体加载时对其进行设置，如下面的代码所示。</p>
<pre><code class="vb">Private Sub UserForm_Initialize()
    With Me
        .StartUpPosition = 0
        .Left = 500
        .Top = 300
    End With
End Sub
代码解析：
窗体的初始化事件，在窗体加载时设置其显示位置。
第3行代码，将窗体的StartUpPosition属性设置成手动。
StartUpPosition属性返回或设置一个值，用来指定窗体第一次出现时的位置，设置值如表格 142 1所示。
设置    值    描述
手动    0    没有初始设置指定
所有者中心    1    在 UserForm 所属项目的中央
屏幕中心    2    在整个屏幕的中央
窗口缺省    3    在屏幕的左上角
表格 142 1    StartUpPosition属性设置值
StartUpPosition属性可以在程序中设置，也可以在窗体的属性窗口中设置。
第4、5行代码，设置窗体的Left属性和Top属性，使其加载时显示在屏幕的右下角。
经过设置后的窗体加载时显示位置如图 142 1所示。

图 142 1    调整窗体的显示位置</code></pre>
<h3 id="技巧143-由鼠标确定窗体显示位置"><a href="#技巧143-由鼠标确定窗体显示位置" class="headerlink" title="技巧143     由鼠标确定窗体显示位置"></a>技巧143     由鼠标确定窗体显示位置</h3><p>窗体加载时其显示位置还可以由鼠标的坐标来确定，如下面的代码所示。</p>
<pre><code class="vb">Private Sub CommandButton1_Click()
    Dim ActiveCellX As Integer
    Dim ActiveCellY As Integer
    ActiveCellX = ExecuteExcel4Macro(&quot;GET.CELL(44)&quot;)
    ActiveCellY = ExecuteExcel4Macro(&quot;GET.CELL(43)&quot;)
    With UserForm1
        .Show 0
        .Top = ActiveCellY
        .Left = ActiveCellX
    End With
End Sub
代码解析：
使用ExecuteExcel4Macro方法执行Microsoft Excel 4.0 宏函数取得鼠标的坐标，ExecuteExcel4Macro方法的语法如下：
expression.ExecuteExcel4Macro(String)
expression参数是可选的，返回一个Application对象。
String参数是必需的，一个不带等号的Microsoft Excel 4.0宏语言函数。
第4行代码使用GET.CELL(44) 宏函数取得鼠标的X坐标，第5行代码使用GET.CELL(43) 宏函数取得鼠标的Y坐标。
第6行到第10行代码显示窗体并设置其Top属性和Left属性，调整其显示的位置。</code></pre>
<p>还可以利用工作表SelectionChange事件的Target参数取得鼠标的坐标，如下面的代码所示。</p>
<pre><code class="vb">Private Sub Worksheet_SelectionChange(ByVal Target As Range)
    With UserForm1
        .Show 0
        .Top = Target.Top
        .Left = Target.Left
    End With
End Sub
代码解析：
工作表的SelectionChange事件过程，Target参数代表新选定的区域，返回一个Range对象，在显示窗体时取得其Top和Left属性后设置窗体显示的Top和Left属性。</code></pre>
<h3 id="技巧144-用户窗体的打印"><a href="#技巧144-用户窗体的打印" class="headerlink" title="技巧144     用户窗体的打印"></a>技巧144     用户窗体的打印</h3><p>在使用如图 144 1所示的窗体录入数据时，如果需要把窗体打印出来，可以使用PrintForm方法，如下面的代码所示。</p>
<p>图 144 1    录入窗体</p>
<pre><code class="vb">Private Sub CommandButton7_Click()
    Dim myHeight As Integer
    Application.ScreenUpdating = False
    With UserForm1
        myHeight = .Height
        .DTPicker1.Visible = False
        .Frame1.Visible = False
        .Height = myHeight - 30
        .PrintForm
        .Height = myHeight
        .DTPicker1.Visible = True
        .Frame1.Visible = True
    End With
    Application.ScreenUpdating = True
End Sub
代码解析：
录入窗体中的“打印”按钮的单击代码，使用PrintForm方法打印窗体。
第5行代码使用变量myHeight记录窗体的Height属性值，以便在第10行代码中恢复窗体原有的高度。
第6、7行代码将窗体中的DTP日历控件和功能按钮的Visible属性设置为False，使之隐藏，这样在打印时就不会被打印出来。
第9行代码使用PrintForm方法打印窗体，PrintForm方法将UserForm对象的图象逐位发送到打印机，语法如下：
object.PrintForm
参数object代表对象表达式，其值为“应用于”列表中的对象。如果省略该参数，则把焦点所在的窗体当做object。
第11、12行代码重新显示窗体中的DTP日历控件和功能按钮。
窗体打印后的效果如图 144 2所示。

图 144 2    窗体打印效果</code></pre>
<h3 id="技巧145-使用自定义颜色设置窗体颜色"><a href="#技巧145-使用自定义颜色设置窗体颜色" class="headerlink" title="技巧145     使用自定义颜色设置窗体颜色"></a>技巧145     使用自定义颜色设置窗体颜色</h3><p>在用VBA进行设计时，会发现控件与颜色相关的属性中系统提供可选择的颜色太少。比如窗体的BackColor属性，如果需要把窗体的背景颜色设置为淡蓝色RGB(52,150,203)，可以在窗体初始化过程中对之进行设置，可以实现想要的效果，但是在设计时却不能看到最终效果。<br>其实窗体的BackColor属性（包括ForeColor以及BorderColor等等这些设置颜色的属性）允许输入一个以十六进制表示的长整型数值，这样在设计时就看到效果。<br>首先获取所需要的颜色值并以十六进制表示。还以上面的颜色为例，在立即窗口输入“? Hex(RGB(52,150,203))”可得到一个十六进制数据CB9634，然后把光标定位在窗体属性窗口的BackColor属性值中，删除原来的数值后，输入“&amp;HCB9634&amp;”后按<enter>键，窗体颜色效果立即就出现了，如图 145 1所示。</enter></p>
<p>图 145 1    在窗体设计时显示自定义颜色</p>
<h3 id="技巧146-在窗体中显示图表"><a href="#技巧146-在窗体中显示图表" class="headerlink" title="技巧146     在窗体中显示图表"></a>技巧146     在窗体中显示图表</h3><p>工作表中的图表是不能直接显示在窗体中的，如果需要在窗体上显示图表，除了使用技巧61 介绍的使用ShowWindow属性将工作表中嵌入的图表显示在独立的窗口中，还可以使用以下的方法。</p>
<h4 id="146-1-使用Export方法"><a href="#146-1-使用Export方法" class="headerlink" title="146-1    使用Export方法"></a>146-1    使用Export方法</h4><p>可以把图表以图形格式从工作表中导出，再用窗体上的Image控件把图表显示出来，如下面的代码所示。</p>
<pre><code class="vb">Private Sub UserForm_Initialize()
    Dim Charts As Chart
    Dim cName As String
    Set Charts = Sheets(&quot;Sheet2&quot;).ChartObjects(1).Chart
    cName = ThisWorkbook.Path &amp; &quot;\Temp.gif&quot;
    Charts.Export Filename:=cName, FilterName:=&quot;GIF&quot;
    Image1.Picture = LoadPicture(cName)
End Sub
代码解析：
窗体的初始化事件过程，窗体加载时将工作表中的图表显示在窗体中。
第4行到第6行代码，使用Export方法把Sheet2表中的第一个图表导出到工作簿的同一目录下。
Export方法以图形格式导出图表，语法如下：
expression.Export(Filename, FilterName, Interactive)
参数expression是必需的，一个有效的对象。
参数Filename是必需的，导出的文件的名称。
本例中设置Filename参数时加上了导出路径，将图形导出到同一文件夹下。
参数FilterName是可选的，导出文件的格式。
第7行代码，设置窗体中Image控件的Picture属性为导出文件的完整路径。
Picture 属性指定显示在对象上的位图，语法如下：
object.Picture = LoadPicture( pathname )
参数expression是必需的，一个有效的对象。
参数pathname是必需的，一个图片文件的完整路径。
为了使窗体关闭时删除导出的图片文件，在窗体的QueryClose事件中写入下面的代码。
Private Sub UserForm_QueryClose(Cancel As Integer, CloseMode As Integer)
    Kill ThisWorkbook.Path &amp; &quot;\Temp.gif&quot;
End Sub
代码解析：
窗体关闭时使用Kill方法删除导出的图片文件。Kill方法的语法如下：
Kill pathname
参数Pathname是必需的，用来指定一个文件名的字符串表达式。Pathname参数可以包含目录或文件夹、以及驱动器。
运行窗体，将工作表的图表显示在窗体中，如图 146 2所示。

图 146 1    在窗体上显示图表</code></pre>
<h4 id="146-2-使用API函数"><a href="#146-2-使用API函数" class="headerlink" title="146-2    使用API函数"></a>146-2    使用API函数</h4><p>可以使用API函数把图表从工作表中导出，再用窗体上的Image控件把图表显示出来，如下面的代码所示。</p>
<pre><code class="vb">Private Declare Function CreateStreamOnHGlobal Lib &quot;ole32&quot; (ByVal hGlobal As Long, ByVal fDeleteOnRelease As Long, ppstm As Any) As Long
Private Declare Function OleLoadPicture Lib &quot;olepro32&quot; (pStream As Any, ByVal lSize As Long, ByVal fRunmode As Long, riid As Any, ppvObj As Any) As Long
………代码略详见附件
Private Declare Function GetClipboardFormatName Lib &quot;user32&quot; Alias &quot;GetClipboardFormatNameA&quot; (ByVal wFormat As Long, ByVal lpString As String, ByVal nMaxCount As Long) As Long
Public Function LoadShapePicture(shp As Object) As IPictureDisp
    Dim nClipsize As Long
    Dim hMem As Long
    Dim lpData As Long
    Dim sdata() As Byte
    Dim fmt As Long
    Dim fmtName As String
    Dim iClipBoardFormatNumber As Long
    Dim IID_IPicture(15)
……代码略详见附件
    EmptyClipboard
    CloseClipboard
End Function
Private Sub UserForm_Initialize()
    Image1.Picture = LoadShapePicture(Sheet1.ChartObjects(1))
End Sub
代码解析：
第1行到第12行代码API函数声明。
第13行到第60行代码LoadShapePicture函数，导出工作表中的图表。
第61行到第63行代码窗体的初始化事件过程，窗体加载时将工作表中的图表显示在窗体中，如图 146 2所示。关于Image 控件的Picture属性请参阅技巧146-1。

图 146 2    在窗体上显示图表</code></pre>
<h3 id="技巧147-窗体运行时调整控件大小"><a href="#技巧147-窗体运行时调整控件大小" class="headerlink" title="技巧147     窗体运行时调整控件大小"></a>技巧147     窗体运行时调整控件大小</h3><p>用户窗体中的控件在运行时是不能调整大小的，而在某些情况下需要在窗体运行时调整控件的大小，此时可以利用控件的MouseMove事件。<br>步骤1，在VBE窗口中单击菜单“插入”→“用户窗体”，在窗体中添加两个框架控件，在框架控件中间添加一个Image控件，如图 147 1所示。</p>
<p>图 147 1    添加控件<br>步骤2，Image控件是用来在窗体运行时拖动调整框架控件大小的，所以需要在Image控件的属性窗口将BackStyle属性设置为fmBackStyleTransparent，使控件的背景为透明；将BorderStyle属性设置为fmBorderStyleNone，使控件无可见的边框线；MousePointer属性设置为fmMousePointerSizeWE，当用户把鼠标放到Image控件上时，鼠标指针的类型为东西向的双箭头。关于控件的MousePointer属性请参阅技巧141 中的表格 141 1。<br>步骤3，在窗体中调整好控件的位置后双击Image控件写入下面的代码：</p>
<pre><code class="vb">Dim Abscissa As Single
Private Sub Image1_MouseDown(ByVal Button As Integer, ByVal Shift As Integer, ByVal x As Single, ByVal y As Single)
    Abscissa = x
End Sub
Private Sub Image1_MouseMove(ByVal Button As Integer, ByVal Shift As Integer, ByVal x As Single, ByVal y As Single)
    If Button = 1 Then
        If Abscissa - x &gt; Frame1.Width Or x &gt; Frame2.Width Then Exit Sub
        Frame1.Width = Frame1.Width - Abscissa + x
        Image1.Left = Image1.Left - Abscissa + x
        Frame2.Left = Frame2.Left - Abscissa + x
        Frame2.Width = Frame2.Width + Abscissa - x
    End If
End Sub
代码解析：
第2行到第4行代码，Image控件的MouseDown事件过程，用户按下鼠标按键时发生，语法如下：
Private Sub object_MouseDown( ByVal Button As fmButton, ByVal Shift As fmShiftState, ByVal X As Single, ByVal Y As Single)
其中参数x是可选的，控件位置的横坐标，以磅为单位，从左边开始测量。
第3行代码将控件的横坐标赋给变量Abscissa。
第5行到第12行代码，Image控件的MouseMove事件过程，用户移动鼠标时该事件发生，语法如下：
Private Sub object_MouseMove( ByVal Button As fmButton, ByVal Shift As fmShiftState, ByVal X As Single, ByVal Y As Single)
其中参数Button是必需的，标识鼠标按键状态的整数值，其设置值如表格 147 1所示。
值    说明    值    说明
0    按键未被按下    4    按下中键
1    按下左键    5    同时按下左键和中键
2    按下右键    6    同时按下中键和右键
3    同时按下左键和右键    7    三个按键全都按下
表格 147 1    Button参数的设置值
参数x是可选的，控件位置的水平坐标，以磅为单位，从左边开始测量。
在MouseMove事件过程中，当用户在窗体上按下左键移动鼠标时，调整两个框架控件的Width属性和框架2的Left属性，使其达到窗体运行时可以进行拖动调整大小的效果。
当鼠标指针在对象上移动时，MouseMove事件是连续发生的，只要鼠标位于对象的边界之内，对象就会不断的识别MouseMove事件，所以框架控件可以连续的进行拖动调整大小。
运行窗体的，选择两个框架控件的中间位置，当鼠标指针变成东西向的双箭头时按下鼠标左键拖动可以进行拖动调整框架控件的大小，如图 147 2所示。

图 147 2    窗体运行时调整控件大小</code></pre>
<h3 id="技巧148-在用户窗体上添加菜单"><a href="#技巧148-在用户窗体上添加菜单" class="headerlink" title="技巧148     在用户窗体上添加菜单"></a>技巧148     在用户窗体上添加菜单</h3><p>在VBA中，用户窗体上是没有菜单的，为了使用方便，我们可以使用API函数在用户窗体上添加菜单，示例代码如下：</p>
<pre><code class="vb">Private Declare Function FindWindow Lib &quot;user32&quot; Alias &quot;FindWindowA&quot; (ByVal lpClassName As String, ByVal lpWindowName As String) As Long
Private Declare Function SetMenu Lib &quot;user32&quot; (ByVal hwnd As Long, ByVal hMenu As Long) As Long
Private Declare Function CreateMenu Lib &quot;user32&quot; () As Long
Private Declare Function AppendMenu Lib &quot;user32&quot; Alias &quot;AppendMenuA&quot; (ByVal hMenu As Long, ByVal wFlags As Long, ByVal wIDNewItem As Long, ByVal lpNewItem As Any) As Long
Private Declare Function DestroyMenu Lib &quot;user32&quot; (ByVal hMenu As Long) As Long
Private Declare Function CreatePopupMenu Lib &quot;user32&quot; () As Long
Private Declare Function SetWindowLong Lib &quot;user32&quot; Alias &quot;SetWindowLongA&quot; (ByVal hwnd As Long, ByVal nIndex As Long, ByVal dwNewLong As Long) As Long
Private Declare Function GetWindowLong Lib &quot;user32&quot; Alias &quot;GetWindowLongA&quot; (ByVal hwnd As Long, ByVal nIndex As Long) As Long
Private Const GWL_WNDPROC = (-4)
Private Const MF_STRING = &amp;H0&amp;
Private Const MF_POPUP = &amp;H10&amp;
Private Const MF_SEPARATOR = &amp;H800&amp;
Dim MenuWnd As Long, Dump As Long, PopupMenuID As Long, PopupMenuWnd As Long, MenuID As Long
Private Sub UserForm_Initialize()
    If Val(Application.Version) &lt; 9 Then
        hwnd = FindWindow(&quot;ThunderXFrame&quot;, Me.Caption)
    Else
        hwnd = FindWindow(&quot;ThunderDFrame&quot;, Me.Caption)
    End If
    MenuWnd = CreateMenu()
    PopupMenuID = CreatePopupMenu()
    Dump = AppendMenu(MenuWnd, MF_STRING + MF_POPUP, PopupMenuID, &quot;系统设置(&amp;X)&quot;)
    Dump = AppendMenu(PopupMenuID, MF_STRING, 100, &quot;保存(&amp;S)...&quot;)
    Dump = AppendMenu(PopupMenuID, MF_STRING, 101, &quot;备份(&amp;E)&quot;)
    Dump = AppendMenu(PopupMenuID, MF_STRING, 102, &quot;退出(&amp;X)&quot;)
    PopupMenuID = CreatePopupMenu()
    Dump = AppendMenu(MenuWnd, MF_STRING + MF_POPUP, PopupMenuID, &quot;会计凭证(&amp;P)&quot;)
    Dump = AppendMenu(PopupMenuID, MF_STRING, 110, &quot;录入(&amp;L)&quot;)
    Dump = AppendMenu(PopupMenuID, MF_STRING, 111, &quot;审核(&amp;C)&quot;)
    PopupMenuID = CreatePopupMenu()
    Dump = AppendMenu(MenuWnd, MF_STRING + MF_POPUP, PopupMenuID, &quot;会计账簿(&amp;Z)&quot;)
    Dump = AppendMenu(PopupMenuID, MF_STRING, 112, &quot;记账(&amp;T)&quot;)
    Dump = AppendMenu(PopupMenuID, MF_STRING, 113, &quot;结账(&amp;J)&quot;)
    PopupMenuID = CreatePopupMenu()
    Dump = AppendMenu(MenuWnd, MF_STRING + MF_POPUP, PopupMenuID, &quot;会计报表(&amp;B)&quot;)
    Dump = AppendMenu(PopupMenuID, MF_STRING, 114, &quot;资产负债表(&amp;F)&quot;)
    Dump = AppendMenu(PopupMenuID, MF_STRING, 115, &quot;损益表(&amp;Y)&quot;)
    Dump = SetMenu(hwnd, MenuWnd)
    PreWinProc = GetWindowLong(hwnd, GWL_WNDPROC)
    SetWindowLong hwnd, GWL_WNDPROC, AddressOf MsgProcess
End Sub
Private Sub UserForm_Terminate()
    DestroyMenu MenuWnd
    DestroyMenu PopupMenuID
    DestroyMenu PopupMenuWnd
    SetWindowLong hwnd, GWL_WNDPROC, PreWinProc
End Sub
代码解析：
第1行到第13行代码，API函数声明。
第14行到第41代码，用户窗体的Initialize事件过程，在窗体显示时使用API函数在窗体上添加菜单。其中第22行代码添加第一个“系统设置”菜单，第23、24、25行代码在“系统设置”菜单中添加三个子菜单，第26行代码往下继续添加其他菜单。
第40行代码，为窗体中添加的菜单指定所执行的过程名称为“MsgProcess”函数过程。
第42行到第47行代码，用户窗体的Terminate事件过程，将所有引用对象的变量设置成Nothing，从而删除对象的所有引用。</code></pre>
<p>为了能够使用窗体中添加的菜单，需要在模块中写入下面的代码：</p>
<pre><code class="vb">Public PreWinProc As Long, hwnd As Long
Public Declare Function CheckMenuRadioItem Lib &quot;user32&quot; (ByVal hMenu As Long, ByVal un1 As Long, ByVal un2 As Long, ByVal un3 As Long, ByVal un4 As Long) As Long
Public Declare Function CheckMenuItem Lib &quot;user32&quot; (ByVal hMenu As Long, ByVal wIDCheckItem As Long, ByVal wCheck As Long) As Long
Public Declare Function EnableMenuItem Lib &quot;user32&quot; (ByVal hMenu As Long, ByVal wIDEnableItem As Long, ByVal wEnable As Long) As Long
Public Const MF_UNCHECKED = &amp;H0&amp;
Public Const MF_CHECKED = &amp;H8&amp;
Public Const MF_DISABLED = &amp;H2&amp;
Public Const MF_GRAYED = &amp;H1&amp;
Public Const MF_ENABLED = &amp;H0&amp;
Private Declare Function CallWindowProc Lib &quot;user32&quot; Alias &quot;CallWindowProcA&quot; (ByVal lpPrevWndFunc As Long, ByVal hwnd As Long, ByVal Msg As Long, ByVal wParam As Long, ByVal lParam As Long) As Long
Private Declare Function GetMenu Lib &quot;user32&quot; (ByVal hwnd As Long) As Long
Private Declare Function GetSubMenu Lib &quot;user32&quot; (ByVal hMenu As Long, ByVal nPos As Long) As Long
Private Const MF_BYCOMMAND = &amp;H0&amp;
Public Function MsgProcess(ByVal hwnd As Long, ByVal Msg As Long, ByVal wParam As Long, ByVal lParam As Long) As Long
    Dim SubMenu_hWnd As Long
    Select Case wParam
        Case 100
            MsgBox &quot;你选择的是&quot;&quot;保存&quot;&quot;按钮!&quot;
        Case 101
            MsgBox &quot;你选择的是&quot;&quot;备份&quot;&quot;按钮!&quot;
        Case 102
            Unload UserForm1
        Case 110
            MsgBox &quot;你选择的是&quot;&quot;录入&quot;&quot;按钮!&quot;
        Case 111
            MsgBox &quot;你选择的是&quot;&quot;审核&quot;&quot;按钮!&quot;
        Case 112
            MsgBox &quot;你选择的是&quot;&quot;记账&quot;&quot;按钮!&quot;
        Case 113
            MsgBox &quot;你选择的是&quot;&quot;结账&quot;&quot;按钮!&quot;
        Case 114
            MsgBox &quot;你选择的是&quot;&quot;资产负债表&quot;&quot;按钮!&quot;
        Case 115
            MsgBox &quot;你选择的是&quot;&quot;损益表&quot;&quot;按钮!&quot;
        Case Else
            MsgProcess = CallWindowProc(PreWinProc, hwnd, Msg, wParam, lParam)
    End Select
End Function
代码解析：
第1行到第13行代码，API函数声明。
第14行到第36行代码，MsgProcess函数过程，根据参数wParam的值为窗体中的菜单指定所执行的操作，为了演示方便只使用MsgBox函数显示一个消息框，在实际应用中可以为菜单写入代码或指定过程名称。
运行窗体后在窗体上添加菜单，如图 148 1所示。

图 148 1    用户窗体上添加菜单</code></pre>
<h3 id="技巧149-在用户窗体上添加工具栏"><a href="#技巧149-在用户窗体上添加工具栏" class="headerlink" title="技巧149     在用户窗体上添加工具栏"></a>技巧149     在用户窗体上添加工具栏</h3><p>在技巧148 中我们在用户窗体上使用API函数添加了菜单，还可以在用户窗体上继续添加工具栏用以显示一列下拉菜单的位图按钮，单击一个工具栏按钮等于选择一个菜单命令，以提供对常用功能和命令的快速访问。<br>在用户窗体上添加工具栏可以使用Toolbar控件，在设计模式下右键单击“工具箱”，在显示的右键菜单中选择“附加控件”，在显示的对话框中选择“Microsoft Toolbar Control， veision 6.0”控件，在用户窗体上添加一个Toolbar控件。如图 149 1所示。</p>
<p>图 149 1    选择Toolbar控件<br>因为需要在Toolbar控件按钮中使用图标，所以还需要在用户窗体中添加一个ImageList控件保存所需要的图像文件，在ImageList控件的属性页中插入6张图片，如图 149 2所示。</p>
<p>图 149 2    ImageList控件插入图片<br>用户窗体上添加了Toolbar控件后还需要设置其属性和添加按钮控件，可以在Toolbar控件的属性页中进行设置和添加，如图 149 3所示。</p>
<p>图 149 3    设置Toolbar控件属性<br>还可以在代码运行时对其进行设置和添加按钮，双击用户窗体写入下面的代码：</p>
<pre><code class="vb">Private Sub UserForm_Initialize()
    ……使用API函数添加菜单代码略，详见附件
    Dim arr As Variant
    Dim i As Byte
    arr = Array(&quot; 录入 &quot;, &quot; 审核&quot;, &quot; 记账 &quot;, &quot; 结账 &quot;, &quot;负债表&quot;, &quot;损益表&quot;)
    With Toolbar1
        .ImageList = ImageList1
        .Appearance = ccFlat
        .BorderStyle = ccNone
        .TextAlignment = tbrTextAlignBottom
        With .Buttons
            .Add(1, , &quot;&quot;).Style = tbrPlaceholder
            For i = 0 To UBound(arr)
                .Add(i + 2, , , , i + 1).Caption = arr(i)
            Next
        End With
    End With
End Sub
代码解析：
第5行代码数组arr用来保存按钮的标题文字。
第7行代码建立Toolbar控件和ImageList控件的关联。
第8行代码设置Toolbar控件的外观效果，Appearance属性获得或设置控件的外观效果，设置值如表格 149 1所示。
设置值    值    说明
ccFlat    0    平面
cc3D    1    立体
表格 149 1    Appearance属性值
第9行代码设置Toolbar控件的边界样式，BorderStyle属性获得或设置边界样式，设置值如表格 149 2所示。
设置值    值    说明
ccNone    0    无边界线
ccFixedSingle    1    固定单线框
表格 149 2    BorderStyle属性值
第10行代码设置按钮文本显示在按钮图像下方，TextAlignment属性获得或设置一个值，决定按钮文本显示在按钮图像下方还是右侧，设置值如表格 149 3所示。
设置值    值    说明
tbrTextAlignBottom    0    下方
tbrTextAlignRight    1    右侧
表格 149 3    TextAlignment属性值
第11行到第15行代码在Toolbar控件中添加按钮，添加按钮需要在Buttons的集合对象中使用Add方法，语法如下：
object.Buttons.Add(index, key, caption, style, image)
参数object是必需的，代表Toolbar对象。
参数index是可选的，指定新增按钮的索引值，该索引值决定了按钮在Toolbar控件中的位置。如果省略index参数新增按钮添加到Butons集合的最后。
参数key是可选的，指定新增按钮的关键字。
参数caption是可选的，指定新增按钮的标题文本。
参数style是可选的，指定新增按钮的样式，设置值如表格 149 1所示。
属性值    值    说明
tbrDefault    0    一般按钮
tbrCheck    1    开关按钮
tbrButtonGroup    2    编组按钮
tbrSeparator    3    分隔按钮
tbrPlaceholder    4    占位按钮
表格 149 4    Style参数值
参数image是可选的，指定新增按钮载入的图像，图像必须是与该Toolbar控件相关联的ImageList控件图像库中的一个。image参数可以是一个整数，对应ImageList图像库中某个图片的Index值也可以是一个字符串，对应图片的关键字Key。
第12行代码代码首先在Toolbar控件中添加占位按钮，设置其style属性为tbrPlaceholder，添加的就是占位按钮，在Toolbar控件中是不显示的，仅仅起到占位的作用。
第14行代码在占位按钮后继续添加6个按钮，设置其标题文本和图像在ImageList控件中的编号。</code></pre>
<p>为了响应Toolbar控件，双击Toolbar控件写入下面的代码：</p>
<pre><code class="vb">Private Sub Toolbar1_ButtonClick(ByVal Button As MSComctlLib.Button)
    Select Case Button.Index
        Case 2
            MsgBox &quot;录入&quot;
        Case 3
            MsgBox &quot;审核&quot;
        Case 4
            MsgBox &quot;记账&quot;
        Case 5
            MsgBox &quot;结账&quot;
        Case 6
            MsgBox &quot;资产负债表&quot;
        Case 7
            MsgBox &quot;损益表&quot;
    End Select
End Sub
代码解析：
Toolbar控件的ButtonClick事件，在单击Toolbar控件的按钮时发生，参数Button代表单击的按钮。为了演示方便，根据其Index属性值使用消息框显示按钮标题文本，在实际应用中可以为菜单写入代码或指定过程名称。
运行窗体后在窗体上添加工具栏，如图 149 4所示。

图 149 4    在用户窗体上添加工具栏</code></pre>
<h3 id="技巧150-使用代码添加窗体及控件"><a href="#技巧150-使用代码添加窗体及控件" class="headerlink" title="技巧150     使用代码添加窗体及控件"></a>技巧150     使用代码添加窗体及控件</h3><p>VBA中的用户窗体为用户提供了可视化的操作界面，在用户窗体中一般都包含控件以便与用户进行交互。我们通常是在VBE中使用菜单“插入”→“用户窗体”来创建用户窗体，然后拖动工具箱中的控件到用户窗体中，也可以使用代码来添加用户窗体及其控件，代码如下：</p>
<pre><code class="vb">Private Sub CommandButton1_Click()
    Dim myForm As VBComponent
    Dim myTextBox As Control
    Dim myButton As Control
    Dim i As Integer
    Set myForm = ThisWorkbook.VBProject.VBComponents.Add(vbext_ct_MSForm)
    With myForm
        .Properties(&quot;Name&quot;) = &quot;Formtest&quot;
        .Properties(&quot;Caption&quot;) = &quot;演示窗体&quot;
        .Properties(&quot;Height&quot;) = &quot;180&quot;
        .Properties(&quot;Width&quot;) = &quot;240&quot;
        Set myTextBox = .Designer.Controls.Add(&quot;Forms.CommandButton.1&quot;)
        With myTextBox
            .Name = &quot;myTextBox&quot;
            .Caption = &quot;新建文本框&quot;
            .Top = 40
            .Left = 138
            .Height = 20
            .Width = 70
        End With
        Set myButton = .Designer.Controls.Add(&quot;Forms.CommandButton.1&quot;)
        With myButton
            .Name = &quot;myButton&quot;
            .Caption = &quot;删除文本框&quot;
            .Top = 70
            .Left = 138
            .Height = 20
            .Width = 70
        End With
        With .CodeModule
            i = .CreateEventProc(&quot;Click&quot;, &quot;myTextBox&quot;)
            .ReplaceLine i + 1, Space(4) &amp; &quot;Dim myTextBox As Control&quot; &amp; Chr(10) &amp; Space(4) &amp; &quot;Dim i As Integer&quot; &amp; Chr(10) &amp; Space(4) &amp; &quot;Dim k As Integer&quot; _
                &amp; Chr(10) &amp; Space(4) &amp; &quot;k = 10&quot; &amp; Chr(10) &amp; Space(4) &amp; &quot;For i = 1 To 5&quot; &amp; Chr(10) &amp; Space(8) &amp; &quot;Set myTextBox = Me.Controls.Add(bstrprogid:=&quot;&quot;Forms.TextBox.1&quot;&quot;)&quot; _
                &amp; Chr(10) &amp; Space(8) &amp; &quot;With myTextBox&quot; &amp; Chr(10) &amp; Space(12) &amp; &quot;.Name = &quot;&quot;myTextBox&quot;&quot; &amp; i&quot; &amp; Chr(10) &amp; Space(12) &amp; &quot;.Left = 20&quot; _
                &amp; Chr(10) &amp; Space(12) &amp; &quot;.Top = k&quot; &amp; Chr(10) &amp; Space(12) &amp; &quot;.Height = 18&quot; &amp; Chr(10) &amp; Space(12) &amp; &quot;.Width = 80&quot; _
                &amp; Chr(10) &amp; Space(12) &amp; &quot;k = .Top + 28&quot; &amp; Chr(10) &amp; Space(8) &amp; &quot;End With&quot; &amp; Chr(10) &amp; Space(4) &amp; &quot;Next&quot;
            i = .CreateEventProc(&quot;Click&quot;, &quot;myButton&quot;)
            .ReplaceLine i + 1, Space(4) &amp; &quot;Dim i As Integer&quot; &amp; Chr(10) &amp; Space(4) &amp; &quot;On Error Resume Next&quot; &amp; Chr(10) &amp; Space(4) &amp; &quot;For i = 1 To 5&quot; &amp; Chr(10) &amp; Space(8) &amp; &quot;Formtest.Controls.Remove &quot;&quot;myTextBox&quot;&quot; &amp; i&quot; &amp; Chr(10) &amp; Space(4) &amp; &quot;Next&quot;
        End With
    End With
End Sub
代码解析：
使用代码添加一个用户窗体及其两个按钮控件，并为按钮控件添加单击事件及其相应的代码。
第2行到第5行代码声明变量类型，如果发生错误请在菜单“工具”→“引用”中引用“Microsoft Visual Basic for Applications Extensibility 5.3”，如图 150 1所示。

图 150 1    引用
第6行代码，使用Add方法添加用户窗体，应用于VBComponents集合的Add方法将一个对象添加到集合，语法如下：
object.Add(component)
参数object是必需的，一个有效的对象名。
参数component是必需的，对于VBComponents集合，则为表示类模块、窗体、标准模块的列举常数，可以为表格 150 1所示的常量之一。
常量    值    描述
vbext_ct_ClassModule    2    将一个类模块添加到集合
Vbext_ct_MSForm    3    将窗体添加到集合
vbext_ct_StdModule    1    将标准模块添加到集合
表格 150 1    component参数值
第8行到第11行代码，使用VBComponent对象的Properties属性设置用户窗体的相关属性。
第12行代码，使用Add方法添加在用户窗体上添加一个按钮控件。VBComponent对象的Designer属性返回一个设计器对象，其Controls属性返回Controls集合，代表用户窗体中所有的控件。应用于Controls集合对象的Add方法在用户窗体中添加控件，语法如下：
object.Add( ProgID [, Name [, Visible]])
参数object是必需的，一个有效的对象名。
参数ProgID是必需的，程序设计标识符。是用于标识对象类的、没有空格的文本串。关于程序设计标识符请参阅技巧119-3中的表格 119 1。
参数Name是可选的，指定被添加的对象的名称。
参数Visible是可选的，若对象为可见的为True，若对象为隐藏的则为False。默认值为True。
第13行到第20行代码设置添加的按钮控件的相关属性。
第21行到第29行代码继续添加一个按钮控件并设置其相关属性。
第30行到第40行代码为添加的按钮控件创建单击事件过程并在其单击事件中添加代码。
其中第30、39行代码使用CreateEventProc方法为按钮控件创建单击事件过程，应用于CodeModule对象的CreateEventProc方法创建一个事件过程，语法如下：
object.CreateEventProc(eventname, objectname) As Long
参数object是必需的，一个有效的对象名。
参数eventname是必需的，字符串表达式，用来指定欲添加到模块的事件名称。
参数objectname是必需的，字符串表达式，用来指定事件源的对象名称。
CreateEventProc方法可返回事件过程的开始行，所以使用变量i保存开始行。
第32行代码使用ReplaceLine方法在按钮控件的单击事件过程中添加代码，应用于CodeModule对象的ReplaceLine方法用特定的代码代替原代码，语法如下：
object.ReplaceLine(line, code)
参数object是必需的，一个有效的对象名。
参数line是必需的，用来指定所要代替的行。
参数code是必需的，用来指定要插入的代码。
在使用ReplaceLine方法时将line参数设置为变量i加1，也就是在单击事件过程的第2行开始添加代码，在添加代码时使用Space函数插入空格，使用Chr函数进行换行。</code></pre>
<p>运行CommandButton1_Click过程，添加一个用户窗体及两个按钮控件，并在用户窗体中添加以下的代码：</p>
<pre><code class="vb">Private Sub myTextBox_Click()
    Dim myTextBox As Control
    Dim i As Integer
    Dim k As Integer
    k = 10
    For i = 1 To 5
        Set myTextBox = Me.Controls.Add(&quot;Forms.TextBox.1&quot;)
        With myTextBox
            .Name = &quot;myTextBox&quot; &amp; i
            .Left = 20
            .Top = k
            .Height = 18
            .Width = 80
            k = .Top + 28
        End With
    Next
End Sub
Private Sub myButton_Click()
    Dim i As Integer
    On Error Resume Next
    For i = 1 To 5
        Formtest.Controls.Remove &quot;myTextBox&quot; &amp; i
    Next
End Sub
代码解析：
第1行到第17行代码，用户窗体中“添加文本框”按钮的单击事件，在用户窗体运行时使用Add方法在用户窗体中添加5个文本框控件并设置其相关属性。
第18行到第24行代码，用户窗体中“删除文本框”按钮的单击事件，在用户窗体运行时使用Remove方法删除文本框控件。应用于Controls集合的Remove方法从集合中删除一个成员，或者从框架、页面或窗体中删除一个控件，语法如下：
object.Remove( collectionindex)
参数object是必需的，一个有效的对象名。
参数collectionindex是必需的，成员在集合内的位置或索引。
注意 Remove方法只能删除在运行时间添加的控件，如果想删除在设计时间添加的控件则会出错。
运行CommandButton1_Click过程添加的用户窗体如图 150 2所示。

图 150 2    添加的用户窗体
单击“新建文本框”按钮在用户窗体中添加5个文本框控件，如图 150 3所示，而单击“删除文本框”按钮则删除用户窗体中添加的文本框控件。</code></pre>
<h3 id="技巧151-用户窗体的全屏显示"><a href="#技巧151-用户窗体的全屏显示" class="headerlink" title="技巧151     用户窗体的全屏显示"></a>技巧151     用户窗体的全屏显示</h3><p>在需要用户窗体全屏显示时，可以将窗体的Height属性和Width属性设置为一定的数值，使之显示时和显示器一样大小。<br>使用这种方法虽然可以达到全屏显示的要求，但是如果换台显示器不一样的电脑时，此种方法便会失效。为了使用户窗体达到真正的全屏显示，可以使用以下的方法。</p>
<h4 id="151-1-设置用户窗体为应用程序的大小"><a href="#151-1-设置用户窗体为应用程序的大小" class="headerlink" title="151-1    设置用户窗体为应用程序的大小"></a>151-1    设置用户窗体为应用程序的大小</h4><p>将用户窗体的高度和宽度设置为应用程序的高度和宽度，如下面的代码所示。</p>
<pre><code class="vb">Private Sub UserForm_Initialize()
    Application.WindowState = xlMaximized
    With Me
        .Width = Application.Width
        .Height = Application.Height
        .Left = Application.Left
        .Top = Application.Top
    End With
End Sub
代码解析：
用户窗体初始化时，将高度和宽度设置成与Excel应用程序窗口一样。
第2行代码，将Excel应用程序的WindowState属性设置为xlMaximized，使Excel应用程序最大化显示。
不使用对象识别符时Application属性返回一个Application对象，代表Excel应用程序。WindowState属性返回或设置窗口的状态，可以为表格 151 1所示的XlWindowState常量之一。
常量    值    说明
xlMaximized    -4137    最大化
xlNormal    -4143    不变化
xlMinimized    -4140    最小化
表格 151 1    XlWindowState常量
第3行到第8行代码将用户窗体的Width属性、Height属性设置为Excel应用程序的高度和宽度，Width属性、Height属性以磅为单位返回或设置对象的高度和宽度。将用户窗体的Left属性、Top属性设置为和最大化后的Excel应用程序的一样。</code></pre>
<h4 id="151-2-根据屏幕分辨率进行设置"><a href="#151-2-根据屏幕分辨率进行设置" class="headerlink" title="151-2    根据屏幕分辨率进行设置"></a>151-2    根据屏幕分辨率进行设置</h4><p>根据屏幕分辨率的大小自动调整用户窗体的高度和宽度，如下面的代码所示。</p>
<pre><code class="vb">Private Declare Function GetSystemMetrics Lib &quot;user32&quot; (ByVal nIndex As Long) As Long
Const SM_CXSCREEN As Long = 0
Const SM_CYSCREEN As Long = 1
Private Sub UserForm_Initialize()
    With Me
        .Height = GetSystemMetrics(SM_CYSCREEN) * 0.72 
        .Width = GetSystemMetrics(SM_CXSCREEN) * 0.75
        .Left = 0
        .Top = 0
    End With
End Sub
代码解析：
用户窗体初始化时根据屏幕分辨率的大小自动调整用户窗体的高度和宽度。
第1行到第3行代码，API函数声明。
第6行代码设置用户窗体的高度，屏幕分辨率的Y坐标值乘以0.72将其换算成以磅为单位的数值。
第7行代码设置用户窗体的宽度，屏幕分辨率的X坐标值乘以0.75将其换算成以磅为单位的数值。
经过以上两种方法的设置，用户窗体显示时始终以全屏显示。</code></pre>
<h3 id="技巧152-在用户窗体上添加状态栏"><a href="#技巧152-在用户窗体上添加状态栏" class="headerlink" title="技巧152     在用户窗体上添加状态栏"></a>技巧152     在用户窗体上添加状态栏</h3><p>在技巧148 、技巧149 中我们在用户窗体上添加了菜单和工具栏，为了使窗体更像正规的软件，还需要在用户窗体的底部添加一个状态栏，用于显示程序的各种状态信息。<br>在用户窗体上添加状态栏使用StatusBar控件，StatusBar控件用于设计窗体状态栏，状态栏由一组连续的窗格（最多16个）对象组合而成，用于显示应用程序当前的工作状态，其位置通常在应用程序窗体的底部。在设计模式下右键单击“工具箱”，在显示的右键菜单中选择“附加控件”，在显示的对话框中选择“Microsoft StatusBar Control， veision 6.0”控件如图 152 1所示，拖动后就可以在用户窗体上添加一个StatusBar控件。</p>
<p>在用户窗体上添加了StatusBar控件后还需要添加窗格，可以在StatusBar控件的属性页中进行设置和添加，在StatusBar控件的属性窗口中选择“自定义”按钮，在显示的属性页中设置属性和添加窗格，如图 152 2所示。也可以在代码运行时对其进行属性设置和添加窗格，双击用户窗体写入下面的代码：</p>
<pre><code class="vb">Private Sub UserForm_Initialize()
    ……使用API函数添加菜单代码略，详见附件。
    Dim arr As Variant
    Dim i As Byte
    ……使用Toolbar控件添加工具栏代码略，详见附件。
    arr = Array(0, 6, 5)
    With StatusBar1
        .Width = Me.Width - 10
        For i = 1 To 3
            .Panels.Add(i, , &quot;&quot;).Style = arr(i - 1)
        Next
        .Panels(1).Text = &quot;准备就绪!&quot;
        .Panels(2).Width = 60
        .Panels(3).Width = 75
        .Panels(1).Width = Me.Width - .Panels(1).Width - .Panels(2).Width
        .Panels(3).Picture = LoadPicture(ThisWorkbook.Path &amp; &quot;\123.BMP&quot;)
        For i = 0 To 2
            .Panels(i + 1).Alignment = i
        Next
    End With
End Sub
代码解析：
第8行代码设置StatusBar控件的宽度比用户窗体略小一点。
第9行到第11行代码在StatusBar控件中添加三个窗格并指定窗格的样式。添加窗格需要在Panels集合对象中使用Add方法，语法如下：
object.Panels.Add(index, key, text, style, picture)
参数object是必需的，代表StatusBar对象。
参数index是可选的，指定新增窗格的索引值，该索引值决定了窗格在StatusBar控件中的位置。如果省略index参数新增窗格添加到Panels集合的最后。
参数key是可选的，指定新增窗格的关键字。
参数text是可选的，指定新增窗格中显示的文本。
参数style是可选的，指定新增窗格的样式，设置值如表格 152 1所示。
属性值    值    说明
sbrText    0    显示文本与图形
sbrCaps    1    显示大小写状态
sbrNum    2    显示numlock键状态
sbrIns    3    显示Insert状态
sbrScrl    4    显示Scroll键状态
sbrtime    5    按系统格式显示时间
sbrDate    6    按系统格式显示日期
表格 152 1    Style参数值
参数picture是可选的，指定新增窗格载入的图像。
第12行代码设置第一个窗格显示的文本。
第13行到第15行代码设置三个窗格的宽度。
第16行代码为第三个窗格加载指定的图像。
第17行到第19行代码设置三个窗格中文本的对齐方式。Panels对象的Alignment属性返回或设置窗格中文本的对齐方式，设置值如表格 152 2所示。
属性值    值    说明
sbrLeft    0    文本左对齐
sbrCenter    1    文本居中对齐
sbrRight    3    文本右对齐
表格 152 2    Alignment属性值</code></pre>
<p>在示例中使用StatusBar控件的第一个窗格在用户窗体的文本框输入时显示所输入的内容，需要在文本框中写入下面的代码。</p>
<pre><code class="vb">Private Sub TextBox1_Change()
    StatusBar1.Panels(1).Text = &quot;正在录入:&quot; &amp; TextBox1.Text
End Sub
代码解析：
文本框的Change事件过程，将文本框中输入的内容显示在StatusBar控件的第一个窗格中。
运行窗体后在窗体上添加状态栏，如图 152 3所示</code></pre>

    </div>
</article>
                </main>
                <aside class="aside">
                    <section class="aside-section">
                        
    <h1>Categories</h1>

    <ul class="category-list"><li class="category-list-item"><a class="category-list-link" href="/categories/生の术/">生の术</a></li><li class="category-list-item"><a class="category-list-link" href="/categories/生の迹/">生の迹</a></li><li class="category-list-item"><a class="category-list-link" href="/categories/生の道/">生の道</a></li></ul>

                    </section>
                    <section class="aside-section">
                        
    <h1>Archives</h1>

    <ul class="archive-list"><li class="archive-list-item"><a class="archive-list-link" href="/archiveshl/2018/">2018</a></li></ul>


                    </section>
                    <section class="aside-section tag">
                        
    <h1>Tags</h1>

    <ul class="tag-list"><li class="tag-list-item"><a class="tag-list-link" href="/tags/Markdown/">Markdown</a></li><li class="tag-list-item"><a class="tag-list-link" href="/tags/Oracle/">Oracle</a></li><li class="tag-list-item"><a class="tag-list-link" href="/tags/VBA/">VBA</a></li><li class="tag-list-item"><a class="tag-list-link" href="/tags/数据科学/">数据科学</a></li><li class="tag-list-item"><a class="tag-list-link" href="/tags/爬虫/">爬虫</a></li><li class="tag-list-item"><a class="tag-list-link" href="/tags/自言语/">自言语</a></li></ul>

                    </section>
                </aside>
        </div>
    </body>

</html>